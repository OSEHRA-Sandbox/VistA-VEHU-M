SDHLAPT2 ;MS/PB - VISTA SCHEDULING RPCS ;Nov 14, 2014
 ;;5.3;Scheduling;**704**;Nov 14, 2018;Build 64
 ;
 Q
PROVSITE(X) ;
 N PROVAPT
 M PROVAPT=X
 Q:'$D(PROVAPT)
 N SDECY,SDECSTART,SDECEND,DFN,SDECRES,SDECLEN,SDECNOTE,SDECATID,SDECCR,SDMRTC,SDDDT,SDREQBY,SDLAB,PROVIEN,SDID,SDAPTYP,SDSVCP,SDSVCPR,SDCL,SDEKG,SDXRAY,APPTYPE,EESTAT,OVB,SDPARENT,SDEL
 S (SDECY,SDECSTART,SDECEND,DFN,SDECRES,SDECLEN,SDECNOTE,SDECATID,SDECCR,SDMRTC,SDDDT,SDREQBY,SDLAB,PROVIEN,SDID,SDAPTYP,SDSVCP,SDSVCPR,SDCL,SDEKG,SDXRAY,APPTYPE,EESTAT,OVB,SDPARENT,SDEL)=""
 S XX=0 F  S XX=$O(PROVAPT(XX)) Q:XX'>0  D
 . N STM,SEG
 . I $P(SEG,"|")="SCH" D PARSESEG^SDHL7APU(SEG,.SCH,.HL) D
 . . S TM=$G(SCH(11,1,4)),STM=$P(TM,":",1,2)_":00Z",SDECLEN=$G(SEG,9)
 . . S SDECATID=$G(SCH,6),SDECCR=$G(SEG(6,1,2))
 . . S FLMNFMT=$$CONVTIME^SDHL7APU(STM),TMPSTART=FLMNFMT,SDECSTART=$$FMTE^XLFDT(FLMNFMT),SDECEND=$$FMADD^XLFDT(FLMNFMT,,,SDECLEN,0)
 . . ;I $P(PROVAPT(XX+1),"|")="NTE" S SDECNOTE=$P($G(PROVAPT(XX+1)),"|",4)
 . . ;S MSGARY("CNCLRSN")=$G(SCH(6,1,2))
 . . ;S MSGARY("CANCODE")=$G(SCH(6,1,4))
 . . ;S MSGARY("CANREMARKS")=$G(SCH(6,1,5))
 . I $P(SEG,"|")="AIL" D AIL
 S OVB=1,SDEL=""
 D APPADD^SDEC07(.SDECY,SDECSTART,SDECEND,DFN,SDECRES,SDECLEN,SDECNOTE,SDECATID,SDECCR,SDMRTC,SDDDT,SDREQBY,SDLAB,PROVIEN,SDID,SDAPTYP,SDSVCP,SDSVCPR,SDCL,SDEKG,SDXRAY,APPTYPE,EESTAT,OVB,SDPARENT,SDEL) ;ADD NEW APPOINTMENT
 Q
AIL ;
 D PARSESEG^SDHL7APU(SEG,.AIL,.HL)
 S SDCL=+$G(AIL(3,1,1)) N RET,RET1 D RESLKUP^SDHL7APU(SDCL) S SDECRES=RET1
 N STCREC,CONSID,MTC
 S STCREC=""
 S SDAPTYP=""
 S (SDPARENT)=$G(AIL(1,4,1,4))
 I $G(AIL(1,4,1,2))="C" S CONSID=$G(AIL(1,4,1,1)),SDAPTYP="C|"_$G(AIL(1,4,1,1))
 I $G(AIL(1,4,1,2))="R" D
 . S MTC=$P($G(^SDEC(409.85,+$G(SDPARENT),3)),"^"),SDMRTC=$S(MTC>0:1,1:0)
 . ;get the last child sequence number and set RTCID and MSGARY("RTCID") = to last sequence number plus 1
 . K X12,RTCID S RTCID="",X12=0 I +$L(SDPARENT) F  S X12=$O(^SDEC(409.85,SDPARENT,2,X12)) Q:X12'>0  S RTCID=X12+1
 . S:$G(MTC)=1 SDAPTYP="R|"_$G(RTCID) ; if this is a multi RTC order $P(SDAPTYP,"|",2) is the next child sequence number, else it is null
 . Q
 ;Get parent rtc order if it is a multi appointment rtc
 S:$G(AIL(1,4,1,2))="A" SDAPTYP="A|"
 I $P(PROVAPT(XX+1),"|")="NTE" S SDECNOTE=$P($G(PROVAPT(XX+1)),"|",4)
 ;
 Q
NEWTIME ;
 N ST1,ST12
 S ST12=$P(SDTMPHL(1),"|",12)
 S ST1=$P(ST12,"^",4) ;2019-02-19T19:00
 S ST1=$TR(ST1,"-",""),ST1=$TR(ST1,"T",""),ST1=$TR(ST1,":",""),ST1=+ST1
 S TZONE=$$GET1^DIQ(4.3,"1,",1,"I"),DIFF=$$GET1^DIQ(4.4,$G(TZONE)_",",2,"E")*(-1)
 S ST1=$$HL7TFM^XLFDT(ST1,"U"),ST1=$$FMADD^XLFDT(ST1,,-$G(DIFF),5)
 S ST1=$$FMTHL7^XLFDT(ST1)
 S ST1=$$FMTE^XLFDT(ST1)
 S ST1=$E(ST1,1,4)_"-"_$E(ST1,5,6)_"-"_$E(ST1,7,8)_"T"_$E(ST1,9,10)_":"_$E(ST1,11,12)_":"_$E(ST1,13,14)_"Z"
 S $P(ST12,"^",4)=$G(ST1)
 S $P(SDTMPHL(1),"|",12)=$G(ST12)
 S $P(SDTMPHL(5),"|",5)=$P(ST12,"^",4)
 Q
 ;
TMCONV(X) ;
 ;convert time to Zulu timezone
 N TZONE,DIFF,UTC,UTC1,UTC2
 S TZONE=$$GET1^DIQ(4.3,"1,",1,"I"),DIFF=$$GET1^DIQ(4.4,$G(TZONE)_",",2,"E")*(-1)
 S UTC=$$FMADD^XLFDT(X,,$G(DIFF),,),UTC2=$$FMTHL7^XLFDT(UTC)
 S UTC1=$E(UTC2,1,4)_"-"_$E(UTC2,5,6)_"-"_$E(UTC2,7,8)_"T"_$E(UTC2,9,10)_":"_$E(UTC2,11,12)_":00.000Z"
 Q UTC1
 ;
CHKCON(DFN,SDAPTYP) ; checks if both consult ids or both rtc ids match the patient, if the consult or rts is not for the patient, reject
 Q:$G(AIL(1,3,1,4))'=$G(AIL(2,3,1,4))
 S STOPME=0
 N IENS,X1,GMRDFN
 I $P($G(SDAPTYP),"|",1)="C" D
 .F X1=1:1:2 D
 ..Q:$G(STOPME)=1
 ..S IENS=+$G(AIL(X1,4,1,1))
 ..Q:+$G(IENS)'>0
 ..S GMRDFN=$$GET1^DIQ(123,IENS_",",.02,"I","ERR")
 ..I $G(GMRDFN)'=$G(DFN)!($G(^GMR(123,+$G(IENS),0))="") D
 ...S ERR="MSA^1^^100^AE^CONSULT ID# "_+$G(IENS)_" IS NOT FOR PATIENT "_$P(^DPT(DFN,0),"^")
 ...D SENDERR^SDHL7APU(ERR)
 ...S STOPME=1
 ..Q
 .Q
 I $P($G(SDAPTYP),"|",1)="R" D
 .F X1=1:1:2 D
 ..Q:$G(STOPME)=1
 ..S IENS=+$G(AIL(X1,4,1,1))
 ..Q:+$G(IENS)'>0
 ..I $G(DFN)'=$P($G(^SDEC(409.85,IENS,0)),"^",1)!($G(^SDEC(409.85,IENS,0))="") D
 ...S STOPME=1
 ...S ERR="MSA^1^^100^AE^RTC ORDER# "_+$P($G(SDAPTYP),"|",2)_" IS NOT FOR PATIENT "_$P(^DPT(DFN,0),"^")
 ...D SENDERR^SDHL7APU(ERR)
 ..Q
 Q
CHKCAN(PAT,CLINIC,DATE) ; check to see if the appointment in 44 is canceled correctly. if not cancel it
 N TIEN,DIK,DA
 Q:$G(PAT)'>0
 Q:$G(CLINIC)'>0
 Q:$G(DATE)=""
 S TIEN=$$SCIEN^SDECU2(PAT,CLINIC,DATE)
 Q:$G(TIEN)'>0
 I $G(TIEN)>0 D
 .S DIK="^SC("_CLINIC_",""S"","_DATE_",1,"
 .S DA(2)=CLINIC,DA(1)=DATE,DA=TIEN
 .D ^DIK
 .K DIK,DA
 Q
