EC2P144B ;ALB/JR - EC National Procedure Update ;5/4/18 12:00pm
 ;;2.0;EVENT CAPTURE;**144**;8 May 96;Build 6
 ;
 ;this routine is used as a post-init in a KIDS build
 ;to modify the EC National Procedure file (#725)
 ;
 Q
 ;
ADDPROC ;* add national procedures
 ;
 ;  ECXX is in format:
 ;   NAME^NATIONAL NUMBER^CPT CODE^FIRST NATIONAL NUMBER SEQUENCE
 ;   LAST NATIONAL NUMBER SEQUENCE
 ;
 N ECX,ECXX,ECDINUM,ECNAME,ECCODE,ECCPT,ECCOUNT,X,Y,DIC,DIE,DA,DR,DLAYGO,DINUM
 N ECADD,ECBEG,ECEND,ECCODX,ECNAMX,ECSEQ,ECLIEN,ECSTR,ECCPTN
 D MES^XPDUTL(" ")
 D BMES^XPDUTL("Adding new procedures to EC NATIONAL PROCEDURE File (#725)...")
 D MES^XPDUTL(" ")
 S ECDINUM=$O(^EC(725,9999),-1),ECCOUNT=$P(^EC(725,0),U,4)
 F ECX=1:1 S ECXX=$P($T(NEW+ECX),";;",2) Q:ECXX="QUIT"  D
 .S ECNAME=$P(ECXX,U,1),ECCODE=$P(ECXX,U,2),ECCPTN=$P(ECXX,U,3),ECCODX=ECCODE
 .S ECCPT=""
 .I ECCPTN'="" S ECCPT=$$FIND1^DIC(81,"","X",ECCPTN) I +ECCPT<1 D  Q
 ..S ECSTR="   CPT code "_ECCPTN_" not a valid code in CPT File."
 ..D MES^XPDUTL(" ")
 ..D BMES^XPDUTL("   ["_ECCODE_"] "_ECSTR)
 .S ECBEG=$P(ECXX,U,4),ECEND=$P(ECXX,U,5),ECNAMX=ECNAME
 .I ECBEG="" S X=ECNAME D FILPROC Q
 .F ECSEQ=ECBEG:1:ECEND D
 ..S ECADD="000"_ECSEQ,ECADD=$E(ECADD,$L(ECADD)-2,$L(ECADD))
 ..;S ECNAME=ECNAMX_ECADD,X=ECNAME,ECCODE=ECCODX_ECADD
 ..I $E(ECCODX,1,3)'="RCM" S ECNAME=ECNAMX_ECSEQ,X=ECNAME,ECCODE=ECCODX_ECADD
 ..E  S ECNAME=ECNAMX_$E(ECADD,2,99),X=ECNAME,ECCODE=ECCODX_$E(ECADD,2,99)
 ..D FILPROC
 S $P(^EC(725,0),U,4)=ECCOUNT,X=$O(^EC(725,999999),-1),$P(^EC(725,0),U,3)=X
 Q
 ;
FILPROC ;File national procedures
 I '$D(^EC(725,"D",ECCODE)) D
 .S ECDINUM=ECDINUM+1,DINUM=ECDINUM,DIC(0)="L",DLAYGO=725,DIC="^EC(725,"
 .S DIC("DR")="1////^S X=ECCODE;4///^S X=ECCPT"
 .D FILE^DICN
 .I +Y>0 D
 ..S ECCOUNT=ECCOUNT+1
 ..D MES^XPDUTL(" ")
 ..S ECSTR="   Entry #"_+Y_" for "_$P(Y,U,2)
 ..S ECSTR=ECSTR_$S(ECCPT'="":" [CPT: "_ECCPT_"]",1:"")_" ("_ECCODE_")"
 ..D BMES^XPDUTL(ECSTR)
 ..D BMES^XPDUTL("      ...successfully added.")
 .I Y=-1 D
 ..D MES^XPDUTL(" ")
 ..D BMES^XPDUTL("ERROR when attempting to add "_ECNAME_" ("_ECCODE_")")
 I $D(^EC(725,"DL",ECCODE)) D
 .S ECLIEN=$O(^EC(725,"DL",ECCODE,""))
 .D MES^XPDUTL(" ")
 .D BMES^XPDUTL("   Your site has a local procedure (entry #"_ECLIEN_") in File #725")
 .D BMES^XPDUTL("   which uses "_ECCODE_" as its National Number.")
 .D BMES^XPDUTL("   Please inactivate this local procedure.")
 .K Y
 Q
NEW ;national procedures to add;;descript^nation #^CPT code^beg seq^end seq
 ;;DX- ENERGY BALANCE^NU300^
 ;;DX- ORAL/NUTR SUPPORT^NU301^
 ;;DX- FLUID INTAKE^NU302^
 ;;DX- BIOACTIVE SUBSTANCES^NU303^
 ;;DX- NUTRIENT INTAKE^NU304^
 ;;DX- FUNCTIONAL^NU305^
 ;;DX- BIOCHEMICAL^NU306^
 ;;DX- WEIGHT^NU307^
 ;;DX- KNOWLEDGE/BELIEFS^NU308^
 ;;DX- ACTIVITY & FXN^NU309^
 ;;DX- FOOD SAFETY & ACCESS^NU310^
 ;;INTERV-MEALS/SNACKS^NU400^
 ;;INTERV-EN or PN^NU401^
 ;;INTERV-SUPPL^NU402^
 ;;INTERV-FEEDING ASSIST^NU403^
 ;;INTERV-FEEDING ENVIRON^NU404^
 ;;INTERV-MED MGMT^NU405^
 ;;INTERV - NUTR EDUC^NU406^
 ;;INTERV- NUTR COUNSELING^NU407^
 ;;INTERV-COORD OF CARE^NU408^
 ;;INTERV-POP ACTION^NU409^
 ;;M&E-PROBLEM RESOLVED^NU500^
 ;;M&E-PROBLEM IMPROVED^NU501^
 ;;COGNITIVE SKILLS DVMNT^RC132^G0515
 ;;INPT CONSULT NO PSYCHOTHERAPY^SW183^T1016
 ;;INPT D/C PLAN COORDINATION^SW184^T1016
 ;;CONTRACT NURSING HOME F/U^SW185^T1016
 ;;INELIG NON-PT REFER/CM^SW186^T1016
 ;;INELIG PT REFER/CASE MGMT^SW187^T1016
 ;;COMMUNITY RES CARE F/U^SW188^T1016
 ;;QUIT
 ;
NAMECHG ;* change national procedure names
 ;
 ;  ECXX is in format:
 ;   NATIONAL NUMBER^NEW NAME
 ;
 N ECX,ECXX,ECDA,DA,DR,DIC,DIE,X,Y,ECSTR
 D MES^XPDUTL(" ")
 D BMES^XPDUTL("Changing names in EC NATIONAL PROCEDURE File (#725)...")
 D MES^XPDUTL(" ")
 F ECX=1:1 S ECXX=$P($T(CHNG+ECX),";;",2) Q:ECXX="QUIT"  D
 .I $D(^EC(725,"D",$P(ECXX,U,1))) D
 ..S ECDA=+$O(^EC(725,"D",$P(ECXX,U,1),0))
 ..I $D(^EC(725,ECDA,0)) D
 ...S DA=ECDA,DR=".01////^S X=$P(ECXX,U,2)",DIE="^EC(725," D ^DIE
 ...D MES^XPDUTL(" ")
 ...D MES^XPDUTL("   Entry #"_ECDA_" for "_$P(ECXX,U,1))
 ...D BMES^XPDUTL("      ... field (#.01) updated to  "_$P(ECXX,U,2)_".")
 .I '$D(^EC(725,"D",$P(ECXX,U,1))) D
 ..D MES^XPDUTL(" ")
 ..S ECSTR="Can't find entry for "_$P(ECXX,U,1)
 ..D BMES^XPDUTL(ECSTR_" ...field (#.01) not updated.")
 Q
 ;
CHNG ;name changes -national code #^new procedure name
 ;;NU007^ASSMT-NO PROBLEM
 ;;NU008^ASSMT-PREDICTED PROBLEM
 ;;NU009^ASSMT-PROBLEM PRESENT
 ;;NU215^DX-MALNUTR DISORDERS
 ;;SP101^INTRAOP MONITORING PER HR
 ;;SW006^HEALTH & BEH INTERVENT IND
 ;;SW009^TEAM CONFERENCE W/PT 30M
 ;;SW019^HEALTH & BEH INTERVENT GRP
 ;;SW020^GROUP PSYCHOTHERAPY
 ;;SW087^CASE MGMT 15M IN-HOME F/U
 ;;SW093^TEAM CONF W/O PT NO PRESCRIBER
 ;;SW122^ADVANCED DIRECTIVES
 ;;SW137^MH ASSESSMENT BY NON-MD
 ;;SW138^PSYCHIATRIC DIAGNOSTIC EVALUATION
 ;;SW153^PROGRAM INTAKE SCREENING
 ;;SW154^HOME VISIT IND/FM MARRIAGE COUNSEL
 ;;QUIT
