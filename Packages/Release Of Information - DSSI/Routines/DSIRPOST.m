DSIRPOST ;EWL - Document Storage System Inc - Post Install Routine ;12/08/2011 11:18
 ;;8.2;RELEASE OF INFORMATION - DSSI;;Nov 08, 2011;Build 25
 ;Copyright 1995-2012, Document Storage Systems, Inc., All Rights Reserved
 ;
 ;DBIA# Supported Reference
 ;----- --------------------------------
 ;2263  EN^XPAR
 ;10013 IXALL^DIK
 Q
V82 ;EWL - Document Storage System Inc - conversion routine for 8.1
 D MES^XPDUTL("DSIR 8.2 POST INITIALIZATION EXECUTION")
 ;CREATE AND LOAD NEW PARAMETERS
 D EN^XPAR("PKG","DSIR REPORT KILL CHECK DATE",1,DT)
 D EN^XPAR("PKG","DSIR REPORT RETENTION PERIOD",1,7)
 ;CREATE NEW BILLING INDEX
 S DIK="^DSIR(19620.2,",DIK(1)="2.05^ARBAL" D IXALL^DIK
 ; INITIALIZE RECEIVED TYPE AND PATIENT ADDRESS TYPE
 D MES^XPDUTL("INITIALIZING 'ADDRESS TYPE', 'RECEIVED TYPE' AND 'HOW CREATED' FIELDS")
 N I,IS,FDA,GET S I=0 F  S I=$O(^DSIR(19620,I)) Q:'I  D
 .K FDA,GET
 .S IS=I_","
 .D GETS^DIQ(19620,IS,"6.08;6.09;.83","I","GET")
 .S:GET(19620,IS,.83,"I")']"" FDA(19620,IS,.83)=0
 .S:GET(19620,IS,6.08,"I")']"" FDA(19620,IS,6.08)=0
 .S:GET(19620,IS,6.09,"I")']"" FDA(19620,IS,6.09)="W"
 .D FILE^DIE(,"FDA")
 D MES^XPDUTL("BUILDING RECEIVED TYPE INDEX - 'ARCVDTYP' ON FILE 19620")
 I '$D(^DSIR(19620,"ARCVDTYP")) D
 .S DIK="^DSIR(19620,",DIK(1)=6.08 D ENALL^DIK
 Q
