DSIRRPTB ;AMC/EWL - Document Storage Systems;Billing Report RPC's ;04/14/2011 11:18
 ;;8.2;RELEASE OF INFORMATION - DSSI;;Nov 08, 2011;Build 25
 ;Copyright 1995-2012, Document Storage Systems, Inc., All Rights Reserved
 ;
 ;DBIA# Supported Reference
 ;----- --------------------------------
 ;2053  WP^DIE
 ;2056  GETS^DIQ
 Q
 ;
AMTB(AXY,FRDT,TODT,DIVL,SCHED,ESTART) ; RPC - DSIRRPTB AMTB AMOUNT BILLED
 ;Input Parameters:
 ;     FRDT - Start Date - Required (FM fromat)
 ;     TODT - End Date - Optional (FM format, default = Current date)
 ;     DIVL - Divisions
 ;     SCHED  Schedule - Boolean for scheduled or immediate run
 ;            1 = Schedule / 0 or Null = Run Immediately
 ;     ESTART Earliet time to start the scheaduled task
 ;                               
 ;Return Array:
 ; -1^ERROR MESSAGE
 ; OR
 ; task #^REPORT AFTER earliest report time
 ; OR
 ; Bill IEN^Patient Name/FOIA^Requestor^Open Date^Bill Entered Date
 ; ^Current Status^Amount Billed^Division
 ;
 S AXY=$NA(^TMP("DSIRRPTBAMTB",$J)),SCHED=+$G(SCHED),FRDT=+$G(FRDT),TODT=$S(+$G(TODT):TODT,1:DT) K @AXY
 ; From date is required for this report
 I 'FRDT S @AXY@(1)="-1^Missing Start Date!" Q
 I 'SCHED D AMTB3(.AXY,FRDT,TODT,$G(DIVL))
 I SCHED D
 .N RPT,SFDA,EMSG,PARRAY
 .;CREATE THE REPORT SCHEDULE RECORD, FILE THE PARAMETERS, AND SUBMIT
 .S ESTART=$G(ESTART),RPT="AMOUNT BILLED",IEN=$$UDSCHED^DSIRRPTR(RPT,.ESTART)
 .S PARRAY(1)="S FRDT="_$G(FRDT),PARRAY(2)="S TODT="_$G(TODT),PARRAY(3)="S DIVL="""_$G(DIVL)_""""
 .D PREPSUB^DSIRRPTR(.AXY,ESTART,RPT,"AMTB2^DSIRRPTB",IEN,.PARRAY)
 Q
AMTB2(SIEN) ; THIS IS LAUNCHED BY THE TASK MANAGER AND IT REASSEMBLES THE PARAMETERS 
 N DONE,FRDT,TODT,DIV,PRMS,RET S DONE=0,RET="TASK",ZTREQ="@" I '$$INIT^DSIRRPTR(SIEN) Q
 D AMTB3(.RET,FRDT,TODT,DIVL)  I 'DONE,'$$STOPCHK^DSIRRPTR(SIEN) D RPTDONE^DSIRRPTR(SIEN) ;
 Q
AMTB3(AXY,FRDT,TODT,DIV) ;RPC - DSIR AMOUNT BILLED RPT
 N TASK,LOOPCT,LOOPCHK S LOOPCT=1,LOOPCHK=50,TODT=TODT+.03
 S TASK=(AXY="TASK") I TASK S AXY=$NA(^TMP("DSIRRPTBAMTB",$J)) K @AXY
 K ^TMP("DSIR",$J)  N DLIST,DLIST1,I,NLIST
 I $G(DIV)  F I=0:1 Q:'$P(DIV,U,I+1)  S DLIST(I)=$P(DIV,U,I+1),NLIST(I)=$P(^DIC(4,DLIST(I),0),U)
 I '$G(DIV) I '$D(^XUSEC("DSIR MDIV",DUZ)) S DIV=DUZ(2),DLIST(0)=DIV,NLIST(0)=$P(^DIC(4,DIV,0),U),I=1
 I '$G(DIV) D GETDIVS^DSIROI1(.DLIST1) F I=0:1 Q:'$D(DLIST1(I))  S DLIST(I)=$P(DLIST1(I),U),NLIST(I)=$P(DLIST1(I),U,2)
 N DIVT,RDIV,ROI,BAL,BIL,BILN,IEN,FIL,GET,XRF,MDIV,DIVS,OPDT,BILDT,REQTR,LODT,AA,BB
 S I=I-1,FIL=19620.2,XRF="ADENT",YY=0,LODT=FRDT-.1
 F  S LODT=$O(^DSIR(FIL,XRF,LODT)) Q:('LODT)!(LODT>TODT)!DONE  S BIL=0 D
 .I TASK S LOOPCT=LOOPCT+1 I ('(LOOPCT#LOOPCHK)),$G(SIEN),$$STOPCHK^DSIRRPTR(SIEN) S DONE=1 D BAILOUT Q
 .F  S BIL=$O(^DSIR(FIL,XRF,LODT,BIL)) Q:'BIL  D
 ..S IEN=BIL_"," K GET D GETS^DIQ(FIL,IEN,".01;2.01","IE","GET")
 ..S ROI=+$G(GET(FIL,IEN,.01,"I")) Q:'ROI
 ..S DIVT=$$DIVTEST^DSIROI1(ROI,.DLIST) Q:DIVT<0
 ..D ROIDATA(ROI)
 ..S BILN=$G(GET(FIL,IEN,.01,"E")),BILDT=LODT\1,BAL=$G(GET(FIL,IEN,2.01,"I")) I BAL>0.00999 D
 ...S XX=$G(GET(FIL,IEN,.01,"I"))_U_BILN_U_REQTR_U_OPDT_U_BILDT_U_STAT_U_BAL_U_DLIST(DIVT)_U_NLIST(DIVT)
 ...S YY=YY+1,^TMP("DSIR",$J,BILN,YY)=XX
 ..K GET
 S (AA,BB)=$NA(^TMP("DSIR",$J)),BB=$P(BB,")")_",",YY=0
 Q:DONE
 F  S AA=$Q(@AA) Q:(AA'[BB)!DONE  D
 .I TASK S LOOPCT=LOOPCT+1 I ('(LOOPCT#LOOPCHK)),$G(SIEN),$$STOPCHK^DSIRRPTR(SIEN) S DONE=1 D BAILOUT Q
 .S RDIV=$P(@AA,U,9),YY=YY+1,@AXY@(RDIV,YY)=@AA
 I 'YY S @AXY@(1)="-1^No bills entered in selected date range!"
 K ^TMP("DSIR",$J)
 I TASK N DATA,DTMP,CT,PREFIX S CT=0,PREFIX=$P(AXY,")"),DTMP=$NA(@AXY),DATA=$NA(^TMP("DSIR",$J)) D
 .F  S DTMP=$Q(@DTMP) Q:DTMP'[PREFIX  S CT=CT+1,@DATA@(CT)=@DTMP
 .D WP^DIE(19620.35,SIEN_",",2,,DATA,"EMSG")
 Q
 ;
FEER(AXY,FRDT,T0DT,DIVL,SCHED,ESTART) ; RPC - DSIRRPTB FEER FEES REC RPT
 ;Input Parameters:
 ;     FRDT - Start Date (Required, FileMan format)
 ;     TODT - End Date (Optional, FileMan format, Default = Current day)
 ;     DIVL - String of Division or NULL for all
 ;     SCHED  Schedule - Boolean for scheduled or immediate run
 ;            1 = Schedule / 0 or Null = Run Immediately
 ;     ESTART Earliet time to start the scheaduled task
 ;                       
 ;Return Array:
 ; -1^ERROR MESSAGE
 ; OR
 ; task #^REPORT AFTER earliest report time
 ; OR
 ; Bill IEN^Patient Name/FOIA^Requestor^Open Date^Bill Entered Date
 ; ^Current Status^Payment Date^Payment Amount^Waived^Allowance^Division ;                       
 S AXY=$NA(^TMP("DSIRRPTBFEES",$J)),SCHED=+$G(SCHED),FRDT=+$G(FRDT),TODT=$S(+$G(TODT):TODT,1:DT) K @AXY
 ; From date is required for this report
 I 'FRDT S @AXY@(1)="-1^Missing Start Date!" Q
 I 'SCHED D FEER3(.AXY,FRDT,TODT,$G(DIVL))
 I SCHED D
 .N RPT,SFDA,EMSG,PARRAY
 .;CREATE THE REPORT SCHEDULE RECORD, FILE THE PARAMETERS, AND SUBMIT
 .S ESTART=$G(ESTART),RPT="FEES RECEIVED",IEN=$$UDSCHED^DSIRRPTR(RPT,.ESTART)
 .S PARRAY(1)="S FRDT="_$G(FRDT),PARRAY(2)="S TODT="_$G(TODT),PARRAY(3)="S DIVL="""_$G(DIVL)_""""
 .D PREPSUB^DSIRRPTR(.AXY,ESTART,RPT,"FEER2^DSIRRPTB",IEN,.PARRAY)
 Q
 ;
FEER2(SIEN) ; THIS IS LAUNCHED BY THE TASK MANAGER AND IT REASSEMBLES THE PARAMETERS 
 N DONE,FRDT,TODT,DIV,RET S DONE=0,RET="TASK",ZTREQ="@" I '$$INIT^DSIRRPTR(SIEN) Q
 D FEER3(RET,FRDT,TODT,DIVL)  I 'DONE,'$$STOPCHK^DSIRRPTR(SIEN) D RPTDONE^DSIRRPTR(SIEN)
 Q
FEER3(AXY,FRDT,TODT,DIV) ; FEES REPORT 
 N TASK,LOOPCT,LOOPCHK S LOOPCT=1,LOOPCHK=50,TODT=TODT+.03
 S TASK=(AXY="TASK") I TASK S AXY=$NA(^TMP("DSIRRPTBFEER",$J)) K @AXY
 N RDIV,DIVT,DLIST1,DLIST,NLIST,IEN,I K ^TMP("DSIR",$J)
 I $G(DIV)  F I=0:1 Q:'$P(DIV,U,I+1)  S DLIST(I)=$P(DIV,U,I+1),NLIST(I)=$P(^DIC(4,DLIST(I),0),U)
 I '$G(DIV) I '$D(^XUSEC("DSIR MDIV",DUZ)) S DIV=DUZ(2),DLIST(0)=DIV,NLIST(0)=$P(^DIC(4,DIV,0),U),I=1
 I '$G(DIV) D GETDIVS^DSIROI1(.DLIST1) F I=0:1 Q:'$D(DLIST1(I))  S DLIST(I)=$P(DLIST1(I),U),NLIST(I)=$P(DLIST1(I),U,2)
 N ROI,LODT,FIL,IEN,GET,XRF,PAY,XX,YY,BILN,BILIEN,OPDT,MDIV,DIVS,STAT,BILLS,BILDT,ALLOW,WAVE,PAYDT,PAYMT
 S YY=0,I=I-1,LODT=FRDT-.1,FIL=19620.21,XRF="APD"
 F  S LODT=$O(^DSIR(FIL,XRF,LODT)) Q:'LODT!(LODT>TODT)  S PAY=0 F  S PAY=$O(^DSIR(FIL,XRF,LODT,PAY)) Q:'PAY  D
 .S LOOPCT=LOOPCT+1 I ('(LOOPCT#LOOPCHK)),$G(SIEN),$$STOPCHK^DSIRRPTR(SIEN) G BAILOUT
 .K GET S IEN=PAY_"," D GETS^DIQ(FIL,IEN,"*","IE","GET")
 .S BILN=$G(GET(FIL,IEN,.01,"E")),BILIEN=+$G(GET(FIL,IEN,.01,"I")),ALLOW=$G(GET(FIL,IEN,.07,"I")),WAVE=$G(GET(FIL,IEN,.05,"I")) Q:'BILIEN
 .S BILLS=BILIEN_"," D GETS^DIQ(19620.2,BILLS,".01;.02","IE","GET") S ROI=+$G(GET(19620.2,BILLS,.01,"I"))
 .S BILDT=$G(GET(19620.2,BILLS,.02,"I")),DIVT=$$DIVTEST^DSIROI1(ROI,.DLIST) S:DIVT<0 ROI=0
 .Q:'ROI  S PAYDT=$G(GET(FIL,IEN,.06,"I")),PAYMT=$G(GET(FIL,IEN,.02,"I")) D ROIDATA(ROI)
 .S XX=BILIEN_U_BILN_U_REQTR_U_OPDT_U_BILDT_U_STAT_U_PAYDT_U_PAYMT_U_WAVE_U_ALLOW_U_DLIST(DIVT)_U_NLIST(DIVT)
 .S YY=YY+1,^TMP("DSIR",$J,BILN,YY)=XX
 S BILN="",YY=0
 F  S BILN=$O(^TMP("DSIR",$J,BILN)) Q:(BILN="")!DONE  D
 .I TASK S LOOPCT=LOOPCT+1 I ('(LOOPCT#LOOPCHK)),$G(SIEN),$$STOPCHK^DSIRRPTR(SIEN) S DONE=1 D BAILOUT Q
 .S XX=0 F  S XX=$O(^TMP("DSIR",$J,BILN,XX)) Q:('XX)!DONE  D
 ..I TASK S LOOPCT=LOOPCT+1 I ('(LOOPCT#LOOPCHK)),$G(SIEN),$$STOPCHK^DSIRRPTR(SIEN) S DONE=1 D BAILOUT Q
 ..S YY=YY+1,RDIV=$P(^TMP("DSIR",$J,BILN,XX),U,12),@AXY@(RDIV,YY)=^TMP("DSIR",$J,BILN,XX)
 S:'YY @AXY@($J,0)="-1^No Payment Activity Found for Date Range!"
 K ^TMP("DSIR",$J)
 I TASK N DATA,DTMP,CT,PREFIX S CT=0,PREFIX=$P(AXY,")"),DTMP=$NA(@AXY),DATA=$NA(^TMP("DSIR",$J)) D
 .F  S DTMP=$Q(@DTMP) Q:DTMP'[PREFIX  S CT=CT+1,@DATA@(CT)=@DTMP
 .D WP^DIE(19620.35,SIEN_",",2,,DATA,"EMSG")
 Q
 ;
FEEO(AXY,FRDT,TODT,DIVL,SCHED,ESTART) ; RPC - DSIRRPTB FEEO FEES OUT RPT
 ;Input Parameter:
 ;     FRDT - Start Date (Required, FileMan format)
 ;     TODT - End Date (Optional, FileMan format, Default = Current day)
 ;     DIVL -  String of Division or NULL for all
 ;     SCHED   Schedule - Boolean for scheduled or immediate run
 ;             1 = Schedule / 0 or Null = Run Immediately
 ;     ESTART  Earliet time to start the scheaduled task
 ;
 ;RETURN PARAMETER DESCRIPTION:
 ;Return Array:
 ; -1^ERROR MESSAGE
 ; OR
 ; task #^REPORT AFTER earliest report time
 ; OR
 ; Bill IEN^Patient Name/FOIA^Requestor^Open Date^Bill Entered Date
 ; ^Current Status^Current Balance Due^Division^Division Name
 ;
 S AXY=$NA(^TMP("DSIRRPTBFEER",$J)),SCHED=+$G(SCHED),FRDT=+$G(FRDT),TODT=$S(+$G(TODT):TODT,1:DT) K @AXY
 ; From date is required for this report
 I 'FRDT S @AXY@(1)="-1^Missing Start Date!" Q
 I 'SCHED D FEEO3(.AXY,FRDT,TODT,$G(DIVL))
 I SCHED D
 .N RPT,PARRAY,SFDA,EMSG
 .S ESTART=$G(ESTART),RPT="FEES OUTSTANDING",IEN=$$UDSCHED^DSIRRPTR(RPT,.ESTART)
 .S PARRAY(1)="S FRDT="_$G(FRDT),PARRAY(2)="S TODT="_TODT,PARRAY(3)="S DIVL="""_$G(DIVL)_""""
 .D PREPSUB^DSIRRPTR(.AXY,ESTART,RPT,"FEEO2^DSIRRPTB",IEN,.PARRAY)
 Q
FEEO2(SIEN) ; THIS IS LAUNCHED BY THE TASK MANAGER AND IT REASSEMBLES THE PARAMETERS
 N DONE,FRDT,TODT,DIVL,RET S DONE=0,RET="TASK",ZTREQ="@" I '$$INIT^DSIRRPTR(SIEN) Q
 D FEEO3(RET,FRDT,TODT,DIVL)  I 'DONE,'$$STOPCHK^DSIRRPTR(SIEN) D RPTDONE^DSIRRPTR(SIEN)
 Q
FEEO3(AXY,FRDT,TODT,DIV) ;RPC - DSIR FEES OUT RPT
 N TASK,LOOPCT,LOOPCHK S LOOPCT=1,LOOPCHK=50
 S TASK=(AXY="TASK") I TASK S AXY=$NA(^TMP("DSIRRPTBFEEO",$J)) K @AXY
 K ^TMP("DSIR",$J) N DLIST,DLIST1,I,NLIST
 I $G(DIV)  F I=0:1 Q:'$P(DIV,U,I+1)  S DLIST(I)=$P(DIV,U,I+1),NLIST(I)=$P(^DIC(4,DLIST(I),0),U)
 I '$G(DIV) I '$D(^XUSEC("DSIR MDIV",DUZ)) S DIV=DUZ(2),DLIST(0)=DIV,NLIST(0)=$P(^DIC(4,DIV,0),U),I=1
 I '$G(DIV) D GETDIVS^DSIROI1(.DLIST1) F I=0:1 Q:'$D(DLIST1(I))  S DLIST(I)=$P(DLIST1(I),U),NLIST(I)=$P(DLIST1(I),U,2)
 N LODT,DIVT,RDIV,GLRF,ROI,BAL,BILN,BILIEN,IEN,FI,GET,XRF,MDIV,DIVS,OPDT,BILDT,REQTR,STAT
 ;S I=I-1,FIL=19620.2,XRF="ACBD",(BAL,YY)=0
 S I=I-1,FI=19620.2,XRF="AENBAL",YY=0,LODT=FRDT-.0000001
 F  S LODT=$O(^DSIR(FI,XRF,LODT)) Q:(LODT>TODT)!('LODT)!DONE  S BAL=0 D
 .I TASK S LOOPCT=LOOPCT+1 I ('(LOOPCT#LOOPCHK)),$G(SIEN),$$STOPCHK^DSIRRPTR(SIEN) S DONE=1 D BAILOUT Q
 .F  S BAL=$O(^DSIR(FI,XRF,LODT,BAL)) Q:('BAL)!DONE  S BILIEN=0 D
 ..I TASK S LOOPCT=LOOPCT+1 I ('(LOOPCT#LOOPCHK)),$G(SIEN),$$STOPCHK^DSIRRPTR(SIEN) S DONE=1 D BAILOUT Q
 ..F  S BILIEN=$O(^DSIR(FI,XRF,LODT,BAL,BILIEN)) Q:('BILIEN)!DONE  D
 ...I TASK S LOOPCT=LOOPCT+1 I ('(LOOPCT#LOOPCHK)),$G(SIEN),$$STOPCHK^DSIRRPTR(SIEN) S DONE=1 D BAILOUT Q
 ...K GET S IEN=BILIEN_"," D GETS^DIQ(FI,IEN,".01;.02","IE","GET") S ROI=+$G(GET(FI,IEN,.01,"I")) Q:'ROI
 ...S DIVT=$$DIVTEST^DSIROI1(ROI,.DLIST) Q:DIVT<0 
 ...D ROIDATA(ROI) S BILN=$G(GET(FI,IEN,.01,"E")),BILDT=$G(GET(FI,IEN,.02,"I"))
 ...S YY=YY+1,XX=BILIEN_U_BILN_U_REQTR_U_OPDT_U_BILDT_U_STAT_U_BAL_U_DLIST(DIVT)_U_NLIST(DIVT),^TMP("DSIR",$J,BILN,YY)=XX
 S BILN="",YY=0 F  S BILN=$O(^TMP("DSIR",$J,BILN)) Q:(BILN="")!DONE  S XX=0 D
 .I TASK S LOOPCT=LOOPCT+1 I ('(LOOPCT#LOOPCHK)),$G(SIEN),$$STOPCHK^DSIRRPTR(SIEN) S DONE=1 D BAILOUT Q
 .F  S XX=$O(^TMP("DSIR",$J,BILN,XX)) Q:('XX)!DONE  D
 ..I TASK S LOOPCT=LOOPCT+1 I ('(LOOPCT#LOOPCHK)),$G(SIEN),$$STOPCHK^DSIRRPTR(SIEN) S DONE=1 D BAILOUT Q
 ..S RDIV=$P(^TMP("DSIR",$J,BILN,XX),U,9),YY=YY+1,@AXY@(RDIV,YY)=^TMP("DSIR",$J,BILN,XX)
 S:'YY @AXY@(1)="-1^No Fees Outstanding Found!"
 K ^TMP("DSIR",$J)
 I TASK N DATA,DTMP,CT,PREFIX S CT=0,PREFIX=$P(AXY,")"),DTMP=$NA(@AXY),DATA=$NA(^TMP("DSIR",$J)) D
 .F  S DTMP=$Q(@DTMP) Q:DTMP'[PREFIX  S CT=CT+1,@DATA@(CT)=@DTMP
 .D WP^DIE(19620.35,SIEN_",",2,,DATA,"EMSG")
 Q
 ;
ROIDATA(ROI) ;Get the instance level data, Current Status, Open Date & Requestor
 N GET,ROIS,FIL S FIL=19620,ROIS=ROI_","
 D GETS^DIQ(FIL,ROIS,"10.06;.11","IE","GET")
 S OPDT=$G(GET(FIL,ROIS,10.06,"I")),REQTR=$G(GET(FIL,ROIS,.11,"E")),STAT=$P($$STONDAT^DSIROI6(ROI,DT),U,2)
 Q
DIVLOAD ;Load the array DIVS with the internal division numbers passed in for multi-divisional processing.
 S MDIV=$D(^XUSEC("DSIR MDIV",DUZ)),DIVS=$G(DIVL)]""
 I DIVS F II=1:1:$L(DIVL,U) S:$P(DIVL,U,II) DIVS($P(DIVL,U,II))=""
 Q
BAILOUT ; CLEAN UP CODE AFTER A STOP
 K ^TMP("DSIR",$J)
 Q
