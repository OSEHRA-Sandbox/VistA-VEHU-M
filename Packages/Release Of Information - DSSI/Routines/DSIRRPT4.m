DSIRRPT4 ;EWL - Document Storage Systems; ROI Report RPC'S ;07/01/2011 11:18
 ;;8.2;RELEASE OF INFORMATION - DSSI;;Nov 08, 2011;Build 25
 ;Copyright 1995-2012, Document Storage Systems, Inc., All Rights Reserved
 ;
 ;DBIA# Supported Reference
 ;----- --------------------------------
 ;2053  WP^DIE
 ;2056  GETS^DIQ
 ;10103 FMDIFF^XLFDT
 ;10046 EN^XUWORKDY
 Q
 ;************************
 ; Requests by clerk report
EXP(AXY,FRDT,TODT,DIV,SCHED,ESTART)   ;RPC - DSIRRPT4 EXP EXPEDITED REPORT
 ;Input Parameters
 ;  FRDT - From Date - FileMan Format - No time
 ;  TODT - To Date - FileMan Format - No time
 ;  DIV  - Division - String with selected division
 ;  SCHED  Schedule - Boolean for scheduled or immediate run
 ;         1 = Schedule / 0 or Null = Run Immediately
 ;  ESTART Earliet time to start the scheaduled task
 ;
 ; RETURN ARRAY
 ; if immediate ^ delimited 2 parts
 ;
 ; PART 1 - one record only
 ;  1. AVG Days top adjucicate
 ;  2. Median days to adjudicate
 ;  3. AVG days to grant
 ;  4. Median days to grant
 ;  5. Average days to close
 ;  6. Median days to close
 ;
 ; PART 2 - one or more records
 ;   1. Patient
 ;   2. SSN
 ;   3. DateRequestReceived
 ;   4. DateExpeditedRequested
 ;   5. ExpediteAdjudicated
 ;   6. DateAdjudicated
 ;   7. Granted
 ;   8. LastClosedDate
 ;   9. CurrentStatus
 ;  10. PatientType
 ;  11. Division
 ;  12. Requestor
 ;  13. Denied
 ;  14. CalendarDaysToAdjudicate
 ;  15. WorkdaysToClose
 ;  16. Adjudicated Count
 ;  17. Granted Count
 ;  18. Closed Count
 ;   or
 ;   if scheduled
 ;   Task^REPORT AFTER yyymmdd.hhmmss
 ;   or
 ;   -1^error message
 ;
 S AXY=$NA(^TMP("DSIRRPT4EXP",$J)) K @AXY
 S FRDT=$G(FRDT),TODT=$G(TODT)
 I (FRDT']"")!(TODT']"") S @AXY@(1)="-1^BOTH FROM DATE AND TO DATE ARE REQUIRED FIELDS." Q
 I $G(DIV)']"" S DIV=DUZ(2)
 S SCHED=+$G(SCHED) I 'SCHED D EXP3(.AXY,FRDT,TODT,DIV)
 I SCHED D
 .N RPT,EMSG,PARRAY,I
 .;CREATE THE REPORT SCHEDULE RECORD, FILE THE PARAMETERS, AND SUBMIT
 .S ESTART=$G(ESTART),RPT="EXPEDITED REPORT",IEN=$$UDSCHED^DSIRRPTR(RPT,.ESTART)
 .S PARRAY(1)="S FRDT="_$G(FRDT),PARRAY(2)="S TODT="_$G(TODT),PARRAY(3)="S DIV="""_$G(DIV)_""""
 .D PREPSUB^DSIRRPTR(.AXY,ESTART,RPT,"EXP2^DSIRRPT4",IEN,.PARRAY)
 Q
 ;************************
EXP2(SIEN) ; THIS IS LAUNCHED BY THE TASK MANAGER AND IT REASSEMBLES THE PARAMETERS
 N DONE,FRDT,TODT,DIVL,REQS,PRMS,RET S DONE=0,RET="TASK",ZTREQ="@" I '$$INIT^DSIRRPTR(SIEN) Q
 D EXP3(.RET,FRDT,TODT,DIV)  I 'DONE,'$$STOPCHK^DSIRRPTR(SIEN) D RPTDONE^DSIRRPTR(SIEN) ;
 Q
 ;************************
EXP3(AXY,FRDT,TODT,DIV) ;
 N CDIV,FI,IEN,IENS,GBLA,GBLG,GBLC,FLDS,GET,EMSG,CTR,FI,ADAYSUM,GDAYSUM,CDAYSUM,ACT,GCT,CCT,CTR
 N NAME,SSN,RDT,EXPR,EXPA,ADJDT,EXPG,CLDT,STAT,PTYP,DIVSN,RQSTR
 N TASK,DEN,CAL2ADJ,WDY2CLS,ADJCT,GRCT,CLCT,AVGA,AVGG,AVGC,MEDA,MEDG,MEDC,LOOPCT,LOOPCHK
 S LOOPCT=1,LOOPCHK=50,CDIV=DUZ(2),(ADAYSUM,GDAYSUM,CDAYSUM,ACT,GCT,CCT,CTR,ADJCT,GRCT,CLCT,AVGA,AVGG,AVGC,MEDA,MEDG,MEDC)=0
 S TASK=($G(AXY)="TASK") I TASK S AXY=$NA(^TMP("DSIRRPT4EXP",$J))
 ;
 S GBLA=$NA(^TMP("DSIREXPA",$J)),GBLG=$NA(^TMP("DSIREXPG",$J)),GBLC=$NA(^TMP("DSIREXPC",$J)) K @GBLA,@GBLG,@GBLC
 S FI="19620",DIV=$G(DIV) S:DIV="" DIV=CDIV
 S FLDS=".01;203;10.06;.06;.07;.08;.05;10.07;10.08;204;.63;.11;.05",IEN=0
 ;
 F  S IEN=$O(^DSIR(FI,"AEXPROC",1,IEN)) Q:('IEN)!DONE  S IENS=IEN_",",CTR=CTR+1 D
 .I TASK S LOOPCT=LOOPCT+1 I '(LOOPCT#LOOPCHK),SIEN,$$STOPCHK^DSIRRPTR(SIEN) S DONE=1 D CLEANUP Q
 .K GET D GETS^DIQ(FI,IENS,FLDS,"IE","GET","EMSG")
 .; QUIT IF OUTSIDE OF DATE RANGE, WRONG DIVISION, CANDELLED OR ERROR
 .Q:(GET(FI,IENS,10.06,"I")<FRDT)!(GET(FI,IENS,10.06,"I")>TODT)!(GET(FI,IENS,.63,"E")=DIV)
 .S STAT=GET(FI,IENS,10.08,"E") Q:"CANCELLEDERROR"[STAT
 .;
 .; IF NOT ADJUDICATED
 .I 'GET(FI,IENS,.07,"I") S EXPG="NO",DEN="NO",CAL2ADJ="",ADJDT="",ADJCT=0,GRCT=0
 .; IF ADJUDICATED
 .I GET(FI,IENS,.07,"I") S ADJDT=GET(FI,IENS,.08,"E"),ACT=ACT+1,ADJCT=1 D
 ..S CAL2ADJ=$$FMDIFF^XLFDT(GET(FI,IENS,.08,"I"),GET(FI,IENS,.06,"I"),1) S:CAL2ADJ=0 CAL2ADJ=1
 ..S ADAYSUM=ADAYSUM+CAL2ADJ,@GBLA@(CAL2ADJ,CTR)=""
 ..I GET(FI,IENS,.05,"I") S EXPG="YES",DEN="NO",GCT=GCT+1,GRCT=1,GDAYSUM=GDAYSUM+CAL2ADJ,@GBLG@(CAL2ADJ,CTR)=""
 ..I 'GET(FI,IENS,.05,"I") S EXPG="NO",DEN="YES",GRCT=0
 .; GET THE REST OF THE FIELDS
 .S NAME=GET(FI,IENS,.01,"E"),EXPR=GET(FI,IENS,.06,"E"),EXPA=GET(FI,IENS,.07,"E")
 .S RQSTR=GET(FI,IENS,.11,"E"),SSN=GET(FI,IENS,203,"E"),PTYP=GET(FI,IENS,204,"E")
 .S DIVSN=GET(FI,IENS,.63,"E"),RDT=GET(FI,IENS,10.06,"E"),CLDT=GET(FI,IENS,10.07,"E")
 .;CLOSED REWQUEST PROCESSING
 .I CLDT']"" S WDY2CLS="",CLCT=0
 .I CLDT]"" D
 ..S WDY2CLS=$$EN^XUWORKDY(GET(FI,IENS,10.06,"I"),GET(FI,IENS,10.07,"I"))
 ..S CCT=CCT+1,CLCT=1 S:WDY2CLS=0 WDY2CLS=1
 ..S CDAYSUM=CDAYSUM+WDY2CLS,@GBLC@(WDY2CLS,CTR)=""
 .;ASSEMBLE AND WRITE OUTPUT
 .S @AXY@(IEN+1)=NAME_U_SSN_U_RDT_U_EXPR_U_EXPA_U_ADJDT_U_EXPG_U_CLDT_U_STAT_U_PTYP_U_DIVSN_U_RQSTR_U_DEN_U_CAL2ADJ_U_WDY2CLS_U_ADJCT_U_GRCT_U_CLCT
 Q:DONE
 I '$D(@AXY) S @AXY@(1)="-1^No records found for sort criteria!"
 ;STATISTICS
 S AVGA="",MEDA="",AVGG="",MDAG="",AVGC="",MEDC=""
 S:ACT AVGA=ADAYSUM/ACT,MEDA=$$MEDCALC^DSIRRPTR(GBLA,ACT,3)
 S:GCT AVGG=GDAYSUM/GCT,MEDG=$$MEDCALC^DSIRRPTR(GBLG,GCT,3)
 S:CCT AVGC=CDAYSUM/CCT,MEDC=$$MEDCALC^DSIRRPTR(GBLC,CCT,3)
 S @AXY@(1)=$FN(AVGA,"",2)_U_$FN(MEDA,"",2)_U_$FN(AVGG,"",2)_U_$FN(MEDG,"",2)_U_$FN(AVGC,"",2)_U_$FN(MEDC,"",2)
 M ^TMP("TSK")=^TMP("DSIRRPT4EXP",$J)
 I TASK,'DONE N CT D WP^DIE(19620.35,SIEN_",",2,"",AXY,"EMSG")
 D CLEANUP
 Q
CLEANUP ; DELETE ^TMP GLOBAL ENTRIES
 K @GBLA,@GBLG,@GBLC ;,@AXY
 Q
 ;************************
CDD(AXY,FRDT,TODT,CLRK,SCHED,ESTART)   ;RPC - DSIRRPT4 CDD DISCREPANCY RPT
 ;Input Parameters
 ;  FRDT - From Date - FileMan Format - No time
 ;  TODT - To Date - FileMan Format - No time
 ;  CLRK  - Array, each element set to the file 200 pointer (DUZ) of selected clerks
 ;  SCHED  Schedule - Boolean for scheduled or immediate run
 ;         1 = Schedule / 0 or Null = Run Immediately
 ;  ESTART Earliet time to start the scheaduled task
 ;
 ; RETURN ARRAY
 ; if immediate ^ delimited 2 parts
 ;
 ;   Clerk Name ^ Request Name ^ Open Date ^ Closed Date ^ Status Code ^ Status Date ^ Date User Entered Status
 ;
 ;   or
 ;   if scheduled
 ;   Task^REPORT AFTER yyymmdd.hhmmss
 ;
 ;   or
 ;   -1^error message
 ;
 S AXY=$NA(^TMP("DSIRRPT4CDD",$J)),SCHED=+$G(SCHED) K @AXY
 S FRDT=$G(FRDT),TODT=$G(TODT) I $G(TODT)']"" S TODT=DT
 I FRDT']"" S @AXY@(1)="-1^FROM DATE IS A REQUIRED FIELD." Q
 I 'SCHED D CDD3(.AXY,FRDT,TODT,.CLRK)
 I SCHED D
 .N RPT,EMSG,PARRAY,I
 .;CREATE THE REPORT SCHEDULE RECORD, FILE THE PARAMETERS, AND SUBMIT
 .S ESTART=$G(ESTART),RPT="CLOSED DATE DISCREPANCY",IEN=$$UDSCHED^DSIRRPTR(RPT,.ESTART)
 .S PARRAY(1)="S FRDT="_$G(FRDT),PARRAY(2)="S TODT="_$G(TODT)
 .S I=0 F  S I=$O(CLRK(I)) Q:'I  S PARRAY(I+2)="S CLRK("_I_")="_CLRK(I)
 .D PREPSUB^DSIRRPTR(.AXY,ESTART,RPT,"CDD2^DSIRRPT4",IEN,.PARRAY)
 Q
 ;************************
CDD2(SIEN) ; THIS IS LAUNCHED BY THE TASK MANAGER AND IT REASSEMBLES THE PARAMETERS
 N DONE,FRDT,TODT,DIVL,REQS,PRMS,RET S DONE=0,RET="TASK",ZTREQ="@" I '$$INIT^DSIRRPTR(SIEN) Q
 D CDD3(.RET,FRDT,TODT,.CLRK)  I 'DONE,'$$STOPCHK^DSIRRPTR(SIEN) D RPTDONE^DSIRRPTR(SIEN) ;
 Q
 ;************************
CDD3(AXY,STDT,ENDT,DIV,CLRK) ; CLOSED DATE DISCREPANCY REPORT
 ;       
 N LOOPCT,LOOPCHK S LOOPCT=1,LOOPCHK=50
 N TASKIEN,FIL,XX,YY,LOOP,XREF,CLRKS,ALLCLRK S YY=0,FIL=19620.91,XREF="AST"
 S DIV=DUZ(2),ALLCLRK=$O(CLRK(""))="",ENDT=$G(ENDT) S:'ENDT ENDT=DT
 S TASK=($G(AXY)="TASK") I TASK S AXY=$NA(^TMP("DSIRRPT4CDD",$J))
 I 'ALLCLRK S XX=0 F  S XX=$O(CLRK(XX)) Q:XX=""  S CLRKS(CLRK(XX))=""
 S XREF=XREF_$S(ALLCLRK:"DTCL",1:"CLDT")
 I 'ALLCLRK D  S:'YY @AXY@(1)="-1^No Records Found For Specified Clerk(s)!" Q
 .S CL=0 F  S CL=$O(CLRKS(CL)) Q:('CL)!DONE  D
 ..I TASK S LOOPCT=LOOPCT+1 I '(LOOPCT#LOOPCHK),$G(SIEN),$$STOPCHK^DSIRRPTR(SIEN) S DONE=1 Q
 ..S LODT=STDT-.3 F  S LODT=$O(^DSIR(FIL,XREF,"C",CL,LODT)) Q:('LODT)!(LODT>ENDT)!DONE  D
 ...I TASK S LOOPCT=LOOPCT+1 I '(LOOPCT#LOOPCHK),$G(SIEN),$$STOPCHK^DSIRRPTR(SIEN) S DONE=1 Q
 ...S IEN=0 F  S IEN=$O(^DSIR(FIL,XREF,"C",CL,LODT,IEN)) Q:('IEN)!DONE  D
 ....I TASK S LOOPCT=LOOPCT+1 I '(LOOPCT#LOOPCHK),$G(SIEN),$$STOPCHK^DSIRRPTR(SIEN) S DONE=1 Q
 ....Q:$P($G(^DSIR(FIL,IEN,0)),U,3)=$P($P($G(^DSIR(FIL,IEN,0)),U,5),".")
 ....N CSTAT S CSTAT=$E($$CURSTAT^DSIROI6($P($G(^DSIR(FIL,IEN,0)),U))) Q:"CEX"'[CSTAT
 ....N GET,IENS,RQIEN D GET
 ....S YY=YY+1,@AXY@(YY)=$$SET()
 Q:DONE
 S LODT=STDT-.3 F  S LODT=$O(^DSIR(FIL,XREF,"C",LODT)) Q:('LODT)!(LODT>ENDT)!DONE  D
 .I TASK S LOOPCT=LOOPCT+1 I '(LOOPCT#LOOPCHK),$G(SIEN),$$STOPCHK^DSIRRPTR(SIEN) S DONE=1 Q
 .S CL=0 F  S CL=$O(^DSIR(FIL,XREF,"C",LODT,CL)) Q:('CL)!DONE  D
 ..I TASK S LOOPCT=LOOPCT+1 I '(LOOPCT#LOOPCHK),$G(SIEN),$$STOPCHK^DSIRRPTR(SIEN) S DONE=1 Q
 ..S IEN=0 F  S IEN=$O(^DSIR(FIL,XREF,"C",LODT,CL,IEN)) Q:('IEN)!DONE  D
 ...I TASK S LOOPCT=LOOPCT+1 I '(LOOPCT#LOOPCHK),$G(SIEN),$$STOPCHK^DSIRRPTR(SIEN) S DONE=1 Q
 ...Q:$P($G(^DSIR(FIL,IEN,0)),U,3)=$P($P($G(^DSIR(FIL,IEN,0)),U,5),".")
 ...N GET,IENS,RQIEN D GET
 ...S YY=YY+1,@AXY@(YY)=$$SET()
 Q:DONE
 S:'YY @AXY@(1)="-1^No records found within your search criteria."
 I TASK,'DONE N CT D WP^DIE(19620.35,SIEN_",",2,"",AXY,"EMSG")
 I TASK K @AXY
 Q
 ;************************
GET ;
 S IENS=IEN_"," D GETS^DIQ(FIL,IENS,"*","EI","GET")
 S RQIEN=+$G(GET(FIL,IENS,.01,"I"))_","
 Q:'RQIEN  D GETS^DIQ(19620,RQIEN,"10.06;10.07","EI","GET")
 Q
 ;************************
SET() ;
 N RET S RET=$G(GET(FIL,IENS,.04,"E"))_U_$G(GET(FIL,IENS,.01,"E"))_U_$G(GET(19620,RQIEN,10.06,"E"))
 S RET=RET_U_$G(GET(19620,RQIEN,10.07,"E"))_U_$G(GET(FIL,IENS,.02,"E"))_U_$G(GET(FIL,IENS,.03,"E"))
 S RET=RET_U_$G(GET(FIL,IENS,.05,"E")) Q RET
