AXAPCHS1 ;WPB/JLTP ; PATCH SERVER FUNCTIONS ; 24-JUL-98
 ;;2.0;WPB Patch Tracking;10-SEP-1998;;Build 2
XMZ(X,Z) ;------ Send Patch Message Info To Other Account ------
 N DOM,XMZ,XMTEXT,XMY,XMSUB,XMDUZ,XMDUN,TEXT
 D DOM^AXAPCHU
 S XMSUB="PATCH SERVER MESSAGE",(XMDUZ,XMDUN)="WPB PATCHMAN"
 S TEXT(1,0)="$AXAPCH XMZ"
 S TEXT(2,0)=X_U_Z_U_$G(TVER)_U_DOM(0)
 S XMTEXT="TEXT("
 S X="S.AXAPCHS XMZ@"_$S(DOM(0)="TEST":DOM(2),1:DOM(1)),XMY(X)=""
 D ^XMD
 Q
MN(T,L) ;------ Make Patch Notes ------
 N DOM,I,TEXT,XMZ,XMTEXT,XMY,XMSUB,XMDUZ,XMDUN
 D DOM^AXAPCHU
 S MSGL=$S(DOM(0)="LIVE":L,1:T),MSGR=$S(DOM(0)="LIVE":T,1:L)
 K ^TMP("TEXT",$J,0) M TEXT=^TMP("TEXT",$J)
 S:$D(^XMB(3.9,MSGL,0)) X=$$ENT^XMA2R(MSGL,"Patch Note",.TEXT,"",DUZ)
 K TEXT
 S TEXT(1,0)="$AXAPCH NOTE"
 S TEXT(2,0)=MSGR_U_$P($G(^VA(200,+DUZ,0)),U)
 F I=0:0 S I=$O(^TMP("TEXT",$J,I)) Q:'I  S TEXT(I+2,0)=^(I,0)
 S XMTEXT="TEXT("
 S X="S.AXAPCHS XMZ@"_$S(DOM(0)="TEST":DOM(2),1:DOM(1)),XMY(X)=""
 S XMSUB="PATCH SERVER MESSAGE",(XMDUZ,XMDUN)="WPB PATCHMAN"
 D ^XMD
 Q
DIE(DA) ;------ Send Patch Updates To Other Account ------
 N DOM,F,FIELD,XMZ,XMTEXT,XMY,XMSUB,XMDUZ,XMDUN,TEXT
 D DOM^AXAPCHU
 Q:$D(AXAPCH("SERVER"))
 S F=$G(AXAPCH("FROM")),FIELD=$S(F="TEST":2.01,F="LIVE":2.03,F="OK":2.05,1:2.07)
 S X=$P($G(^AXA(548261,+$G(DA),2)),U,FIELD-2*100) Q:X=""
 S VAL=$S(FIELD=2.07:X,1:$$FMTE^XLFDT(X)),X=^AXA(548261,DA,0)
 S WHO=$P($G(^VA(200,DUZ,0)),U),DESIG=$P(X,U),TV=$P(X,U,4)
 S XMSUB="PATCH SERVER MESSAGE",(XMDUZ,XMDUN)="WPB PATCHMAN"
 S TEXT(1,0)="$AXAPCH DIE"
 S TEXT(2,0)=DESIG_U_TV_U_WHO_U_FIELD_U_VAL
 S XMTEXT="TEXT("
 S X="S.AXAPCHS XMZ@"_$S(DOM(0)="TEST":DOM(2),1:DOM(1)),XMY(X)=""
 D ^XMD
 K AXAPCH("FROM")
 Q
XMZR ;------ Receive Patch Message Info ------
 N DA,DESIG,DIE,DR,FROM,MSG,TV,X
 S X=^XMB(3.9,XMZ,2,1,0)
 I X="$AXAPCH DIE" D DIER Q
 I X="$AXAPCH NOTE" D RNOTE Q
 I X="$AXAPCHPKG XMZ" D PKGRCV Q
 I X'="$AXAPCH XMZ" Q
 S X=^XMB(3.9,XMZ,2,2,0)
 S DESIG=$P(X,U),MSG=$P(X,U,2),TV=$P(X,U,3),FROM=$P(X,U,4)
 S DA=$O(^AXA(548261,"B",DESIG,""),-1) I 'DA D REQ("XMZR") Q
 S X=^AXA(548261,DA,0) I $P(X,U,4)'=TV D REQ("XMZR") Q
 S DIE="^AXA(548261,",DR=$S(FROM="TEST":.19,1:.2)_"///^S X=MSG"
 D ^DIE
 Q
RNOTE ;------ Receive Patch Note ------
 N MSG S MSG=XMZ
 N DOM,I,TEXT,X,XMZ,XMTEXT,XMY,XMSUB,XMDUZ,XMDUN
 S X=$G(^XMB(3.9,+MSG,2,2,0)),XMZ=+X,XMDUN=$P(X,U,2)
 S I=2,X=0 F  S I=$O(^XMB(3.9,MSG,2,I)) Q:'I  S X=X+1,TEXT(X,0)=^(I,0)
 S:$D(^XMB(3.9,XMZ,0)) X=$$ENT^XMA2R(XMZ,"Patch Note",.TEXT,"",XMDUN)
 Q
DIER ;------ Update Patch File ------
 N AXAPCH,FIELD,OLDDUZ,VAL,WHO,Y
 S X=^XMB(3.9,XMZ,2,2,0),AXAPCH("SERVER")=1
 S DESIG=$P(X,U),TV=$P(X,U,2),WHO=$P(X,U,3),FIELD=$P(X,U,4),VAL=$P(X,U,5)
 S (DA,X)=""
 F  S X=$O(^AXA(548261,"B",DESIG,X),-1) Q:X=""  D  Q:DA
 .I $P($G(^AXA(548261,X,0)),U,4)=TV S DA=X
 I 'DA Q
 S AXAPCH=1,DIE="^AXA(548261,",DR=FIELD_"///^S X=VAL" D ^DIE
 Q
REQ(O) ;------ Requeue Server Option ------
 S ZTRTN=O_U_"AXAPCHS1",ZTDESC="PATCH SERVER",ZTSAVE("*")="",ZTIO=""
 S REDO=$G(REDO)+1 I REDO>10 D  Q
 .I REDO>12 Q
 .S ZTDTH=($H+1)_","_$P($H,",",2)
 .D ^%ZTLOAD
 H 60 S ZTDTH=$H D ^%ZTLOAD
 Q
PKGEDT ;------ Enter/Edit Patch Package File ------
 N DA,DIE,DR,OLDNAME,PATCH
 Q:'$$DIC^AXAFM(548260,.PATCH,"",1)
 S OLDNAME=$P($G(^AXA(548260,+PATCH,0)),U)
 S DA=+PATCH,DIE="^AXA(548260,",DR=".01:.19" D ^DIE,PKGSND
 G PKGEDT
PKGSND ;------ Send Patch Package Update ------
 N DOM,F,XMZ,XMTEXT,XMY,XMSUB,XMDUZ,XMDUN,TEXT,X
 D DOM^AXAPCHU
 S XMSUB="PATCH PACKAGE SERVER MESSAGE",(XMDUZ,XMDUN)="WPB PATCHMAN"
 S TEXT(1,0)="$AXAPCHPKG XMZ"
 S TEXT(2,0)=$G(^AXA(548260,+PATCH,0))
 S TEXT(3,0)=$G(OLDNAME)
 S X=TEXT(2,0) F I=3,4,5,6 D
 .;I $P(TEXT(2,0),U,I)]"" S $P(TEXT(2,0),U,I)=$P($G(^VA(200,+$P(TEXT(2,0),U,I),0)),U)
 .I $P(X,U,I)]"" S F=$O(^DD(548260,"GL",0,I,0)),$P(X,U,I)=$$IVP2E(F,$P(X,U,I))
 S TEXT(2,0)=X
 S XMTEXT="TEXT("
 S X="S.AXAPCHS XMZ@"_$S(DOM(0)="TEST":DOM(2),1:DOM(1)),XMY(X)=""
 D ^XMD
 Q
PKGRCV ;------ Receive Patch Package Update ------
 S ZNODEN=^XMB(3.9,XMZ,2,2,0),OLD=^XMB(3.9,XMZ,2,3,0),NEW=$P(ZNODEN,U)
 S DA=$O(^AXA(548260,"B",OLD,0))
 I 'DA,NEW]"" S DA=$O(^AXA(548260,"B",NEW,0))
 I 'DA Q:'$$DIC^AXAFM(548260,.Y,NEW,1,"L")  S DA=+Y
 S ZNODEO=$G(^AXA(548260,DA,0))
 F PIECE=1:1:19 D
 .S FLD=PIECE/100,NEW=$P(ZNODEN,U,PIECE),OLD=$P(ZNODEO,U,PIECE)
 .I NEW=OLD Q
 .S NEW=$C(34)_NEW_$C(34)
 .S SL=$S("3^4^5^6"[PIECE:"///",1:"////")
 .S:NEW="" NEW="@",SL="///" S DR=FLD_SL_"^S X="_NEW
 .D DIE^AXAFM(548260,DA,DR)
 Q
IVP2E(F,IVP) ; Convert internal variable pointer to external
 N DD,EV,GRT,PF,PFI
 S IFN=+IVP,GRT=U_$P(IVP,";",2),DD=+$P(@(GRT_"0)"),U,2)
 S PFI=+$O(^DD(548260,F,"V","B",DD,0)),PF=$P(^DD(548260,F,"V",PFI,0),U,4)
 S EV=$E($P(@(GRT_IFN_",0)"),U),1,30)
 Q PF_"."_EV
