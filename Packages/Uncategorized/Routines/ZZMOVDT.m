ZZMOVDT ; SEB - Move dates for various data domains
 ;;1.0;VistA Data Support;;JUNE 20, 2015;Build 18
APPT(RETURN,DFN,APPTDT,CLINIC,VALUE) ; Move appointment
 ;^DPT(DFN,"S",APPTDT)
 ;^DPT("ASADM",ENTRYDATE,DFN,APPTDT)=""
 ;^SC(CLINICID,"S",APPTDT)
 ;VALUE should be a MUMPS date, a date offset, or "TODAY"+-a date offset
 I $G(DFN)=""!($G(APPTDT)="")!($G(CLINIC)="")!($G(VALUE)="") S RETURN="-1^Missing parameter(s)!" Q
 S (RETURN,NEWDT)=""
 I $E(VALUE,1,8)?7N1"." S NEWDT=VALUE
 E  I +VALUE=VALUE S NEWDT=$$CALCDT(APPTDT,VALUE)
 I $E(VALUE,1,5)="TODAY" D
 . D NOW^%DTC S NEWDT=X,$P(NEWDT,".",2)=$P(APPTDT,".",2)
 . S OFFSET=0 I "+-"[$E(VALUE,6) S OFFSET=$E(VALUE,6,99)
 . S NEWDT=$$CALCDT(NEWDT,OFFSET)
 . Q
 I NEWDT="" S RETURN="-1^Invalid parameters!" Q
 I '$D(^DPT(DFN,"S",APPTDT)) S RETURN="-1^Missing appointment data in patient file!" Q
 I '$D(^SC(CLINIC,"S",APPTDT)) S RETURN="-1^Missing appointment data in clinic file!" Q
 D PATIENT(.RETURN,DFN,CLINIC,APPTDT,NEWDT)
 I RETURN="" D CLINIC(.RETURN,CLINIC,DFN,APPTDT,NEWDT)
 S RETURN="1^OK"
 Q
 ;
ALLAPPT(RETURN,DFN,OFFSET) ; Move all appointments for one patient
 S OFFSET=$G(OFFSET),(RETURN,TODO)="",APPTDT=0 K TODO
 F  S APPTDT=$O(^DPT(DFN,"S",APPTDT)) Q:APPTDT=""  D
 . S CLINIC=+$G(^DPT(DFN,"S",APPTDT,0)) I CLINIC=0 S RETURN="-1^Missing clinic!" Q
 . S VALUE=OFFSET I VALUE="" S VALUE=$G(^ZZEHMP("ZZMOVDT",DFN,CLINIC)) I $E(VALUE,1,5)'="TODAY" S VALUE=""
 . I VALUE]"" S TODO(APPTDT,CLINIC)=VALUE
 . Q
 I RETURN]"" Q
 S APPTDT=0 F  S APPTDT=$O(TODO(APPTDT)) Q:APPTDT=""  D
 . S CLINIC=0 F  S CLINIC=$O(TODO(APPTDT,CLINIC)) Q:CLINIC=""  D
 . . S VALUE=TODO(APPTDT,CLINIC)
 . . D APPT(.RETURN,DFN,APPTDT,CLINIC,VALUE)
 . . W !,DFN,?8,APPTDT,?25,CLINIC
 . . Q
 . Q
 I RETURN="" S RETURN="1^OK"
 W !,RETURN
 Q
 ;
CALCDT(X1,X2) ; Return date + offset
 ;S NEWDT=DATE+OFFSET
 D C^%DTC S NEWDT=X
 Q NEWDT
 ;
PATIENT(RETURN,PATID,CLINIC,APPTDT,NEWDT)
 S ENTRYDT=$P($G(^DPT(PATID,"S",APPTDT,0)),"^",19)
 I ENTRYDT="" S RETURN="-1^No entry date!" Q
 ;
 I $D(^DPT(PATID,"S",NEWDT)) S RETURN="-1^An appointment already exists at the new date/time!" Q
 ;W !,CT," - PATIENT: ",PATID," / ",CLINIC," / ",APPTDT," / ",NEWDT Q
 M ^DPT(PATID,"S",NEWDT)=^DPT(PATID,"S",APPTDT)
 K ^DPT(PATID,"S",APPTDT)
 S ^DPT("ASADM",ENTRYDT,PATID,NEWDT)=""
 K ^DPT("ASADM",ENTRYDT,PATID,APPTDT)
 D ENCVSIT(PATID,APPTDT,NEWDT)
 Q
 ;
ENCVSIT(PATID,APPTDT,NEWDT)
 ;S ENCID=$P(^DPT(PATID,"S",NEWDT,0),"^",20) I ENCID]"" S VISITID=$P($G(^SCE(ENCID,0)),"^",5)
 S REVDT=9999999-$P(APPTDT,".")_"."_$P(APPTDT,".",2)
 S ENCID=$O(^SCE("ADFN",PATID,APPTDT,"")),VISITID=$O(^AUPNVSIT("AA",PATID,REVDT,""))
 I ENCID]"" S FDA(1,409.68,ENCID_",",.01)=NEWDT
 I VISITID]"" S FDA(1,9000010,VISITID_",",.01)=NEWDT
 D FILE^DIE(,"FDA(1)")
 Q
 ;
CLINIC(RETURN,CLINIC,PATID,APPTDT,NEWDT)
 S APPTID=0
 F  S APPTID=$O(^SC(CLINIC,"S",APPTDT,1,APPTID)) Q:APPTID=""  S APPTDATA=$G(^SC(CLINIC,"S",APPTDT,1,APPTID,0)) Q:+APPTDATA=PATID
 I APPTDATA="" S RETURN="-1^Missing appointment data in clinic file!" Q
 ;W !,CT," - CLINIC: ",CLINIC," / ",PATID," / ",APPTDT," / ",NEWDT Q
 I '$D(^SC(CLINIC,"S",NEWDT)) S ^SC(CLINIC,"S",NEWDT,0)=NEWDT,^SC(CLINIC,"S",NEWDT,1,0)="^44.003PA^^"
 F NEWAPPTID=1:1 I '$D(^SC(CLINIC,"S",NEWDT,1,NEWAPPTID,0)) Q
 M ^SC(CLINIC,"S",NEWDT,1,NEWAPPTID)=^SC(CLINIC,"S",APPTDT,1,APPTID)
 K ^SC(CLINIC,"S",APPTDT,1,APPTID)
 Q
 ;
PERF(COUNT,MOVE) ; Locate patients to move appointments for Performance Testing team
 K ^ZZEHMP("PERF") D NOW^%DTC S TODAY=$P(X,".")
 S (DFN,TOTAL)=0 F  S DFN=$O(^DPT(DFN)) Q:DFN=""  D
 . S APPTDT=3100101 F  S APPTDT=$O(^DPT(DFN,"S",APPTDT)) Q:APPTDT=""!(APPTDT>TODAY)!(TOTAL>COUNT)  D
 . . S DATA=$G(^DPT(DFN,"S",APPTDT,0)) I $P(DATA,"^",2)'="NT" Q
 . . S CLINIC=+DATA
 . . S UNIQUE=0
 . . F DAY=1:1:10 D  Q:UNIQUE=1
 . . . D NOW^%DTC S NEWDT=X,$P(NEWDT,".",2)=$P(APPTDT,".",2),NEWDT=$$CALCDT(NEWDT,DAY)
 . . . I '$D(^ZZEHMP("PERF",DFN,NEWDT)),'$D(^DPT(DFN,"S",NEWDT)) S UNIQUE=1
 . . I UNIQUE=0 Q
 . . S TOTAL=TOTAL+1
 . . S ^ZZEHMP("PERF",DFN,NEWDT,CLINIC)=APPTDT
 . . Q
 . Q
 I $G(MOVE)=1 D PERFMV
 Q
 ;
PERFMV ; Move appointments to T+1 through T+5 for Performance Testing team
 S DFN=0,CT=0 F  S DFN=$O(^ZZEHMP("PERF",DFN)) Q:DFN=""  D
 . S NEWDT=0 F  S NEWDT=$O(^ZZEHMP("PERF",DFN,NEWDT)) Q:NEWDT=""  D
 . . S CLINIC=0 F  S CLINIC=$O(^ZZEHMP("PERF",DFN,NEWDT,CLINIC)) Q:CLINIC=""  D
 . . . S APPTDT=^ZZEHMP("PERF",DFN,NEWDT,CLINIC)
 . . . D APPT^ZZMOVDT(.ZZZ,DFN,APPTDT,CLINIC,NEWDT)
 . . . S CT=CT+1 W !,ZZZ
 . . . I +ZZZ'=1 W !,DFN,?10,APPTDT,?20,CLINIC,?30,NEWDT,?40,ZZZ
 . . . Q
 . . Q
 . Q
 Q
 ;
REPORT ; Display report of patients found by process
 W !,"PAT ID",?8,"NAME",?37,"CLINIC",?58,"ORIGINAL DATE",?80,"NEW DATE"
 S DFN=0,CT=0 F  S DFN=$O(^ZZEHMP("PERF",DFN)) Q:DFN=""  D
 . S NEWDT=0 F  S NEWDT=$O(^ZZEHMP("PERF",DFN,NEWDT)) Q:NEWDT=""  D
 . . S CLINIC=0 F  S CLINIC=$O(^ZZEHMP("PERF",DFN,NEWDT,CLINIC)) Q:CLINIC=""  D
 . . . S APPTDT=^ZZEHMP("PERF",DFN,NEWDT,CLINIC)
 . . . S Y=APPTDT X ^DD("DD") S ORG=Y
 . . . S Y=NEWDT X ^DD("DD") S NEW=Y
 . . . W !,DFN,?8,$P($G(^DPT(DFN,0)),"^"),?37,$P($G(^SC(CLINIC,0)),"^"),?58,ORG,?80,NEW
 . . . Q
 . . Q
 . Q
 Q
 
