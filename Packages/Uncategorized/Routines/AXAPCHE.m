AXAPCHE ;WPB/GBH - PRINT PATCH LIST (FROM AXAPCHD)
 ;;2.0;WPB Patch Tracking;10-SEP-1998;;Build 2
 ;
 S COLUMNS="COLS"_XSTAT
 S FIELDS="FLDS"_XSTAT
 D HEADER
 D PRINT
 Q
COLS1 ;
 W "DESIGNATION",?16,"SEQ",?24,"PATCH SUBJECT"
 W ?57,"MSG#(TEST)",?70,"MSG#(LIVE)"
 Q
FLDS1 ;
 W !,DESIG,?16,SEQ,?24,$E(SUBJ,1,30),?57,$J(MSGTEST,7),?70,$J(MSGLIVE,7)
 Q
COLS2 ;
 W "DESIGNATION",?16,"SEQ",?24,"IN TEST",?35,"ADPAC OK",?46,"IN LIVE"
 W ?57,"MSG#(TEST)",?69,"MSG#(LIVE)"
 Q
FLDS2 ;
 W !,DESIG,?16,SEQ,?24,INTEST,?35,OKFOR,?46,INLIVE
 W ?57,$J(MSGTEST,7),?70,$J(MSGLIVE,7)
 Q
COLS3 ;
 W "DESIGNATION",?16,"SEQ",?24,"IN TEST",?35,"ADPAC OK",?46,"IN LIVE"
 W ?57,"MSG#(TEST)",?69,"MSG#(LIVE)"
 Q
FLDS3 ;
 W !,DESIG,?16,SEQ,?24,INTEST,?35,OKFOR,?46,INLIVE
 W ?57,$J(MSGTEST,7),?70,$J(MSGLIVE,7)
 Q
HEADER ;
 W @IOF
 S LINE="",$P(LINE,"-",81)=""
 W !,?30,"P A T C H    L I S T",!
 W !,"PROGRAMMER:",PPROG
 S TAB=79-$L("PATCH STATUS:"_PSTAT) W ?TAB,"PATCH STATUS: ",PSTAT
 W !,LINE,!
 D @COLUMNS
 W !,LINE,!
 Q
PRINT ;
 S (NSP,HOLDNSP)=""
 F  D  Q:$G(NSP)']""
 .S NSP=$O(^TMP($J,NSP)) Q:$G(NSP)']""
 .S VER=""
 .F  D  Q:$G(VER)']""
 ..S VER=$O(^TMP($J,NSP,VER)) Q:$G(VER)']""
 ..S SEQ=""
 ..F  D  Q:$G(SEQ)']""
 ...S SEQ=$O(^TMP($J,NSP,VER,SEQ)) Q:$G(SEQ)']""
 ...S RECORD=^TMP($J,NSP,VER,SEQ)
 ...S (INTEST,OKFOR,INLIVE,MSGTEST,MSGLIVE,SUBJ)=""
 ...S SUBJ=$P($G(RECORD),U,1)
 ...S MSGTEST=$P($G(RECORD),U,2)
 ...S MSGLIVE=$P($G(RECORD),U,3)
 ...S X=$P($G(RECORD),U,4)
 ...I $G(X)]"" S INTEST=$E(X,4,5)_"/"_$E(X,6,7)_"/"_$E(X,2,3)
 ...S X=$P($G(RECORD),U,5)
 ...I $G(X)]"" S OKFOR=$E(X,4,5)_"/"_$E(X,6,7)_"/"_$E(X,2,3)
 ...S X=$P($G(RECORD),U,6)
 ...I $G(X)]"" S INLIVE=$E(X,4,5)_"/"_$E(X,6,7)_"/"_$E(X,2,3)
 ...S DESIG=$P($G(RECORD),U,7)
 ...S XPROG=$P($G(RECORD),U,8)
 ...I $P($G(DESIG),"*",1)'=HOLDNSP W !
 ...S HOLDNSP=$P($G(DESIG),"*",1)
 ...D @FIELDS
 W !!
 Q
