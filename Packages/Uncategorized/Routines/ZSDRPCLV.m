ZSDRPCLV    ;LV/PB - Scheduling RPCs
 ;;1.0;LongView Scheduling;**1**;JUL 9, 2013;Build 67
 ;This routine has multiple RPCs created to support the mobile Scheduling apps
RECALL(RESULTS)  ;pull a list of recalls for a facility
 ;   No input parameters
 ;   Output: RESULTS Array:
 ;       $P(1) = DFN
 ;       $P(2) = PTR TO CLINIC FILE
 ;       $P(3) = ACCESSION#
 ;       $P(4) = TEST/APP - PTR TO FILE 403.51
 ;       $P(5) = PROVIDER - PTR TO FILE 405.54
 ;       $P(6) = RECALL DATE PER PROVIDER
 ;       $P(7) = COMMENT
 ;       $P(8) = FAST/NON-FASTING (SET OF CODES: f = Fasting, n = Non- Fasting)
 ;       $P(9) = LENGTH OF APPOINTMENT
 ;       $P(10) = DATE REMINDER SENT
 ;       $P(11) = USER WHO ENTERED RECALL (PTR to file 200)
 ;       $P(12) = RECALL DATE (PER PATIENT)
 ;       $P(13) = SECOND PRINT DATE
 ;       $P(14) = PATIENT NAME (LAST,FIRST)
 ;       $P(15) = PATIENT SSN
 ;       $P(16) = CLINIC NAME
 ;
 S CNT=0 S XX=0 F  S XX=$O(^SD(403.5,XX)) Q:XX'>0  D
 .K DFN,LNAME,SSN,CLINIC,CIEN,NODE,ACCESS,COMMENT,USER,TESTAPP,FASTNON,PATRECDT,PROVIDER,APPTLEN,SCEPRT,RECALLDT,REMINDDT
 .S NODE=$G(^SD(403.5,XX,0))
 .S DFN=$P(NODE,"^"),CIEN=$P(NODE,"^",2)
 .D DEM^VADPT
 .S LNAME=$G(VADM(1)),SSN=$P($G(VADM(2)),"^",2)
 .I $G(CIEN)>0 S CLINIC=$P(^SC(CIEN,0),"^")
 .S ACCESS=$P(NODE,"^",3),TESTAPP=$P(NODE,"^",4),PROVIDER=$P(NODE,"^",5),RECALLDT=$P(NODE,"^",6)
 .S COMMENT=$P(NODE,"^",7),FASTNON=$P(NODE,"^",8),APPTLEN=$P(NODE,"^",9),REMINDDT=$P(NODE,"^",10)
 .S USER=$P(NODE,"^",11),PATRECDT=$P(NODE,"^",12),SECPRT=$P(NODE,"^",13)
 .S RESULTS(CNT)=$G(^SD(403.5,XX,0))
 .S RESULTS(CNT)=$G(DFN)_U_$G(CIEN)_U_$G(ACCESS)_U_$G(TESTAPP)_U_$G(PROVIDER)_U_$G(RECALLDT)_U_$G(COMMENT)_U_$G(FASTNON)_U_$G(APPTLEN)_U_$G(REMINDDT)_U_$G(USER)_U_$G(PATRECDT)_U_$G(SECPRT)_U_$G(LNAME)_U_$G(SSN)_U_$G(CLINIC)
 .;S:$P(RESULTS(CNT),"^",13)="" RESULTS(CNT)=$G(RESULTS(CNT))_"^^"_$G(LNAME)_"^"_$G(SSN)_"^"_$G(CLINIC)
 .S CNT=CNT+1
 S:$G(RESULTS(0))="" RESULTS(0)="1^RECALL LIST EMPTY"
 K XDT,XX,CNT,DFN,LNAME,SSN,CLINIC,CIEN,NODE,ACCESS,COMMENT,USER,TESTAPP,FASTNON,PATRECDT,PROVIDER,APPTLEN,SCEPRT,RECALLDT,REMINDDT
 Q
RCLDFN(RESULTS,DFN) ;pull a list of recalls by patient
 ;   Input parameter = DFN (PTR to Patient file #2)
 ;   Output: RESULTS Array:
 ;       $P(1) = DFN
 ;       $P(2) = PTR TO CLINIC FILE
 ;       $P(3) = ACCESSION#
 ;       $P(4) = TEST/APP - PTR TO FILE 403.51
 ;       $P(5) = PROVIDER - PTR TO FILE 405.54
 ;       $P(6) = RECALL DATE PER PROVIDER
 ;       $P(7) = COMMENT
 ;       $P(8) = FAST/NON-FASTING (SET OF CODES: f = Fasting, n = Non- Fasting)
 ;       $P(9) = LENGTH OF APPOINTMENT
 ;       $P(10) = DATE REMINDER SENT
 ;       $P(11) = USER WHO ENTERED RECALL (PTR to file 200)
 ;       $P(12) = RECALL DATE (PER PATIENT)
 ;       $P(13) = SECOND PRINT DATE
 ;       $P(14) = PATIENT NAME (LAST,FIRST)
 ;       $P(15) = PATIENT SSN
 ;       $P(16) = CLINIC NAME
 ;
 K RESULTS
 I $G(DFN)="" S RESULTS(0)="1^DFN HAS TO BE SUPPLIED" Q
 S RESULTS(0)="1^RECALL LIST EMPTY"
 S CNT=0,XX=0 F  S XX=$O(^SD(403.5,"B",DFN,XX)) Q:XX'>0  D
 .K LNAME,SSN,CLINIC,CIEN,NODE,ACCESS,COMMENT,USER,TESTAPP,FASTNON,PATRECDT,PROVIDER,APPTLEN,SCEPRT,RECALLDT,REMINDDT
 .S NODE=$G(^SD(403.5,XX,0))
 .S CIEN=$P(NODE,"^",2)
 .D DEM^VADPT
 .S LNAME=$G(VADM(1)),SSN=$P($G(VADM(2)),"^",2)
 .I $G(CIEN)>0 S CLINIC=$P(^SC(CIEN,0),"^")
 .S ACCESS=$P(NODE,"^",3),TESTAPP=$P(NODE,"^",4),PROVIDER=$P(NODE,"^",5),RECALLDT=$P(NODE,"^",6)
 .S COMMENT=$P(NODE,"^",7),FASTNON=$P(NODE,"^",8),APPTLEN=$P(NODE,"^",9),REMINDDT=$P(NODE,"^",10)
 .S USER=$P(NODE,"^",11),PATRECDT=$P(NODE,"^",12),SECPRT=$P(NODE,"^",13)
 .S RESULTS(CNT)=$G(^SD(403.5,XX,0))
 .S RESULTS(CNT)=$G(DFN)_U_$G(CIEN)_U_$G(ACCESS)_U_$G(TESTAPP)_U_$G(PROVIDER)_U_$G(RECALLDT)_U_$G(COMMENT)_U_$G(FASTNON)_U_$G(APPTLEN)_U_$G(REMINDDT)_U_$G(USER)_U_$G(PATRECDT)_U_$G(SECPRT)_U_$G(LNAME)_U_$G(SSN)_U_$G(CLINIC)
 .;S RESULTS(CNT)=$G(^SD(403.5,XX,0))
 .;S:$P(RESULTS(CNT),"^",13)="" RESULTS(CNT)=$G(RESULTS(CNT))_"^"
 .S CNT=CNT+1
 K XDT,XX,CNT
 .K DFN,LNAME,SSN,CLINIC,CIEN,NODE,ACCESS,COMMENT,USER,TESTAPP,FASTNON,PATRECDT,PROVIDER,APPTLEN,SCEPRT,RECALLDT,REMINDDT
 Q
NEARLST(RESULTS) ;Gets the NEAR List for a Facility
 ;  No input parameters required
 ;  Output: TMP($J,"NEAR")=DFN^PATIENTNAME^SSN^ELIGIBILITY CODE INTERNAL^ELIGIBILITY CODE EXTERNAL^HOME PHONE^CELL PHONE^DATE^SITE
 ;    DFN = PATIENT ID
 ;    PATIENTNAME = PATIENT NAME
 ;    SSN = LAST FOUR OF SSN
 ;    ELIGIBILITY CODE INTERNAL = INTERNAL ELIGIBILITY CODE
 ;    ELIGIBILITY CODE EXTERNAL = EXTERNAL ELIGIBILITY CODE
 ;    HOME PHONE
 ;    CELL PHONE
 ;    DATE = APPOINTMENT DATE REQUESTED
 ;    SITE = FACILITY ID
 ;-Build temp global
 K ^TMP($J,"NEAR")
 N DFNIEN,DGSITE,PNAME,SSN,ELIG,PHONE,CELL,DFN
 S DGSITE=+$$SITE^VASITE()
 S (CNT,DFNIEN)=0 F  S DFNIEN=$O(^DPT("AEAR",1,DFNIEN)) Q:'DFNIEN  D
 .I $$GET1^DIQ(2,DFNIEN,1010.159,"I") D
 .Q:$P($G(^DPT(DFNIEN,1010.16)),"^",1)'=""
 .S DFN=DFNIEN D DEM^VADPT,ELIG^VADPT,ADD^VADPT
 .S CNT=CNT+1
 .S PNAME=VADM(1),SSN=+VADM(2),ELIG=VAEL(1),PHONE=VAPA(8),CELL=$P($G(^DPT(DFNIEN,.13)),"^",4)
 .I $G(ELIG)="" S ELIG="^"
 .S ^TMP($J,"NEAR",CNT)=DFNIEN_"^"_$G(PNAME)_"^"_$G(SSN)_"^"_$G(ELIG)_"^"_$G(PHONE)_"^"_$G(CELL)_"^"_$$GET1^DIQ(2,DFNIEN,1010.1511,"I")_"^"_DGSITE_$C(10)
 .K PNAME,DFN,SSN,ELIG,PHONE,CELL
 S RESULTS=$NA(^TMP($J,"NEAR"))
 K DFN,VADM,VAEL,VAPA,VA("BID"),VA("PID"),VACNTRY,VAERR
 Q
 ;
NEARDFN(RESULTS,DFN) ;Gets the NEAR List for a PATIENT
 ;  INPUT: Patient DFN
 ;  Output:
 ;  If the patient is on the NEAR List, returns the array:
 ;    DFN^PATIENTNAME^SSN^ELIGIBILITY CODE INTERNAL^ELIGIBILITY CODE EXTERNAL^HOME PHONE^CELL PHONE^DATE^SITE
 ;    DFN = PATIENT ID
 ;    PATIENTNAME = PATIENT NAME
 ;    SSN = LAST FOUR OF SSN
 ;    ELIGIBILITY CODE INTERNAL = INTERNAL ELIGIBILITY CODE
 ;    ELIGIBILITY CODE EXTERNAL = EXTERNAL ELIGIBILITY CODE
 ;    HOME PHONE
 ;    CELL PHONE
 ;    DATE = APPOINTMENT DATE REQUESTED
 ;    SITE = FACILITY ID
 ;   If the patient is not on the NEAR List returns "1^Patient not on the NEAR List"
 N DGSITE,SSN,ELIG,PHONE,CELL,PNAME
 I $G(DFN)="" S RESULTS(0)="1^DFN IS NOT DEFINED" Q
 S RESULTS(0)=""
 I $$GET1^DIQ(2,DFN,1010.159,"I") D
 .Q:$P($G(^DPT(DFN,1010.16)),"^",1)'=""
 .S DGSITE=+$$SITE^VASITE()
 .D DEM^VADPT,ELIG^VADPT,ADD^VADPT
 .S PNAME=VADM(1),SSN=+VADM(2),ELIG=VAEL(1),PHONE=VAPA(8),CELL=$P($G(^DPT(DFN,.13)),"^",4)
 .I $G(ELIG)="" S ELIG="^"
 .S RESULTS(0)=DFN_"^"_$G(PNAME)_"^"_$G(SSN)_"^"_$G(ELIG)_"^"_$G(PHONE)_"^"_$G(CELL)_"^"_$$GET1^DIQ(2,DFN,1010.1511,"I")_"^"_DGSITE
 I RESULTS(0)="" S RESULTS(0)="1^Patient not on the NEAR List"
 K DFN,VADM,VAEL,VAPA,VA("BID"),VA("PID"),VACNTRY,VAERR
 Q
EWL(RESULTS,DFN) ; gets the EWL data by patient as described in the output data
 ;  Input: Patient DFN (PTR to Patient File (#2))
 ;  Outputs if successful - patient is on the EWL:
 ;  $P(1)  = DFN
 ;  $P(2)  = DSS STOP CODE
 ;  $P(3)  = LAST UPDATED (RETURNED IN FILEMAN FORMAT)
 ;  $P(4)  = PATIENT NAME
 ;  $P(5)  = PATIENT SSN
 ;  $P(6)  = CLINIC NAME
 ;  $P(7)  = ORIGINATING DATE (RETURNED IN FILEMAN FORMAT)
 ;  $P(8)  = DESIRED DATE OF APPOINTMENT
 ;  $P(9)  = DAYS ON EWL
 ;  $P(10) = DATE REMOVED FROM EWL (RETURNED IN FILEMAN FORMAT)
 ;  $P(11) = PATIENT ZIP CODE
 ;  $P(12) = SERVICE CONNECTED %
 ;  $P(13) = ENROLLEE STATUS
 ;  $P(14) = PRIORITY
 ;  $P(15) = CLINIC ID
 ;  $P(16) = IEN FROM SDWL(409.3 FOR THE ENTRY
 ;
 ;  If patient is not on the EWL, returns
 ;  RESULTS(0)="1^PATIENT NOT ON THE EWL."
 ;
 I $G(DFN)="" S RESULTS(0)="1^DFN IS NOT DEFINED" Q
 I '$D(^SDWL(409.3,"B",DFN)) S RESULTS(0)="1^PATIENT NOT ON THE EWL" Q
 S REC=0,CNT=0 F  S REC=$O(^SDWL(409.3,"B",DFN,REC)) Q:REC'>0  D
 .S NODE=$G(^SDWL(409.3,REC,0))
 .Q:$G(NODE)=""
 .I $P($G(NODE),"^",17)="C" S RESULTS(0)="1^PATIENT NOT ON THE EWL" Q
 .I $P($G(NODE),"^",17)="O" S RESULTS(0)=""
 .D NOW^%DTC,DEM^VADPT,ADD^VADPT
 .S (ORIGDT,LASTUPDT)=$P(NODE,"^",2),DAYSON=$$FMDIFF^XLFDT(%,ORIGDT,1)
 .S PTNAME=$G(VADM(1))  ;,LASTNAME=$P(PTNAME,",")
 .S SSN=$P($G(VADM(2)),"^",2),ZIP=$G(VAPA(6))
 .S DESIRED=$P(NODE,"^",16),CLINIEN=$P(NODE,"^",9)
 .I $G(CLINIEN)'="" D
 ..S PTR=$P(^SDWL(409.32,CLINIEN,0),"^")
 ..S CLINNAME=$P(^SC(PTR,0),"^"),STOPPTR=$P(^SC(PTR,0),"^",7),STPCODE=$P(^DIC(40.7,STOPPTR,0),"^")
 .S SC=$P($G(^SDWL(409.3,REC,"SC")),"^",1),PRIOR=$P(NODE,"^",11),ENRSTAT1=$P(NODE,"^",20)
 .S DTREMOVE=$P($G(^SDWL(409.3,REC,"SDAPT")),"^",1)
 .I ENRSTAT1'="" S ENRSTAT=$S(ENRSTAT1="N":"NEW",ENRSTAT1="E":"ESTABLISHED",ENRSTAT1="P":"PRIOR",ENRSTAT1="U":"UNDETERMINED",1:"UNKNOWN")
 .I PRIOR'="" S PRIOR1=$S(PRIOR="A":"ASAP",PRIOR="F":"FUTURE",1:0)
 .S RESULTS(CNT)=DFN_U_$G(STPCODE)_U_$G(LASTUPDT)_U_$G(PTNAME)_U_$G(SSN)_U_$G(CLINNAME)_U_$G(ORIGDT)_U_$G(DESIRED)_U_$G(DAYSON)_U_$G(DTREMOVE)_U_$G(ZIP)_U_$G(SC)_U_$G(ENRSTAT)_U_$G(PRIOR1)_U_$G(CLINIEN)_U_$G(REC),CNT=CNT+1
 .K NODE,%,ORIGDT,LASTUPDT,DAYSON,PTNAME,LASTNAME,SSN
 .K DESIRED,CLINIEN,PTR,CLINNAME,STOPPTR,STPCODE,PRIOR,ENRSTAT,SC,ENRSTAT1,PRIOR1
 K REC,CNT,REC,DFN
 Q
FACEWL(RESULTS) ; gets the EWL data for a facility as described in the output data
 ;  Outputs if successful - patient is on the EWL:
 ;  $P(1)  = DFN
 ;  $P(2)  = DSS STOP CODE
 ;  $P(3)  = LAST UPDATED (RETURNED IN FILEMAN FORMAT)
 ;  $P(4)  = PATIENT NAME
 ;  $P(5)  = PATIENT SSN
 ;  $P(6)  = CLINIC NAME
 ;  $P(7)  = ORIGINATING DATE (RETURNED IN FILEMAN FORMAT)
 ;  $P(8)  = DESIRED DATE OF APPOINTMENT
 ;  $P(9)  = DAYS ON EWL
 ;  $P(10) = DATE REMOVED FROM EWL (RETURNED IN FILEMAN FORMAT)
 ;  $P(11) = PATIENT ZIP CODE
 ;  $P(12) = SERVICE CONNECTED %
 ;  $P(13) = ENROLLEE STATUS
 ;  $P(14) = PRIORITY
 ;  $P(15) = CLINIC ID
 ;  $P(16) = IEN FROM SDWL(409.3 FOR THE ENTRY
 ;
 ;  If no patients are not on the EWL, returns
 ;  RESULTS(0)="1^EWL is empty."
 ;
 I '$D(^SDWL(409.3,"B")) S RESULTS(0)="1^EWL is empty." Q
 S REC=0,CNT=0,DFN=0 F  S DFN=$O(^SDWL(409.3,"B",DFN)) Q:DFN'>0  F  S REC=$O(^SDWL(409.3,"B",DFN,REC)) Q:REC'>0  D
 .S NODE=$G(^SDWL(409.3,REC,0))
 .Q:$G(NODE)=""
 .Q:$P($G(NODE),"^",17)="C"
 .D NOW^%DTC,DEM^VADPT,ADD^VADPT
 .S (ORIGDT,LASTUPDT)=$P(NODE,"^",2),DAYSON=$$FMDIFF^XLFDT(%,ORIGDT,1)
 .S PTNAME=$G(VADM(1)) ;,LASTNAME=$P(PTNAME,",")
 .S SSN=$P($G(VADM(2)),"^",2),ZIP=$G(VAPA(6))
 .S DESIRED=$P(NODE,"^",16),CLINIEN=$P(NODE,"^",9)
 .I $G(CLINIEN)'="" D
 ..S PTR=$P(^SDWL(409.32,CLINIEN,0),"^")
 ..S CLINNAME=$P(^SC(PTR,0),"^"),STOPPTR=$P(^SC(PTR,0),"^",7),STPCODE=$P(^DIC(40.7,STOPPTR,0),"^")
 .S SC=$P($G(^SDWL(409.3,REC,"SC")),"^",1),PRIOR=$P(NODE,"^",11),ENRSTAT1=$P(NODE,"^",20)
 .S DTREMOVE=$P($G(^SDWL(409.3,REC,"SDAPT")),"^",1)
 .I ENRSTAT1'="" S ENRSTAT=$S(ENRSTAT1="N":"NEW",ENRSTAT1="E":"ESTABLISHED",ENRSTAT1="P":"PRIOR",ENRSTAT1="U":"UNDETERMINED",1:"UNKNOWN")
 .I PRIOR'="" S PRIOR1=$S(PRIOR="A":"ASAP",PRIOR="F":"FUTURE",1:0)
 .S RESULTS(CNT)=DFN_U_$G(STPCODE)_U_$G(LASTUPDT)_U_$G(PTNAME)_U_$G(SSN)_U_$G(CLINNAME)_U_$G(ORIGDT)_U_$G(DESIRED)_U_$G(DAYSON)_U_$G(DTREMOVE)_U_$G(ZIP)_U_$G(SC)_U_$G(ENRSTAT)_U_$G(PRIOR1)_U_$G(CLINIEN)_U_$G(REC),CNT=CNT+1
 .K NODE,%,ORIGDT,LASTUPDT,DAYSON,PTNAME,LASTNAME,SSN
 .K DESIRED,CLINIEN,PTR,CLINNAME,STOPPTR,STPCODE,PRIOR,ENRSTAT,SC,ENRSTAT1,PRIOR1
 K REC,CNT,REC,DFN
 Q
EDITNEAR(RESULTS,DFN,STATUS,COMM) ;Edit a patient on the NEAR List
 ;Called by the RPC ZSD EDIT NEAR
 ;Input: 
 ;  DFN         = Patient DFN
 ;  STATUS      = Set of codes:
 ;                  C = Cancelled
 ;                  E = EWL - Moved to the EWL
 ;                  F = Filled
 ;                  I = In Process/Veteran Contacted
 ;  COMM        = Comments. Required if the status is cancelled, otherwise it is optional
 ;Output: RESULTS(0)="0^Patient NEAR List entry updated." if the update was successful
 ;        RESULTS(0)="1^Patient is not on the NEAR List."
 ;        RESULTS(0)="1^DFN was not provided."
 ;        RESULTS(0)="1^Status is null. Status is a required field."
 ;        RESULTS(0)="1^Could not update the record."
 ;        RESULTS(0)="1^Patient record is locked, try again later."
 S RESULTS(0)=""
 I $G(DFN)="" S RESULTS(0)="1^DFN was not provided." Q
 I $G(STATUS)="" S RESULTS(0)="1^Status is null. Status is a required field." Q
 I $D(^DPT(DFN,1010.16)) D
 .I $P(^DPT(DFN,1010.16),U,1)'="" S ERR=1 
 I $G(ERR)=1 S RESULTS(0)="1^Patient is not on the NEAR List." Q
 I '$$GET1^DIQ(2,DFN,1010.159,"I") S RESULTS(0)="1^Patient is not on the NEAR List." Q
 D NOW^%DTC
 S ERR=0
 L +^DPT(DFN):10 I '$TEST S ERR=1,RESULTS(0)="1^Patient record is locked, try again later." Q 
 S DA=DFN,DIE="^DPT(",DR="1010.161////"_$G(STATUS)_";1010.163////"_$G(COMM) D ^DIE
 L -^DPT(DFN)
 K DA,DIE,DR
 I $P($G(^DPT(DFN,1010.16)),"^")='$G(STATUS) S ERR=1
 I $P($G(^DPT(DFN,1010.16)),"^",2)'=% S ERR=1
 I $P($G(^DPT(DFN,1010.16)),"^")=$G(STATUS) S RESULTS(0)="0^Patient removed from NEAR List."
 I $G(ERR)=1 S RESULTS(0)="1^Could not update the record." Q
 S RESULTS(0)="0^Patient NEAR List entry updated."
 Q
