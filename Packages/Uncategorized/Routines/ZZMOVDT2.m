ZZMOVDT2 ; SEB - Move dates for various data domains - part 2
 ;;1.0;VistA Data Support;;DECEMBER 2, 2015;Build 18
 Q
 ;
GETDATE(ORIGDT,VALUE) 
 I $E(VALUE,1,7)?7N S NEWDT=VALUE
 E  I +VALUE=VALUE S NEWDT=$$CALCDT(ORIGDT,VALUE)
 I $E(VALUE,1,5)="TODAY" D
 . D NOW^%DTC S NEWDT=X
 . S OFFSET=0 I "+-"[$E(VALUE,6) S OFFSET=$E(VALUE,6,99)
 . S NEWDT=$$CALCDT(NEWDT,OFFSET)
 . Q
 Q NEWDT
 ;
LRODAY(ORIGDT,VALUE,LRDFN)
 S LRDFN=$G(LRDFN)
 K TODOARR S NEWDT=$$GETDATE(ORIGDT,VALUE),SPECID=0
 F  S SPECID=$O(^LRO(69,ORIGDT,1,SPECID)) Q:SPECID=""  D
 . I LRDFN="" S TODOARR(ORIGDT,SPECID)=NEWDT
 . I +$G(^LRO(69,ORIGDT,1,SPECID,0))=LRDFN S TODOARR(ORIGDT,SPECID)=NEWDT
 D LRO(.TODOARR)
 Q
 ; 
LROPAT(LRDFN,VALUE)
 K TODOARR
 S ORIGDT=0 F  S ORIGDT=$O(^LRO(69,"D",LRDFN,ORIGDT)) Q:ORIGDT=""  D
 . S SPECID=0,NEWDT=$$GETDATE(ORIGDT,VALUE)
 . F  S SPECID=$O(^LRO(69,"D",LRDFN,ORIGDT,SPECID)) Q:SPECID=""  S TODOARR(ORIGDT,SPECID)=NEWDT
 . Q
 D LRO(.TODOARR)
 Q
 ;
LRO(TODOARR)
 S ORIGDT=""
 F  S ORIGDT=$O(TODOARR(ORIGDT)) Q:ORIGDT=""  D
 . S SPECID=""
 . F  S SPECID=$O(TODOARR(ORIGDT,SPECID)) Q:SPECID=""  S NEWDT=TODOARR(ORIGDT,SPECID) D LROONE(ORIGDT,SPECID,NEWDT)
 . Q
 Q
 ;
LROONE(ORIGDT,SPECID,NEWDT)
 I NEWDT'?7N S NEWDT=$$GETDATE(ORIGDT,NEWDT)
 K ORIGARR,NEWARR
 D GETS^DIQ(69.01,SPECID_","_ORIGDT,"**","IN","ORIGARR")
 I '$D(ORIGARR) W !,"No entry in ^LRO(69) for ",ORIGDT,"!" Q
 D GETS^DIQ(69,NEWDT,"*","IN","NEWARR")
 I '$D(NEWARR) S ERR=$$CREATLRO(NEWDT) I ERR]"" W !,ERR Q
 S DA=69,SEQ=0 K SEQARR,NEWARR
 F  S DA=$O(ORIGARR(DA)) Q:DA=""  D
 . S IENS=""
 . F  S IENS=$O(ORIGARR(DA,IENS))  Q:IENS=""  D
 . . S SEQ=SEQ+1,SEQARR(DA,IENS)=SEQ,FIELDID=""
 . . F  S FIELDID=$O(ORIGARR(DA,IENS,FIELDID))  Q:FIELDID=""  D
 . . . I DA=69.01,FIELDID=.001!(FIELDID=.02)!(FIELDID=.03) Q
 . . . S NEWIENS=IENS,DTPIECE=3 S:DA=69.01 DTPIECE=2
 . . . S $P(NEWIENS,",",DTPIECE)=NEWDT
 . . . I DA=69.01 S $P(NEWIENS,",")="+"_$G(SEQARR(DA,IENS))
 . . . E  D
 . . . . S $P(NEWIENS,",")="+"_$G(SEQARR(DA,IENS))
 . . . . S $P(NEWIENS,",",2)="+"_$G(SEQARR(69.01,$P(IENS,",",2,4)))
 . . . . Q
 . . . S VALUE=ORIGARR(DA,IENS,FIELDID,"I")
 . . . I DA=69.01,FIELDID=5!(FIELDID=5.5)!(FIELDID=10)!(FIELDID=20)!(FIELDID=21) S $P(VALUE,".")=NEWDT
 . . . I DA=69.03,FIELDID=2!(FIELDID=22) S $P(VALUE,".")=NEWDT
 . . . S NEWARR(DA,NEWIENS,FIELDID)=VALUE
 . . . Q
 . . Q
 . Q
 K ERRARR D UPDATE^DIE(,"NEWARR",,"ERRARR")
 I '$D(ERRARR) S DA(1)=ORIGDT,DA=SPECID,DIK="^LRO(69,"_DA(1)_",1," D ^DIK
 Q
 ;
CREATLRO(NEWDT) 
 K FDA,ERRARR S FDA(69,"+1,",.01)=NEWDT,LROIEN(1)=NEWDT
 D UPDATE^DIE(,"FDA","LROIEN","ERRARR")
 Q $G(ERRARR("DIERR",1,"TEXT",1))
 ;
CHECK ; Check ^LRO
 Q
 ;
CALCDT(X1,X2) ; Return date + offset
 D C^%DTC S NEWDT=X
 Q NEWDT
 ;
DELLRES(LRDFN,TYPE,DATE) 
 S DA(1)=LRDFN,DA=DATE,DIK="^LR("_LRDFN_","""_TYPE_""","
 D ^DIK
 Q
 ;
DELRRPT(DFN,DATE) 
 S DA(1)=DFN,DA=DATE,DIK="^RADPT("_DFN_",""DT"","
 D ^DIK
 Q
 ;
FIXSIG(TIUDA,ORIGCHK)
 S NAMEENC=$P($G(^TIU(8925,TIUDA,15)),"^",3),TITLEENC=$P($G(^TIU(8925,TIUDA,15)),"^",4)
 S NAMEDEC=$$DECRYPT^TIULC1(NAMEENC,1,ORIGCHK),TITLEDEC=$$DECRYPT^TIULC1(TITLEENC,1,ORIGCHK)
 S GLO="^TIU(8925,"_+TIUDA_",""TEXT"")",NEWCHK=$$CHKSUM^TIULC(GLO)
 S DA=TIUDA,DIE="^TIU(8925,",DR="1503///"_NAMEDEC_";1504///"_TITLEDEC D ^DIE
 Q
