DSIFRPT0 ;DSS/AMC - CIVIL HOSPITAL COST REPORT ;03/01/2009
 ;;3.2;FEE BASIS CLAIMS SYSTEM;;Jun 05, 2009;Build 38
 ;Copyright 1995-2009, Document Storage Systems, Inc., All Rights Reserved
 ; 
 ; Integration Agreements
 ; 10000  ^%DTC
 ;  5090  $$DATX^FBAAUTL,$$SSN^FBAAUTL
 ;  5097  $$NAME^FBCHREQ2
 ;
 ;FBTP SET IN OPTION ENTRANCE ACTION (6=CH/7=CNH)
 ;FBREF SET IN OPTION ENTRANCE ACTION (FBREF="FB7078" OR "FB583")
 ; if UC ask if report for just mill-bill (1725) or just non-mill bill
 Q
COSTRPT(AXY,BEGDATE,ENDDATE,FBRT) ;RPC - DSIF INP HOSPITAL COST RPT
 ;Input Parameters
 ;    BEGDATE - Begining Date (Required : FileMan format)
 ;    ENDDATE - Ending Date (Required : FileMan format)
 ;    FBRT - Report Type (Optional : D = Detailed, S = Summary, Default = Summary)
 ;Return Array
 ;    -1 ^ Invalid Input!
 ;    -1 ^ No payments found within specified timeframe!
 ;    D ^ PATIENT NAME ^ SSN ^ ASSOCIATED 7078 ^ AMOUNT PAID ^ 1 = ANCILLARY PAYMENT ^ FINAL DRG ^ LENGTH OF STAY
 ;    SL ^ TREATING SPECIALTY ^ LENGTH OF STAY ^ NUMBER OF CASES ^ AVG AMOUNT PAID
 ;    ST ^ TOTAL CASES ^ AVG AMOUNT PAID ^ AVG LENGTH OF STAY
 ;    TA ^ TOTAL ANCILLARY PAYMENTS ^ AVG AMOUNT PAID
 ;    
 ;    (First Piece: D = Detail Line, SL = Summary Line, ST = Summary Totals, TA = Total Ancillary)
 S AXY=$NA(^TMP($J,"DSIFRPT0")),FBRT=$G(FBRT),FBTP=6,FBREF="FB7078" S:FBRT="" FBRT="S" K @AXY
 I '$G(BEGDATE)!'$G(ENDDATE)!("DS"'[FBRT) S AXY="-1^Invalid Input!" Q
 N XX,YY,DSIFTS,FBNAME,FBPTC,FBIEN,DFN,FBAMT,FBDRG,FBCHK,FBLOS,FB7078,FBANC,FBCTR,FBTAMT,FBTLOS,FBDT,BEGDT
 N FBTP,Q,QQ,X,X1,X2,FBTYPE,FBINV,FBAAOUT,I,J,K,L,M,N,FBJ,FBREF1,FB583,FB7078,FBEND,FBSUM,FBSUM1,FBSUM2,FB1725,FB1725R
 ;
 K ^TMP($J,"FBCHCR") S (FBIEN,FBCTR,FBTAMT,FBTLOS,FBAAOUT,YY)=0,BEGDT=BEGDATE-1
 F FBDT=BEGDT:0 S FBDT=$O(^FBAAI("AD",FBDT)) Q:FBDT'>0!(FBDT>ENDDATE)  F  S FBIEN=$O(^FBAAI("AD",FBDT,FBIEN)) Q:FBIEN'>0  S FBTYPE=$P($G(^FBAAI(FBIEN,0)),"^",12) I FBTYPE]"",(FBTP=FBTYPE) D
 .S FBINV=^FBAAI(FBIEN,0),FBPTC=$P(FBINV,"^",19) S:FBPTC="" FBPTC="99" S @FBREF=$S($P(FBINV,"^",5)[FBREF:+$P(FBINV,"^",5),1:"") Q:@FBREF<1
 .;if UC and user requested just Mill Bill or just non-Mill Bill then
 .;check claim and skip when appropriate
 .S DFN=$P(FBINV,"^",4) Q:'$G(DFN)  S FBNAME=$$NAME^FBCHREQ2(DFN),FBAMT=$P(FBINV,"^",9),FBDRG=$P(FBINV,"^",24),X1=$P(FBINV,"^",7),X2=$P(FBINV,"^",6) D ^%DTC S FBLOS=$S(X>0:X,1:1)
 .I FBLOS>0 S FBSUM=$G(^TMP($J,"FBCHCR","SUM",FBPTC,FBLOS)),$P(FBSUM,"^")=($P(FBSUM,"^")+1),$P(FBSUM,"^",2)=($P(FBSUM,"^",2)+FBAMT) D
 ..S ^TMP($J,"FBCHCR","SUM",FBPTC,FBLOS)=FBSUM
 .S ^TMP($J,"FBCHCR",FBPTC,FBNAME,@FBREF,"INV",FBIEN)=DFN_"^"_FBAMT_"^"_FBDRG_"^"_FBLOS
 D ANCIL
 I '$D(^TMP($J,"FBCHCR")) S FBEND=1 S @AXY@(0)="-1^No payments found within specified timeframe!" Q
DETAIL ;
 S (FBIEN,FBPTC,FBREF1,DFN,FBAMT,FBLOS,FBANC,L,M,N)=0,(FBNAME,FBDRG,FBCHK)=""
 F  S FBPTC=$O(^TMP($J,"FBCHCR",FBPTC)) Q:FBPTC=""!(FBAAOUT)  F  S FBNAME=$O(^TMP($J,"FBCHCR",FBPTC,FBNAME)) Q:FBNAME=""!(FBAAOUT)   F  S FBREF1=$O(^TMP($J,"FBCHCR",FBPTC,FBNAME,FBREF1)) Q:FBREF1'>0!(FBAAOUT)  D
 .I $D(^TMP($J,"FBCHCR",FBPTC,FBNAME,FBREF1,"INV")) D
 ..F  S FBIEN=$O(^TMP($J,"FBCHCR",FBPTC,FBNAME,FBREF1,"INV",FBIEN)) Q:FBIEN=""!(FBAAOUT)  S FBCTR=FBCTR+1 D
 ...S FBINV=^TMP($J,"FBCHCR",FBPTC,FBNAME,FBREF1,"INV",FBIEN),DFN=+FBINV,FBAMT=$P(FBINV,"^",2),FBDRG=$P(FBINV,"^",3),FBLOS=$P(FBINV,"^",4),FBTAMT=FBTAMT+FBAMT,FBTLOS=FBTLOS+FBLOS D PRINT:FBRT="D"
 .I $D(^TMP($J,"FBCHCR",FBPTC,FBNAME,FBREF1,"ANC")) F  S L=$O(^TMP($J,"FBCHCR",FBPTC,FBNAME,FBREF1,"ANC",L)) Q:'L!(FBAAOUT)  D
 ..F  S M=$O(^TMP($J,"FBCHCR",FBPTC,FBNAME,FBREF1,"ANC",L,M)) Q:'M!(FBAAOUT)  F  S N=$O(^TMP($J,"FBCHCR",FBPTC,FBNAME,FBREF1,"ANC",L,M,N)) Q:'N!(FBAAOUT)  D
 ...S FBANC=1,FBINV=^TMP($J,"FBCHCR",FBPTC,FBNAME,FBREF1,"ANC",L,M,N),DFN=+FBINV,FBAMT=$P(FBINV,"^",2),FBDRG="",FBLOS="" D PRINT:FBRT="D" S FBANC=0
 D SUMMARY
END ;
 K ^TMP($J,"FBCHCR") ;D CLOSE^FBAAUTL
 Q
PRINT ;
 S XX="D^"_$E(FBNAME,1,23)_U_$$SSN^FBAAUTL(DFN)_U_$P(^FB7078(FBREF1,0),U)_U_$S($G(FBAMT):$J($FN(FBAMT,",",2),10),1:"")_U_$S(FBANC:1,1:"")_U_FBDRG_U_$J(FBLOS,5)
 S YY=YY+1,@AXY@(YY)=XX
 Q
HED ;
 Q
 I FBREF="FB583" W !?22,$S(FB1725R="M":"MILL BILL (1725) ",FB1725R="N":"  NON-MILL BILL ",1:"        "),"UNAUTHORIZED CLAIMS"
 W !,@$S(FBTP=6:"?25",1:"?22"),"COST REPORT FOR ",$S(FBTP=6:"CIVIL HOSPITAL",1:"CONTRACT NURSING HOME"),!?28,$$DATX^FBAAUTL(BEGDATE)," THROUGH ",$$DATX^FBAAUTL(ENDDATE),!,@$S(FBTP=6:"?25",1:"?22"),Q I FBTP=7 F J=1:1:7 W "-"
 W !!!,"PATIENT NAME",?25,"PATIENT ID"
 I FBREF="FB583" W ?40,"DT CLAIM REC"
 I FBREF="FB7078" W ?40,"ASSOC 7078"
 W ?55,"AMT PAID",?66,"FINAL DRG",?77,"LOS",!,QQ
 Q
HED1 ;
 S DSIFTS=""
 F I=1:1:8 S J=$T(TEXT+I) I $P(J,";",3)=FBPTC S DSIFTS=$P(J,";",4) Q
 Q
HED2 ;
 Q
PGCHK ;
 Q
ANCIL ;
 S J="",(K,L,M,N)=0,FBDT=BEGDATE
 F  S J=$O(^FBAAC("AM",J)) Q:J=""  I J[FBREF F  S K=$O(^FBAAC("AM",J,K)) Q:K'>0  F  S L=$O(^FBAAC("AM",J,K,L)) Q:L'>0  F  S M=$O(^FBAAC("AM",J,K,L,M)) Q:M'>0  F  S N=$O(^FBAAC("AM",J,K,L,M,N)) Q:N'>0  D
 .S FBINV=$G(^FBAAC(K,1,L,1,M,1,N,0))
 .I $P(FBINV,"^",9)=FBTP,$P(FBINV,"^",6)>(FBDT-1),$P(FBINV,"^",6)<(ENDDATE+1) D
 ..;if UC and user requested just Mill Bill or just non-Mill Bill then
 ..;check claim and skip when appropriate
 ..I FBTP=6,FBREF="FB583","^M^N^"[(U_FB1725R_U),$P(FBINV,"^",13)[FBREF S FB1725=+$P($G(^FB583(+$P(FBINV,U,13),0)),U,28) Q:$S(FB1725R="M"&'FB1725:1,FB1725R="N"&FB1725:1,1:0)
 ..S FBPTC=$P(FBINV,"^",17) S:FBPTC="" FBPTC="99" S DFN=K,FBNAME=$$NAME^FBCHREQ2(DFN),FBAMT=$P(FBINV,"^",3),FBREF1=$P(J,";")
 ..S FBSUM=$G(^TMP($J,"FBCHCR","SUM","ANC")),$P(FBSUM,"^")=($P(FBSUM,"^")+1),$P(FBSUM,"^",2)=($P(FBSUM,"^",2)+FBAMT) S ^TMP($J,"FBCHCR","SUM","ANC")=FBSUM
 ..S ^TMP($J,"FBCHCR",FBPTC,FBNAME,FBREF1,"ANC",L,M,N)=DFN_"^"_FBAMT_"^^"
 Q
SUMMARY ;
 S (FBPTC,FBLOS)=0,FBCHK=""
 F  S FBPTC=$O(^TMP($J,"FBCHCR","SUM",FBPTC)) Q:FBPTC=""!(FBAAOUT)  F  S FBLOS=$O(^TMP($J,"FBCHCR","SUM",FBPTC,FBLOS)) Q:FBLOS=""!(FBAAOUT)  S FBSUM=^(FBLOS),FBSUM1=+FBSUM,FBSUM2=$P(FBSUM,"^",2) D
 .I FBPTC'=FBCHK D HED1 S FBCHK=FBPTC
 .S XX="SL^"_DSIFTS_$J(FBLOS,5)_U_$J(FBSUM1,5)_U_$J((FBSUM2/FBSUM1),10,2),YY=YY+1,@AXY@(YY)=XX
 S XX="ST^"_FBCTR_U_$S($G(FBTAMT):$FN((FBTAMT/FBCTR),",",2),1:"")_U_$S($G(FBTLOS):$FN((FBTLOS/FBCTR),",",2),1:""),YY=YY+1,@AXY@(YY)=XX
 I $D(^TMP($J,"FBCHCR","SUM","ANC")) S FBSUM=^("ANC"),FBSUM1=+FBSUM,FBSUM2=$P(FBSUM,"^",2),XX="TA^"_$J(FBSUM1,5)_U_$J((FBSUM2/FBSUM1),10,2)
 Q
TEXT ;
 ;;00;SURGICAL
 ;;10;MEDICAL
 ;;60;HOME NURSING SERVICE
 ;;85;PSYCHIATRIC-CONTRACT
 ;;86;PSYCHIATRIC
 ;;95;NEUROLOGICAL-CONTRACT
 ;;96;NEUROLOGICAL
 ;;99;UNKNOWN
