DVBAP206 ;ALB/JR - AMIE EXAM (#396.6) FILE UPDATE ;7/9/2018 12:00PM
 ;;2.7;AMIE;**206**;Apr 10, 1995;Build 10
 ;
 ;this routine is used as a post-init in a KIDS build
 ;to modify the AMIE EXAM file (#396.6)
 ;
 Q
 ;
POST ; entry point
 D MES^XPDUTL(" ")
 D BMES^XPDUTL("Updating the AMIE EXAM file (#396.6)...")
 D MES^XPDUTL(" ")
 ;
 D NAMECHG ;change exam names
 D INACT   ;inactivate exams
 ;
 D MES^XPDUTL(" ")
 D BMES^XPDUTL("Update of AMIE EXAM file (#396.6) completed.")
 D MES^XPDUTL(" ")
 Q
NAMECHG ;* change exam names
 ;
 ;  DVBAXX is in format:
 ;   OLD EXAM NAME^NEW EXAM NAME
 ;
 N DVBAX,DVBAXX,DVBADA,DA,DR,DIC,DIE,X,Y,DVBASTR
 D MES^XPDUTL(" ")
 D BMES^XPDUTL("Changing names in AMIE EXAM file (#396.6)...")
 D MES^XPDUTL(" ")
 F DVBAX=1:1 S DVBAXX=$P($T(CHNG+DVBAX),";;",2) Q:DVBAXX="QUIT"  D
 .F DVBADA=0:0 S DVBADA=+$O(^DVB(396.6,"B",$E($P(DVBAXX,U,1),1,30),DVBADA)) Q:DVBADA=0  D
 ..I $D(^DVB(396.6,DVBADA,0)),$P(^DVB(396.6,DVBADA,0),U,5)="A" D
 ...S DA=DVBADA,DR=".01///^S X=$P(DVBAXX,U,2)",DIE="^DVB(396.6," D ^DIE
 ...D MES^XPDUTL(" ")
 ...D MES^XPDUTL("   Entry #"_DVBADA_" for "_$P(DVBAXX,U,1))
 ...D BMES^XPDUTL("      ... field (#.01) updated to  "_$P(DVBAXX,U,2)_".")
 ...D MES^XPDUTL(" ")
 ..I '$D(^DVB(396.6,DVBADA,0)) D
 ...D MES^XPDUTL(" ")
 ...S DVBASTR="Can't find entry for "_$P(DVBAXX,U,1)
 ...D BMES^XPDUTL(DVBASTR_" ...field (#.01) not updated.")
 Q
 ;
CHNG ;name changes - old exam name^new exam name
 ;;AID AND ATTENDANCE OR HOUSEBOUND EXAMINATION^Aid and Attendance or Housebound Examination
 ;;BONES (FRACTURES AND BONE DISEASE)^Bones (Fractures and Bone Diseases)
 ;;DBQ DENTAL Dental & oral (other than TMJ)^DBQ DENTAL Dental & oral (other than TMD)
 ;;DBQ GI Liver conditions Hepatitis, cirrhosis & other liver^DBQ GI Liver Hepatitis, cirrhosis & other liver conditions
 ;;DBQ General Medical Gulf War^DBQ General Medical Gulf War (including Burn Pits)
 ;;DBQ MUSC Foot miscellaneous^DBQ MUSC Foot conditions, including Flatfoot (Pes Planus)
 ;;DBQ MUSC Hip & thigh^DBQ MUSC Hip & thigh conditions
 ;;DBQ Medical SHA^DBQ Separation Health Assessment
 ;;DBQ RHEUM Arthritis: non-degen (inflam, imm, cryst, infect)^DBQ RHEUM Arthritis non-degen (inflam, imm, cryst, infect)
 ;;SOCIAL AND INDUSTRIAL SURVEY^Social and Industrial Survey
 ;;QUIT
 ;
INACT ;* inactivate exams
 ;
 ;  DVBAXX is in format:
 ;   EXAM NAME
 ;
 N DVBAX,DVBAXX,DVBADA,DA,DR,DIC,DIE,X,Y,DVBASTR
 D MES^XPDUTL(" ")
 D BMES^XPDUTL("Inactivating procedures AMIE EXAM file (#396.6)...")
 D MES^XPDUTL(" ")
 F DVBAX=1:1 S DVBAXX=$P($T(OLD+DVBAX),";;",2) Q:DVBAXX="QUIT"  D
 .F DVBADA=0:0 S DVBADA=+$O(^DVB(396.6,"B",$E(DVBAXX,1,30),DVBADA)) Q:DVBADA=0  D
 ..I $D(^DVB(396.6,DVBADA,0)),$P(^DVB(396.6,DVBADA,0),U,5)="A" D
 ...S DA=DVBADA,DR=".5///INACTIVE",DIE="^DVB(396.6," D ^DIE
 ...D MES^XPDUTL(" ")
 ...D MES^XPDUTL("   Entry #"_DVBADA_" for "_DVBAXX)
 ...D BMES^XPDUTL("      ... inactivated")
 ...D MES^XPDUTL(" ")
 ..I '$D(^DVB(396.6,DVBADA,0)) D
 ...D MES^XPDUTL(" ")
 ...S DVBASTR="Can't find entry for "_DVBAXX
 ...D BMES^XPDUTL(DVBASTR_" ...exam cannot be inactivated.")
 Q
 ;
OLD ;national procedures to be inactivated - exam name
 ;;ACROMEGALY
 ;;AMPUTATION, RESIDUALS OF
 ;;ARRHYTHMIAS
 ;;ARTERIES, VEINS AND MISCELLANEOUS
 ;;AUDIO
 ;;BRAIN AND SPINAL CORD
 ;;CHRONIC FATIGUE SYNDROME
 ;;COLD INJURY PROTOCOL EXAMINATION
 ;;CRANIAL NERVES
 ;;CUSHING'S SYNDROME
 ;;DENTAL AND ORAL
 ;;DIABETES MELLITUS
 ;;DIGESTIVE CONDITIONS, MISCELLANEOUS
 ;;EAR DISEASE
 ;;EATING DISORDERS (MENTAL DISORDERS)
 ;;ENDOCRINE DISEASES, MISCELLANEOUS
 ;;EPILEPSY AND NARCOLEPSY
 ;;ESOPHAGUS AND HIATAL HERNIA
 ;;EYE EXAMINATION
 ;;FEET
 ;;FIBROMYALGIA
 ;;GENERAL MEDICAL EXAMINATION
 ;;GENITOURINARY EXAMINATION
 ;;GULF WAR GUIDELINES
 ;;GYNECOLOGICAL CONDITIONS AND DISORDERS OF THE BREAST
 ;;HAND, THUMB, AND FINGERS
 ;;HEART
 ;;HEMIC DISORDERS
 ;;HIV-RELATED ILLNESS
 ;;HYPERTENSION
 ;;INFECTIOUS, IMMUNE, AND NUTRITIONAL DISABILITIES
 ;;INITIAL EVALUATION FOR POST-TRAUMATIC STRESS DISORDER (PTSD)
 ;;INTESTINES (LARGE AND SMALL)
 ;;JOINTS (SHOULDER/ELBOW/WRIST/HIP/KNEE/ANKLE)
 ;;LIVER, GALL BLADDER, AND PANCREAS
 ;;LYMPHATIC DISORDERS
 ;;MENTAL DISORDERS (EXCEPT PTSD AND EATING DISORDERS)
 ;;MOUTH, LIPS, AND TONGUE
 ;;MUSCLES
 ;;NEUROLOGICAL DISORDERS, MISCELLANEOUS
 ;;NOSE, SINUS, LARYNX, AND PHARYNX
 ;;PERIPHERAL NERVES
 ;;PRISONER OF WAR PROTOCOL EXAMINATION
 ;;PULMONARY TUBERCULOSIS AND MYCOBACTERIAL DISEASES
 ;;RECTUM AND ANUS
 ;;RESPIRATORY (OBSTRUCTIVE, RESTRICTIVE, AND INTERSTITIAL)
 ;;RESPIRATORY DISEASES, MISCELLANEOUS
 ;;REVIEW EXAMINATION FOR POST-TRAUMATIC STRESS DISORDER (PTSD)
 ;;SCARS
 ;;SENSE OF SMELL AND TASTE
 ;;SKIN DISEASES (OTHER THAN SCARS)
 ;;SPINE
 ;;STOMACH, DUODENUM AND PERITONEAL ADHESIONS
 ;;THYROID AND PARATHYROID DISEASES
 ;;TRAUMATIC BRAIN INJURY (TBI)
 ;;QUIT
 ;
