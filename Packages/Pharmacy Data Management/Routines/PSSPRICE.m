PSSPRICE ;EPIP/WC - PHARMACY PRICE TRACKER FILE 50;03-06-2017  ; 22 May 2018  10:11 AM
 ;;1.0;PHARMACY DATA MANAGEMENT;**227**;2/28/17;Build 18
 Q  ; call by line tag
 ; UDPATE^DIE supported by ICR #2053
 ; ^XMD supported by ICR #10113
ST(PSSIEN,PSSDUZ) ;
 ;  PSSIEN=DRUG IEN
 ;  PSSNEW=NEW PRICE
 ;  PSSDUZ=USER CHANGING PRICE
 ; CLASS 3 CROSS REFER ON FILE 50 FIELD #16 
 N DA,DIE,X,Y,DIC
 ;LEAST GET THE TIME THE CHANGE WAS MADE
 D NOW^%DTC S PSSTIME=%
 S PSSNEW=$P($G(^PSDRUG(PSSIEN,660)),"^",6)
 ;
QUE ;ENTER THE DATA IN FILE 50 MULTIPLE FIELD 950 
 S ZTRTN="HIS^PSSPRICE"
 S ZTDESC="PHARMACY PRICE TRACKER "
 S ZTSAVE("PSSIEN")=""
 S ZTSAVE("PSSNEW")=""
 S ZTSAVE("PSSDUZ")=""
 S ZTSAVE("PSSTIME")=""
 S ZTIO=""
 D NOW^%DTC S ZTDTH=%
 D ^%ZTLOAD
 D HOME^%ZIS
 Q
HIS ;LOGS CHANGES IN FILE 50 HISTORY PRICE DISPENSE #950
 ; first delete any price updates prior to the retention limit set up in the paramater PSS DRUG AUDIT RETENTION MOS 
 N DEFDT,PSIEN2 S DEFDT=+$$GET^XPAR("ALL","PSS DRUG AUDIT RETENTION MOS")
 S DEFMOS=$S(DEFDT>0:DEFDT,1:999999999)
 S X1=$$NOW^XLFDT,X2=DEFMOS*30 D C^%DTC S ENDDT=X
 S X1=$P($$NOW^XLFDT,".",1)
 S ENDDT=$$FMADD^XLFDT(DT,"-"_(DEFMOS*30))
 I $O(^PSDRUG(PSSIEN,950,0)) D
 . F  S PSIEN2=$O(^PSDRUG(PSSIEN,950,0)) Q:^(PSIEN2,0)>ENDDT  D
 . . N DIK,DA
 . . S DIK="^PSDRUG(PSSIEN,950,",DA(1)=PSSIEN,DA=PSIEN2 D ^DIK  ; Delete old data
 N FDA
 S FDA(50.095,"?+1,"_PSSIEN_",",.01)=PSSTIME
 S FDA(50.095,"?+1,"_PSSIEN_",",1)=PSSDUZ
 S FDA(50.095,"?+1,"_PSSIEN_",",3)=PSSNEW
 D UPDATE^DIE("","FDA")
 S PSSNAME=$$GET1^DIQ(200,PSSDUZ_",",.01)
BULL ;Generate the bulletin.
 S XMY("G.PSS DEE AUDIT")=""
 S XMSUB="Pharmacy Price Tracker",XMDUZ=.5
 S ^UTILITY($J,"PHARM TRACK",1)=PSSNAME_" has changed the PRICE PER DISPENSE UNIT of:"
 S ^UTILITY($J,"PHARM TRACK",2)=$P($G(^PSDRUG(PSSIEN,0)),"^",1)_" to: "_PSSNEW
 S XMTEXT="^UTILITY($J,""PHARM TRACK""," D ^XMD
 K %,PSSTIME,PSSIEN,PSSNAME,PSSOLD,PSSNEW,PSSDUZ,^UTILITY($J),XMSUB,XMTEXT,XMDUZ
 Q
 ;
