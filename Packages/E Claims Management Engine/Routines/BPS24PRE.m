BPS24PRE ;AITC/PD - Pre-install for BPS*1.0*24 ;2/26/2018
 ;;1.0;E CLAIMS MGMT ENGINE;**24**;;Build 43
 ;;Per VA Directive 6402, this routine should not be modified.
 ;
 Q
 ;
PRE ; Pre-install functions are coded here.
 ;
 D MES^XPDUTL("  Starting pre-install of BPS*1.0*24")
 ;
 ; Perform the next two procedures only the first time this patch is loaded.
 ;
 I '$$PATCH^XPDUTL("BPS*1.0*24") D
 . ;
 . ; Update descriptions in BPS NCPDP FIELD DEFS file #9002313.91
 . D FIELDS
 . ;
 . ; Update segment name in BPS NCPDP SEGMENTS file #9002313.9
 . D UPDSEG
 . Q
 ;
 D MES^XPDUTL("  Finished pre-install of BPS*1.0*24")
 Q
 ;
FIELDS ; Update Fields Defs with new descriptions
 N LINE,DATA,NUM,NAME,DA,DIE,DR,CNT
 D MES^XPDUTL("    - Updating BPS NCPDP FIELD DEFS")
 S CNT=0
 F LINE=1:1 S DATA=$P($T(NFLDS+LINE),";;",2,99) Q:DATA=""  D
 . S NUM=$P(DATA,";",1),NAME=$P(DATA,";",2)
 . S DIE=9002313.91
 . S DA=$O(^BPSF(DIE,"B",NUM,""))
 . I 'DA D MES^XPDUTL("      - No IEN found for entry "_NUM_",field: "_NAME) Q
 . S DR=".03////^S X=NAME",CNT=CNT+1
 . D ^DIE
 D MES^XPDUTL("      - "_CNT_" entries updated")
 D MES^XPDUTL("    - Done with BPS NCPDP FIELD DEFS")
 D MES^XPDUTL(" ")
 Q
 ;
UPDSEG ; Update Segment with new 
 N DA,DIE,DR,NAME,ONAME
 D MES^XPDUTL("    - Updating BPS NCPDP SEGMENTS")
 S ONAME="RESPONSE COORDINATION OF BENEF"
 S NAME="RESPONSE OTHER PAYERS"
 S DIE=9002313.9
 S DA=$O(^BPSF(DIE,"B",ONAME,""))
 I 'DA D MES^XPDUTL("      - No IEN found for entry "_ONAME_",field: "_NAME) Q
 S DR=".01////^S X=NAME"
 D ^DIE
 D MES^XPDUTL("      - entry updated")
 D MES^XPDUTL("    - Done with BPS NCPDP SEGMENTS")
 Q
 ;
NFLDS ; Fields to be updated
 ;;101;IIN NUMBER
 ;;348;BASIS OF CALC - REGULATORY FEE
 ;;349;BASIS OF CALC - % TAX
 ;;433;PATIENT PAID AMOUNT REPORTED
 ;;481;REGULATORY FEE AMT SUBMITTED
 ;;482;PERCENT TAX AMT SUBMITTED
 ;;483;PERCENT TAX RATE SUBMITTED
 ;;484;PERCENTAGE TAX BASIS SBMTTD
 ;;530;PREVIOUS DATE OF SERVICE
 ;;531;QTY OF PREVIOUS DISPENSING
 ;;551;FORMULARY ALT PRODUCT COUNT
 ;;552;FORMULARY ALT ID QUALIFIER
 ;;553;FORMULARY ALTERNATIVE ID
 ;;554;FORMULARY ALT INCENTIVE
 ;;555;FORMULARY ALT COST SHARE
 ;;556;FORMULARY ALT DESCRIPTION
 ;;557;PERCENT TAX EXEMPT INDICATOR
 ;;558;REGULATORY FEE AMOUNT PAID
 ;;559;PERCENTAGE TAX AMOUNT PAID
 ;;560;PERCENTAGE TAX RATE PAID
 ;;561;PERCENTAGE TAX BASIS PAID
 ;;568;PAYER/HEALTH PLAN ID QUALIFIER
 ;;569;PAYER/HEALTH PLAN ID
 ;;575;PATIENT PERCENTAGE TAX AMOUNT
 ;;A28;ADJUDICATED PROGRAM TYPE
 ;;A29;REPORTED ADJUDICATED PROG TYPE
 ;;B56;LAST KNOWN IIN NUMBER
 ;;
 ;
