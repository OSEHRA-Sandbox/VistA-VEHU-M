PSOERX ;ALB/BWF - eRx Utilities/RPC's ; 8/3/2016 5:14pm
 ;;7.0;OUTPATIENT PHARMACY;**467,527**;DEC 1997;Build 30
 ;
EN(SRCH,SORTT) ; -- main entry point for PSO ERX HOLDING QUEUE
 N PSNPINST,PSOREFSH
 I '$$CHKKEY^PSOERX(DUZ) D  Q
 .W !,"You do not have the appropriate key to access this option." S DIR(0)="E" D ^DIR K DIR
 D:'$D(PSOPAR) ^PSOLSET I '$D(PSOPAR) D MSG^PSODPT G EX^PSOERX
 D:'$D(PSOPINST) INST^PSOORFI2 I $G(PSOIQUIT) K PSOIQUIT G EX^PSOERX
 S PSNPINST=$$GET1^DIQ(59,PSOSITE,101,"I")
 I 'PSNPINST W !,"NPI Institution must be defined to continue." S DIR(0)="E" D ^DIR K DIR Q
 D EN^VALM("PSO ERX HOLDING QUEUE")
 Q
 ;
HDR ; -- header code
 S VALMHDR(1)="PSO ERX HOLDING QUEUE"
 I $G(PSOREFSH) K @VALMAR D INIT
 Q
 ;
INIT ; -- init variables and list array
 ;/BLB/ PSO*7.0*527 - BEING CHANGE - SIGNIFICANT CHANGES HAVE OCCURED TO FACILITATE FASTER PROCESSING TIMES IN THE ERX HOLDING QUEUE
 N LINE,LINEVAR,X,SGLOB,EF,ESIEN,CHKSTAT,EARY,SUBS,SUBS2,SVAL,SSTWO,EBDATE,ERXIEN,DTLOOP,PTLOOP,ERXDB,ERXDS,ERX,ERXDAT,P5246IEN,BDOVR,EEDATE,CNT
 S EF=52.49
 S SGLOB=$NA(^TMP("PSOERX",$J)) K @SGLOB
 S PSOSRCH=$S($D(SRCH):1,1:0) I PSOSRCH S PSOSRCH(VALMEVL)=""
 S PSOSRT=$S($D(SORTT):1,1:0) I PSOSRT S PSOSRT(VALMEVL)=""
 ; initialize LINE
 I '$G(PSOPINST) Q
 F CHKSTAT="RJ","RM","PR" D
 .S ESIEN=$$PRESOLV^PSOERXA1(CHKSTAT,"ERX") Q:'ESIEN
 .S EARY(ESIEN)=""
 I $D(SRCH) D
 .I $D(SRCH(1)) S SUBS="EPAT",SVAL=$P(SRCH(1),U) Q
 .I $D(SRCH(4)) S SUBS="EPROV",SVAL=$P(SRCH(4),U) Q
 .I $D(SRCH(2)) S SUBS="F",SUBS2="DOB",SVAL=$P(SRCH(2),U) Q
 .I $D(SRCH(5)) S SUBS="F",SVAL=$P(SRCH(5),U) Q
 .S SUBS="F"
 I '$D(SRCH) S SUBS="F"
 S (LINE,CNT)=0
  I '$D(SVAL) S SVAL=0
 ; Look back 90 days.
 S EBDATE=$S($D(SRCH(3)):$P(SRCH(3),U),1:$$FMADD^XLFDT(DT,-90)),EEDATE=$S($D(SRCH(3)):$P(SRCH(3),U,2),1:DT_".9999")
 ; get lookback days override. Use it if we are not searching by date range.
 S BDOVR=$$GET1^DIQ(59,PSOSITE,10.2,"E") I BDOVR,'$D(SRCH(3)) S EBDATE=$$FMADD^XLFDT(DT,-BDOVR)
 F  S EBDATE=$O(^PS(52.49,SUBS,PSNPINST,EBDATE)) Q:'EBDATE!(EBDATE>EEDATE)!(CNT>998)  D
 .I $G(SUBS2)="DOB" D  Q
 ..S P5246IEN=0 F  S P5246IEN=$O(^PS(52.46,SUBS2,SVAL,P5246IEN)) Q:'P5246IEN!(CNT>998)  D
 ...S ERXIEN=0 F  S ERXIEN=$O(^PS(EF,"EPAT",PSNPINST,EBDATE,P5246IEN,ERXIEN)) Q:'ERXIEN!(CNT>998)  D
 ....D BLDITEM(ERXIEN,.CNT)
 .I SVAL D  Q
 ..S ERXIEN=0 F  S ERXIEN=$O(^PS(EF,SUBS,PSNPINST,EBDATE,SVAL,ERXIEN)) Q:'ERXIEN!(CNT>998)  D
 ...D BLDITEM(ERXIEN,.CNT)
 .S SSTWO=0 F  S SSTWO=$O(^PS(EF,SUBS,PSNPINST,EBDATE,SSTWO)) Q:'SSTWO!(CNT>998)  D
 ..; Filter out RJ, RM, and PR status erx's if we are not searching for a specific eRx status
 ..I '$D(SRCH(5)),$D(EARY(SSTWO)) Q
 ..S ERXIEN=0 F  S ERXIEN=$O(^PS(EF,SUBS,PSNPINST,EBDATE,SSTWO,ERXIEN)) Q:'ERXIEN!(CNT>998)  D
 ...D BLDITEM(ERXIEN,.CNT)
 S DTLOOP=0 F  S DTLOOP=$O(@SGLOB@(DTLOOP)) Q:'DTLOOP  D
 .S PTLOOP="" F  S PTLOOP=$O(@SGLOB@(DTLOOP,PTLOOP)) Q:PTLOOP=""  D
 ..S ERXDB="" F  S ERXDB=$O(@SGLOB@(DTLOOP,PTLOOP,ERXDB)) Q:ERXDB=""  D
 ...S ERXDS="" F  S ERXDS=$O(@SGLOB@(DTLOOP,PTLOOP,ERXDB,ERXDS)) Q:ERXDS=""  D
 ....S ERX=0 F  S ERX=$O(@SGLOB@(DTLOOP,PTLOOP,ERXDB,ERXDS,ERX)) Q:'ERX  D
 .....S LINE=LINE+1,LINEVAR=""
 .....S ERXDAT=$G(@SGLOB@(DTLOOP,PTLOOP,ERXDB,ERXDS,ERX))
 .....S LINEVAR=$$SETFLD^VALM1(LINE_".",LINEVAR,"#")
 .....S LINEVAR=$$SETFLD^VALM1($P(ERXDAT,U),LINEVAR,"PATIENT NAME")
 .....S LINEVAR=$$SETFLD^VALM1($P(ERXDAT,U,2),LINEVAR,"DOB")
 .....S LINEVAR=$$SETFLD^VALM1($P(ERXDAT,U,3),LINEVAR,"DRUG")
 .....S LINEVAR=$$SETFLD^VALM1($P(ERXDAT,U,4),LINEVAR,"PROVIDER")
 .....S LINEVAR=$$SETFLD^VALM1($P(ERXDAT,U,8),LINEVAR,"STA")
 .....S LINEVAR=$$SETFLD^VALM1($P(ERXDAT,U,7),LINEVAR,"RECORD DATE")
 .....D SET^VALM10(LINE,LINEVAR,ERX)
 I LINE=0 S LINE=LINE+1 D SET^VALM10(LINE,"No results for current query.")
 S VALMCNT=LINE
 Q
BLDITEM(ERXIEN,CNT) ;
 N ERXIENS,ERXDAT,PATIEN,PATNM,PTDOB,PTDOBE,EXDS,EXPRIEN,EXPRNM,AUTOST,MANST,ERXSTAT,ERXISTAT,ERXDT,ERXEDT
 S ERXIENS=ERXIEN_","
 K ERXDAT D GETS^DIQ(52.49,ERXIENS,".03;.04;4.9;3.1;2.1;1;1.2;1.3","IE","ERXDAT")
 S PATIEN=$G(ERXDAT(EF,ERXIENS,.04,"I"))
 I $D(SRCH(1)),PATIEN'=$P($G(SRCH(1)),U) Q
 ; patient ien missing, do not process
 Q:'PATIEN
 ; patient name/dob
 S PATNM=$G(ERXDAT(EF,ERXIENS,.04,"E"))
 S PTDOBE=""
 S PTDOB=$$GET1^DIQ(52.46,PATIEN,.08,"I")
 I PTDOB]"" S PTDOBE=$$FMTE^XLFDT(PTDOB,"2D")
 I 'PTDOB S PTDOB="N/A"
 I $D(SRCH(2)),PTDOB'=$G(SRCH(2)) Q
 ; external drug/supply
 S EXDS=$G(ERXDAT(EF,ERXIENS,3.1,"E"))
 I $D(SRCH(6)),EXDS'[$P($G(SRCH(6)),U) Q
 ; external provider ien and name
 S EXPRIEN=$G(ERXDAT(EF,ERXIENS,2.1,"I"))
 I $D(SRCH(4)),EXPRIEN'=$P($G(SRCH(4)),U,1) Q
 ; if there is no external provider, quit - FUTURE ENHANCEMENT - may need to find a way to log, view, and deal with these instances.
 S EXPRNM=$G(ERXDAT(EF,ERXIENS,2.1,"E")) Q:'$L(EXPRNM)
 ; auto and manual validation status
 S AUTOST=$G(ERXDAT(EF,ERXIENS,1.2,"E"))
 S MANST=$G(ERXDAT(EF,ERXIENS,1.3,"E"))
 S ERXSTAT=$G(ERXDAT(EF,ERXIENS,1,"E"))
 S ERXISTAT=$G(ERXDAT(EF,ERXIENS,1,"I"))
 I $D(SRCH(5)),ERXSTAT'=$P(SRCH(5),U,2) Q
 ; date ERX was received
 S ERXDT=$G(ERXDAT(EF,ERXIENS,.03,"I")),ERXEDT=$$FMTE^XLFDT($P(ERXDT,"."),"2D")
 I $D(SRCH(3)),(ERXDT<$P($G(SRCH(3)),U,1))!(ERXDT>$P($G(SRCH(3)),U,2)) Q
 S CNT=CNT+1
 I $G(SORTT) D  Q
 .I SORTT=1 S @SGLOB@(1,PATNM,ERXDT-9999999,EXPRNM,ERXIEN)=PATNM_U_PTDOBE_U_EXDS_U_EXPRNM_U_AUTOST_U_MANST_U_ERXEDT_U_ERXSTAT
 .I SORTT=2 S @SGLOB@(1,PTDOB,ERXDT-9999999,PATNM,ERXIEN)=PATNM_U_PTDOBE_U_EXDS_U_EXPRNM_U_AUTOST_U_MANST_U_ERXEDT_U_ERXSTAT
 .I SORTT=3 S @SGLOB@(1,ERXDT-9999999,PATNM,EXPRNM,ERXIEN)=PATNM_U_PTDOBE_U_EXDS_U_EXPRNM_U_AUTOST_U_MANST_U_ERXEDT_U_ERXSTAT
 .I SORTT=4 S @SGLOB@(1,EXPRNM,ERXDT,PATNM,ERXIEN)=PATNM_U_PTDOBE_U_EXDS_U_EXPRNM_U_AUTOST_U_MANST_U_ERXEDT_U_ERXSTAT
 .I SORTT=5 S @SGLOB@(1,ERXSTAT,PATNM,ERXDT-9999999,ERXIEN)=PATNM_U_PTDOBE_U_EXDS_U_EXPRNM_U_AUTOST_U_MANST_U_ERXEDT_U_ERXSTAT
 .I SORTT=6 S @SGLOB@(1,EXDS,PATNM,ERXDT-9999999,ERXIEN)=PATNM_U_PTDOBE_U_EXDS_U_EXPRNM_U_AUTOST_U_MANST_U_ERXEDT_U_ERXSTAT
 ;S @SGLOB@(ERXDT,PATNM,EXPRNM,ERXIEN)=PATNM_U_PTDOBE_U_EXDS_U_EXPRNM_U_AUTOST_U_MANST_U_ERXEDT_U_ERXSTAT
 S @SGLOB@(ERXDT,PATNM,PTDOB,EXDS,ERXIEN)=PATNM_U_PTDOBE_U_EXDS_U_EXPRNM_U_AUTOST_U_MANST_U_ERXEDT_U_ERXSTAT
 Q
HELP ; -- help code
 S X="?" D DISP^XQORM1 W !!
 Q
 ;
EXIT ; -- exit code
 ;S VALMBCK="R"
 K PSOSRCH(VALMEVL),PSOSRT(VALMEVL),@VALMAR,PSOPRMPT
 I VALMEVL=0 K PSOSRCH,PSOSRT
 D FULL^VALM1
 Q
 ;
EX ; early exit logic
 K PSOSRCH,PSOSRT,SRCH,SORTT,PSOPRMPT
 ;K PSOSRCH,PSOSRT,PSOPAR,PSOPINST
 D EX^PSOORFI1
 Q
EXPND ; -- expand code
 Q
 ;
 ; search list and display results
SEARCH ;
 N RES,SVAL,I,DONE,SRCHARY
 D FULL^VALM1
 S DONE=0
 F I=1:1 D  Q:DONE
 .S RES=$$DIR(,I,.SRCHARY)
 .I '+RES S DONE=1 Q
 .S SRCHARY(+RES)=$P(RES,U,2,99)
 I $D(STYP) D EN(.SRCHARY,STYP) Q
 D EN(.SRCHARY)
 ;S VALMBCK="R"
 Q
 ; sort target list
SORT ;
 N RES,STYP,SVAL
 D FULL^VALM1
 S RES=$$DIR(1,0)
 I '+$P(RES,U) Q
 S STYP=$P(RES,U)
 I $D(SRCHARY) D EN(.SRCHARY,STYP) Q
 D EN(,STYP)
 Q
DIR(SORT,CNT,SLIST) ;
 N DIR,Y,RLINE,STAG,SVAL,REFCHK
 K DIR
 S DIR(0)="SO^1:PATIENT NAME;2:DATE OF BIRTH;3:RECEIVED DATE RANGE;4:PROVIDER NAME;5:ERX STATUS;6:DRUG NAME"
 I CNT<2 S DIR("L")="Select one of the following "_$S($G(SORT):"sort",1:"search")_" criteria:"
 I CNT>1 D
 .S DIR("L")=""
 .S DIR("L",11)="Select another search criteria or '^' to exit. Press enter to use the currently"
 .S DIR("L",12)="selected search criteria."
 S DIR("L",2)=""
 S DIR("L",3)=" "_$S($D(SLIST(1)):"*",1:"")_"1.) PATIENT NAME"
 S DIR("L",4)=" "_$S($D(SLIST(2)):"*",1:"")_"2.) DATE OF BIRTH"
 S DIR("L",5)=" "_$S($D(SLIST(3)):"*",1:"")_"3.) RECEIVED DATE"_$S('$G(SORT):" RANGE",1:"")
 S DIR("L",6)=" "_$S($D(SLIST(4)):"*",1:"")_"4.) PROVIDER NAME"
 S DIR("L",7)=" "_$S($D(SLIST(5)):"*",1:"")_"5.) ERX STATUS"
 S DIR("L",8)=" "_$S($D(SLIST(6)):"*",1:"")_"6.) DRUG NAME"
 S DIR("L",9)=""
 S DIR("L",10)=$S($D(SLIST):" * - indicates selected criteria.",1:"")
 D ^DIR K DIR Q:'Y 0
 S RES=Y I $G(SORT) Q RES
 S RLINE=$S(RES=1:"PAT",RES=2:"DOB",RES=3:"RDT",RES=4:"PRVNM",RES=5:"ESTAT",RES=6:"DNAME",1:"")
 I RLINE']"" Q 0
 S STAG=RLINE
 S SVAL=$$@STAG I SVAL="" Q 0
 Q RES_U_SVAL
PAT() ;
 N Y,DIC
 S DIC=52.46,DIC(0)="AEMQ" D ^DIC
 I Y<1 Q ""
 Q Y
DOB() ;
 N %DT,Y
 S %DT="A"
 S %DT("A")="Enter the Date of Birth (DOB): "
 D ^%DT
 I Y<1 Q ""
 Q Y
RDT() ;
 N BDATE,EDATE,%DT,Y
 S %DT="A"
 S %DT("A")="Enter the beginning date: "
 D ^%DT
 I Y<0 Q ""
 S BDATE=Y K Y,%DT
 S %DT="A"
 S %DT("A")="Enter the ending date: "
 S %DT("B")="T"
 D ^%DT
 I Y<0 Q ""
 S EDATE=Y_".999999"
 Q BDATE_U_EDATE
PRVNM() ;
 N Y,DIC
 S DIC("A")="Select PROVIDER: "
 S DIC=52.48,DIC(0)="AEQ" D ^DIC
 I Y<1 Q ""
 Q Y
ESTAT() ;
 ; prompt for erx status
 N Y,DIC
 S DIC("A")="Select eRx Status: "
 S DIC=52.45,DIC(0)="AEQ",DIC("S")="I $D(^PS(52.45,""TYPE"",""ERX"",Y))" D ^DIC
 I Y<1 Q ""
 Q Y
DNAME() ;
 N DIR,Y
 S DIR(0)="FO"
 S DIR("A")="Enter the name or partial name of the incoming eRx drug"
 D ^DIR
 I Y=""!(Y="^") Q ""
 Q $$UP^XLFSTR(Y)
CHKKEY(DUZ) ;
 I $D(^XUSEC("PSDRPH",DUZ))!($D(^XUSEC("PSO ERX ADV TECH",DUZ)))!($D(^XUSEC("PSO ERX TECH",DUZ)))!($D(^XUSEC("PSO ERX VIEW",DUZ))) Q 1
 Q 0
