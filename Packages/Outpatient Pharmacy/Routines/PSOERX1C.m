PSOERX1C ;ALB/BWF - eRx Utilities ; 8/3/2016 5:14pm
 ;;7.0;OUTPATIENT PHARMACY;**467,520,527,508,551**;DEC 1997;Build 37
 ;
 Q
 ; select an item
PRINT(PSOIEN,OP) ;
 N %ZIS,POP
 S OP=$G(OP)
 D FULL^VALM1
 S VALMBCK="R"
 S %ZIS="Q",%ZIS("B")=$G(PSOPROP) D ^%ZIS Q:POP
 ; if queuing, queue it and quit.
 I $D(IO("Q")) D  Q
 .S ZTSAVE("PSOIEN")="",ZTSAVE("OP")=""
 .S ZTRTN="PRINTQ^PSOERX1C(PSOIEN,OP)",ZTDESC="eRx Print" D ^%ZTLOAD
 .K ZTSAVE,ZTRTN
 .D INIT^PSOERX1
 .D TERM^VALM0
 D PRINTQ(PSOIEN,OP)
 D TERM^VALM0
 D INIT^PSOERX1
 Q
 ; fallthrough if no queueing
PRINTQ(PSOIEN,OP) ;
 N %ZIS,POP,RXDAT,PHARIEN,PRVIEN,PATIEN,PHARDAT,PRVDAT,PATDAT,PSOIENS,PHNM,PHAD1,PHAD2,PHCTY,LINE,LOOP
 N PHST,PHZIP,PHNCP,PHTEL,PRFNM,PRLNM,PRMNM,PRAD1,PRAD2,PRCTY,PRST,PRZIP,PRNPI,PRDEA,PRSTL,LTXT,MTYPE,NEWRXIEN
 N PRTEL,PRFAX,PRSUPER,PRAGNT,PTLNM,PTFNM,PTMNM,PTAD1,PTAD2,PTCTY,PTST,PTZIP,PTDOB,PTGEN,PTHPHON,PTPLANID
 N PHARIENS,PATIENS,PRVIENS,PTSSN,PRAGNTFN,PRAGNTMN,PRAGNTLN,TYPE,VALUE,PRFAX,PRTEL,SIEN,REFILL,IENS,CANREQ,CANRES,ERXDSUB
 S OP=$G(OP,"")
 S PSOIEN=$G(PSOIEN)
 I '$G(PSOIEN) D
 .I $D(RXOR) S PSOIEN=$$CHKERX^PSOERXU1($P(RXOR,U,2)) Q:PSOIEN
 .I $D(OR0) S PSOIEN=$$CHKERX^PSOERXU1(OR0)
 Q:'$G(PSOIEN)
 S MTYPE=$$GET1^DIQ(52.49,PSOIEN,.08,"I")
 I MTYPE="CN"!(MTYPE="RE")!(MTYPE="IE") D
 .S CANRES=PSOIEN
 .S CANREQ=$$GETREQ^PSOERXU2(PSOIEN)
 .I $G(CANREQ) S NEWRXIEN=$$RESOLV^PSOERXU2(CANREQ)
 .; if there is no NewRx related to this request, pull the data from the cancel request itself.
 .I '$P(NEWRXIEN,U) S NEWRXIEN=$G(CANREQ)
 I MTYPE="CA"!(MTYPE="RR") D
 .S CANREQ=PSOIEN,CANRES=$$GETRESP^PSOERXU2(CANREQ)
 .S NEWRXIEN=$$RESOLV^PSOERXU2(CANREQ)
 .I '$P(NEWRXIEN,U) S NEWRXIEN=$G(CANREQ)
 S PSOIENS=$S($G(NEWRXIEN):NEWRXIEN_",",1:PSOIEN_",")
 ; if we found the newrxien because of a different message type, reset PSOIEN to pull rx info from the newrx.
 I $G(NEWRXIEN) S PSOIEN=NEWRXIEN
 I $D(ZTQUEUED) S ZTREQ="@"
 U IO
 D GETS^DIQ(52.49,PSOIENS,".04;.08;2.1;2.5","I","RXDAT")
 S PHARIEN=$G(RXDAT(52.49,PSOIENS,2.5,"I")) I PHARIEN S PHARIENS=PHARIEN_","
 S PRVIEN=$G(RXDAT(52.49,PSOIENS,2.1,"I")) I PRVIEN S PRVIENS=PRVIEN_","
 S PATIEN=$G(RXDAT(52.49,PSOIENS,.04,"I")) I PATIEN S PATIENS=PATIEN_","
 I $D(PHARIENS) D GETS^DIQ(52.47,PHARIENS,"**","E","PHARDAT")
 I $D(PRVIENS) D GETS^DIQ(52.48,PRVIENS,"**","E","PRVDAT")
 I $D(PATIENS) D GETS^DIQ(52.46,PATIENS,"**","E","PATDAT")
 ; pharmacy information
 S PHNM=$G(PHARDAT(52.47,PHARIENS,.05,"E"))
 S PHAD1=$G(PHARDAT(52.47,PHARIENS,1.1,"E"))
 S PHAD2=$G(PHARDAT(52.47,PHARIENS,1.2,"E"))
 S PHCTY=$G(PHARDAT(52.47,PHARIENS,1.3,"E"))
 S PHST=$G(PHARDAT(52.47,PHARIENS,1.4,"E"))
 S PHZIP=$G(PHARDAT(52.47,PHARIENS,1.5,"E"))
 S PHNCP=$G(PHARDAT(52.47,PHARIENS,.02,"E"))
 ; future - get telephone
 S PHTEL=""
 ; provider information
 S PRFNM=$G(PRVDAT(52.48,PRVIENS,.03,"E"))
 S PRMNM=$G(PRVDAT(52.48,PRVIENS,.04,"E"))
 S PRLNM=$G(PRVDAT(52.48,PRVIENS,.02,"E"))
 S PRAD1=$G(PRVDAT(52.48,PRVIENS,4.1,"E"))
 S PRAD2=$G(PRVDAT(52.48,PRVIENS,4.2,"E"))
 S PRCTY=$G(PRVDAT(52.48,PRVIENS,4.3,"E"))
 S PRST=$G(PRVDAT(52.48,PRVIENS,4.4,"E"))
 S PRZIP=$G(PRVDAT(52.48,PRVIENS,4.5,"E"))
 S PRNPI=$G(PRVDAT(52.48,PRVIENS,1.5,"E"))
 S PRDEA=$G(PRVDAT(52.48,PRVIENS,1.6,"E"))
 S PRSTL=$G(PRVDAT(52.48,PRVIENS,1.8,"E"))
 I '$G(NEWRXIEN) S PRSUPER=$$GET1^DIQ(52.49,PSOIEN,2.6,"E")
 I $G(NEWRXIEN) S PRSUPER=$$GET1^DIQ(52.49,NEWRXIEN,2.6,"E")
 S PRAGNTFN=$$GET1^DIQ(52.48,PRVIEN,5.2,"E")
 S PRAGNTMN=$$GET1^DIQ(52.48,PRVIEN,5.3,"E")
 S PRAGNTLN=$$GET1^DIQ(52.48,PRVIEN,5.1,"E")
 ;/BLB/ PSO*7.0*520  - BEGIN CHANGE
 S SIEN=0
 F  S SIEN=$O(^PS(52.48,PRVIEN,3,SIEN)) Q:'SIEN  D
 .S IENS=SIEN_","_PRVIEN_","
 .S TYPE=$$GET1^DIQ(52.483,IENS,.02)
 .S VALUE=$$GET1^DIQ(52.483,IENS,.01)
 .I TYPE="FAX" S PRFAX=VALUE
 .I TYPE="TELEPHONE" S PRTEL=VALUE
 ;/BLB/ - END CHANGE
 ; patient information
 S PTLNM=$G(PATDAT(52.46,PATIENS,.02,"E"))
 S PTMNM=$G(PATDAT(52.46,PATIENS,.04,"E"))
 S PTFNM=$G(PATDAT(52.46,PATIENS,.03,"E"))
 S PTAD1=$G(PATDAT(52.46,PATIENS,3.1,"E"))
 S PTAD2=$G(PATDAT(52.46,PATIENS,3.2,"E"))
 S PTCTY=$G(PATDAT(52.46,PATIENS,3.3,"E"))
 S PTST=$G(PATDAT(52.46,PATIENS,3.4,"E"))
 S PTZIP=$G(PATDAT(52.46,PATIENS,3.5,"E"))
 S PTDOB=$G(PATDAT(52.46,PATIENS,.08,"E"))
 S PTGEN=$G(PATDAT(52.46,PATIENS,.07,"E"))
 S PTSSN=$G(PATDAT(52.46,PATIENS,1.4,"E"))
 S PTHPHON=""
 S PTPLANID=""
 W !,"*************************PHARMACY INFORMATION****************************"
 W !,$$UP^XLFSTR(PHNM)
 W !,"Address: "_PHAD1
 I $L(PHAD2) W !,"         "_PHAD2
 W !,!,"         "_PHCTY_", "_PHST_" "_PHZIP
 S LTXT=""
 D ADDITEM^PSOERX1A(.LTXT,"Tel: ",PHTEL,1,26)
 D ADDITEM^PSOERX1A(.LTXT,"NCPDP: ",PHNCP,28,26)
 W !,LTXT S LTXT=""
 W !,"******************PRESCRIBER INFORMATION*********************************"
 ; /BLB/ PSO*7.0*520 - CHANGED ORDER OF NAMES TO LAST,FIRST,MIDDLE - BEGIN CHANGE
 W !,"Last: "_PRLNM
 W !,"First: "_PRFNM
 W !,"Mid: "_PRMNM
 ; /BLB/ - END CHANGE
 W !,"Address: "_PRAD1
 I $L(PHAD2) W !,"         "_PRAD2
 W !,"         "_PRCTY_", "_PRST_" "_PRZIP
 S LTXT="" ;/BLB/ PSO*7.0*527 - BEGIN CHANGE - MOVE FIELDS TO THEIR OWN LINE
 W !,"NPI: "_PRNPI
 W !,"DEA: "_PRDEA
 W !,"State Lic: "_PRSTL
 ;/BLB/ - PSO*7.0*527 END CHANGE
 W !,LTXT S LTXT=""
 ;/BLB/ PSO*7.0*520 - BEGIN CHANGE
 W !,"Tel: "_$G(PRTEL)
 W !,"Fax: "_$G(PRFAX)
 W !,LTXT S LTXT=""
 W !,"Supervisor: "_$G(PRSUPER)
 W !,"Agent Last Name: "_$G(PRAGNTLN)
 W !,"Agent First Name: "_$G(PRAGNTFN)
 W !,"Agent Middle Name: "_$G(PRAGNTMN)
 ;/BLB/ - END CHANGE
 W !,"******************PATIENT INFORMATION************************************"
 S LTXT=""
 ;/BLB/PSO*7.0*520 RESTRUCTURED ORDER OF PATIENT NAME - BEGIN CHANGE
 W !,"Last: "_PTLNM
 W !,"First: "_PTFNM
 W !,"Mid: "_PTMNM
 ;/BLB END CHANGE
 W !,LTXT
 S LTXT=""
 D ADDITEM^PSOERX1A(.LTXT,"SSN: ",PTSSN,1,28)
 D ADDITEM^PSOERX1A(.LTXT,"Sex: ",PTGEN,30,20) W !,LTXT S LTXT=""
 W !,"Address: "_PTAD1
 I $L(PTAD2) W !,"         "_PTAD2
 W !,"         "_PTCTY_", "_PTST_" "_PTZIP
 S LTXT=""
 D ADDITEM^PSOERX1A(.LTXT,"DOB: ",PTDOB,1,26)
 D ADDITEM^PSOERX1A(.LTXT,"Home: ",PTHPHON,28,16)
 D ADDITEM^PSOERX1A(.LTXT,"Plan ID: ",PTPLANID,52,27)
 W !,LTXT S LTXT=""
 W !,"******************PRESCRIPTION INFORMATION*******************************"
 W !,"eRx Drug: "_$$GET1^DIQ(52.49,PSOIEN,3.1,"E")
 S LTXT=""
 D ADDITEM^PSOERX1A(.LTXT,"Qty: ",$$GET1^DIQ(52.49,PSOIEN,5.1,"E"),1,26)
 D ADDITEM^PSOERX1A(.LTXT,"Days Supply: ",$$GET1^DIQ(52.49,PSOIEN,5.5,"E"),28,16)
 ;/BLB/ PSO*7.0*520 - BEGIN CHANGE - MOVING "Written Date" TO ITS OWN LINE
 ;D ADDITEM^PSOERX1A(.LTXT,"Date Written: ",$$FMTE^XLFDT($$GET1^DIQ(52.49,PSOIEN,5.9,"E"),"2D"),52,27)
 W !,"Date Written: "_$$FMTE^XLFDT($$GET1^DIQ(52.49,PSOIEN,5.9,"E"),"2D")
 ;/BLB/ - END CHANGE
 W !,LTXT S LTXT=""
 ;/BLB/PSO*7.0*520 REMOVED ADDITEM CALLS AND MOVED FIELDS TO INDIVIDUAL LINES - BEGIN CHANGE
 ;/BLB/ PSO*7.0*551 - BEGIN CHANGE - CORRECTING QTY QUAL TO CODE LIST QUAL
 W !,"Code List Qualifier: "_$$GET1^DIQ(52.49,PSOIEN,5.2,"E")
 ;/BLB/ PSO*7.0*551 - END CHANGE
 W !,"Drug Form: "_$$GET1^DIQ(52.49,PSOIEN,41,"E")
 W !,"Strength: "_$$GET1^DIQ(52.49,PSOIEN,43,"E")
 ;/BLB/ - END CHANGE
 W !,LTXT S LTXT=""
 ;/BLB/ PSO*7.0*520 - BEGIN CHANGE
 S REFILL=$$GET1^DIQ(52.49,PSOIEN,5.6,"E")
 I REFILL'="" D
 .W !,"Refills: "_REFILL
 ;/BLB/ PSO*7.0*551 - BEGIN CHANGE - CHANGED TEXT FROM "DO NOT SUB" TO "SUBSTITUTIONS? :"
 S ERXDSUB=$$GET1^DIQ(52.49,PSOIEN,5.8,"I")
 S ERXDSUB=$S(ERXDSUB=1:"NO",ERXDSUB=0:"YES",1:"")
 W !,"Substitutions?: "_ERXDSUB
 ;/BLB/ PSO*7.0*551 - END CHANGE
 I REFILL="" D
 .S REFILL=$$GET1^DIQ(52.49,PSOIEN,5.7,"I")
 .W !,"Refills: "_REFILL
 ;/BLB/ - END CHANGE
 W !,"SIG: "_$$GET1^DIQ(52.49,PSOIEN,7,"E")
 D ADDITEM^PSOERX1A(.LTXT,"eRx Reference #: ",$$GET1^DIQ(52.49,PSOIEN,.01,"E"),1,40)
 D ADDITEM^PSOERX1A(.LTXT,"Message ID: ",$$GET1^DIQ(52.49,PSOIEN,25,"E"),42,35)
 W !!,LTXT S LTXT=""
 S ERXDSUB=$$GET1^DIQ(52.49,PSOIEN,5.8,"I")
 S ERXDSUB=$S(ERXDSUB=1:"NO",ERXDSUB=0:"YES",1:"")
 W !,"Substitutions?: "_ERXDSUB
 W !,"Comments: "_$$GET1^DIQ(52.49,PSOIEN,8,"E")
 I MTYPE="CA"!(MTYPE="RR") D
 .S LINE=1 K @VALMAR
 .I MTYPE="CA" D CANREQ^PSOERXU5(CANREQ,.LINE,1),CANRES^PSOERXU5(CANREQ,.LINE,1),MSGHIS^PSOERXU3(CANREQ,.LINE)
 .I MTYPE="RR" D MEDDIS^PSOERXU3(CANREQ,"D",.LINE),RRREQ^PSOERXU3(CANREQ,.LINE),RRRES^PSOERXU3(CANREQ,.LINE),MSGHIS^PSOERXU3(CANREQ,.LINE)
 .S LOOP=0 F  S LOOP=$O(@VALMAR@(LOOP)) Q:'LOOP  D
 ..W !,$G(@VALMAR@(LOOP,0))
 .K @VALMAR
 I MTYPE="CN"!(MTYPE="RE") D
 .S LINE=1 K @VALMAR
 .I MTYPE="CN" D CANRES^PSOERXU5(CANRES,.LINE,1),CANREQ^PSOERXU5(CANRES,.LINE,1),MSGHIS^PSOERXU3(CANRES,.LINE)
 .I MTYPE="RE" D MEDDIS^PSOERXU3(CANRES,"D",.LINE),RRRES^PSOERXU3(CANRES,.LINE),RRREQ^PSOERXU3(CANRES,.LINE),MSGHIS^PSOERXU3(CANRES,.LINE)
 .S LOOP=0 F  S LOOP=$O(@VALMAR@(LOOP)) Q:'LOOP  D
 ..W !,$G(@VALMAR@(LOOP,0))
 .K @VALMAR
 W !,"*******************************END OF eRx********************************"
 D:'$D(ZTQUEUED) ^%ZISC
 Q
