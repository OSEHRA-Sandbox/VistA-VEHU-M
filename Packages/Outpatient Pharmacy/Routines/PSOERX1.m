PSOERX1 ;ALB/BWF - eRx Utilities/RPC's ; 8/3/2016 5:14pm
 ;;7.0;OUTPATIENT PHARMACY;**467,520,527,508,551**;DEC 1997;Build 37
 ;
EN(PSOIEN) ; -- main entry point for PSO ERX HOLDING QUEUE
 D EN^VALM("PSO ERX HQ DISPLAY")
 Q
 ;
HDR ; -- header code
 S VALMHDR(1)="eRx Patient: "_$$GET1^DIQ(52.49,PSOIEN,.04,"E")
 S VALMHDR(2)="eRx Reference #: "_$$GET1^DIQ(52.49,PSOIEN,.01,"E")
 I VALMBCK="R" K @VALMAR S VALMBCK="" D INIT
 Q
 ;
INIT ;
 Q:'$G(PSOIEN)
 N PATIEN,PHARMIEN,PRVIEN,PHDAT,PATDAT,SUPDAT,PRVDAT,LINE,PRVLNM,PRVMI,PRVFN,EPAT,EPATDOB,VPATIEN,HARY,HL
 N VPATNM,VPATDOB,EPRVIEN,EPRVNM,EPRVNPI,VAPRVIEN,VAPRVNM,VAPRVNPI,SUPIEN,ERXDRUG,ERXQTY,ERXFLS,CSCOMM
 N ERXDS,ERXDT,VADRG,LINETXT,ERXCOMM,ERXRFLS,PATIENS,VAHREA,VAQTY,VAREF,VASIG,PATDAT,CURSTATE,CURSTATI
 N LHFOUND,LHMATCH,LHSTATI,VAPDEAEX,ERXCOMM,COMFRST,COMARY,SIGDATA,SIGLOOP,SFIRST,SGLOOP,SIGARY,VADAYS,NRXIEN
 N SLOOP,VASIG,VASARY,FSSIG,VAHSTA,EDIRECT,VAHPER,PAMANVAL,DRMANVAL,PRMANVAL,VPATINST,VAPIARY,VLOOP,FSVPIN
 N DRGARY,DLP,MTYPE,MTYPEE,ERXSTAT,STATIEN,PDIAGTXT,SDIAGTXT,RELIEN,RELMTYPE,ERRIEN,LERXSTAT,LOPSTAT,OPIEN,ERXDSUB,COM
 S LINE=0
 S PATIEN=$$GET1^DIQ(52.49,PSOIEN,.04,"I") I 'PATIEN S PATIEN=$$GETPAT^PSOERXU5(PSOIEN)
 S PATIENS=PATIEN_","
 D GETS^DIQ(52.46,PATIENS,"**","IE","PATDAT")
 S EPAT=$G(PATDAT(52.46,PATIENS,.01,"E"))
 S EPATDOB=$G(PATDAT(52.46,PATIENS,.08,"I")),EPATDOB=$$FMTE^XLFDT(EPATDOB,"2D")
 S VPATIEN=$$GET1^DIQ(52.49,PSOIEN,.05,"I")
 S VPATNM=$S(VPATIEN:$$GET1^DIQ(2,VPATIEN,.01,"E"),1:"NOT LINKED")
 S VPATDOB=$S(VPATIEN:$$GET1^DIQ(2,VPATIEN,.03,"I"),1:"N/A")
 I VPATDOB S VPATDOB=$$FMTE^XLFDT(VPATDOB,"2D")
 S PHARMIEN=$$GET1^DIQ(52.49,PSOIEN,2.5,"I")
 D GETS^DIQ(52.47,PHARMIEN,"**","E","PHDAT")
 S EPRVIEN=$$GET1^DIQ(52.49,PSOIEN,2.1,"I") I 'EPRVIEN S EPRVIEN=$$GETPROV^PSOERXU5(PSOIEN)
 S EPRVNM=$$GET1^DIQ(52.48,EPRVIEN,.01,"E")
 S EPRVNPI=$$GET1^DIQ(52.48,EPRVIEN,1.5,"E")
 S VAPRVIEN=$$GET1^DIQ(52.49,PSOIEN,2.3,"I")
 S EDIRECT=$$GET1^DIQ(52.49,PSOIEN,7,"E")
 S VAPRVNM=$S(VAPRVIEN:$$GET1^DIQ(200,VAPRVIEN,.01,"E"),1:"NOT LINKED")
 S VAPRVNPI=$S(VAPRVIEN:$$GET1^DIQ(200,VAPRVIEN,41.99,"E"),1:"N/A")
 D GETS^DIQ(52.48,EPRVIEN,"**","E","PRVDAT")
 S SUPIEN=$$GET1^DIQ(52.49,PSOIEN,2.6,"I")
 D GETS^DIQ(52.48,SUPIEN,"**","E","SUPDAT")
 S PAMANVAL=$$GET1^DIQ(52.49,PSOIEN,1.7,"I")
 S PRMANVAL=$$GET1^DIQ(52.49,PSOIEN,1.3,"I")
 S DRMANVAL=$$GET1^DIQ(52.49,PSOIEN,1.5,"I")
 S MTYPE=$$GET1^DIQ(52.49,PSOIEN,.08,"I")
 S MTYPEE=$$GET1^DIQ(52.49,PSOIEN,.08,"E")
 S STATIEN=$$GET1^DIQ(52.49,PSOIEN,1,"I")
 S ERXSTAT=$$GET1^DIQ(52.45,STATIEN,.02,"E")
 ; only set the hold reason if the eRx has a hold status
 S CURSTATE=$$GET1^DIQ(52.49,PSOIEN,1,"E")
 S VAHREA=""
 I $E(CURSTATE,1)="H" D
 .S CURSTATI=$$GET1^DIQ(52.49,PSOIEN,1,"I")
 .S LHMATCH=999999,LHFOUND=0 F  S LHMATCH=$O(^PS(52.49,PSOIEN,19,LHMATCH),-1) Q:'LHMATCH!(LHFOUND)  D
 ..S LHSTATI=$$GET1^DIQ(52.4919,LHMATCH_","_PSOIEN_",",.02,"I") I LHSTATI=CURSTATI D  S LHFOUND=LHMATCH Q
 ...S VAHREA=$$GET1^DIQ(52.4919,LHMATCH_","_PSOIEN_",",1)
 ...S VAHSTA=$$GET1^DIQ(52.45,LHSTATI,.01,"E")_" - "_$$GET1^DIQ(52.45,LHSTATI,.02,"E")
 ...S VAHPER=$$GET1^DIQ(52.4919,LHMATCH_","_PSOIEN_",",.03,"E")
 I MTYPE="RE"!(MTYPE="CN") S MTYPEE=$G(MTYPEE)_" - "_$$GET1^DIQ(52.49,PSOIEN,52.1,"E")
 S LINETXT=""
 S LINE=LINE+1 D SET^VALM10(LINE,MTYPEE),CNTRL^VALM10(LINE,1,$L(MTYPEE),IORVON,IORVOFF)
 S LINE=LINE+1 D SET^VALM10(LINE,"eRx Status: "_$S($E(CURSTATE,1)="H":VAHSTA,1:ERXSTAT))
 I MTYPE="CA" D
 .S NRXIEN=$$RESOLV^PSOERXU2(PSOIEN) Q:'NRXIEN
 .S LERXSTAT=$$LASTSTAT^PSOERXU5(NRXIEN)
 .; over-ride for new rx's that have no status history
 .I '$L(LERXSTAT) S LERXSTAT="N - NEW"
 .S OPIEN=$$GET1^DIQ(52.49,NRXIEN,.13,"I") Q:'OPIEN
 .S LOPSTAT=$$GET1^DIQ(52,OPIEN,100,"E")
 .I NRXIEN S CSCOMM=$$CSCOMM^PSOERXU5(NRXIEN)
 I MTYPE="CN" D
 .S RELIEN=$$RESOLV^PSOERXU2(PSOIEN) Q:'RELIEN
 .S NRXIEN=$$RESOLV^PSOERXU2(RELIEN)
 .S LERXSTAT=$$LASTSTAT^PSOERXU5(NRXIEN)
 .I NRXIEN S CSCOMM=$$CSCOMM^PSOERXU5(NRXIEN)
 .I '$L(LERXSTAT) S LERXSTAT="N - NEW"
 .S OPIEN=$$GET1^DIQ(52.49,NRXIEN,.13,"I") Q:'OPIEN
 .S LOPSTAT=$$GET1^DIQ(52,OPIEN,100,"E")
 I $G(CSCOMM)]"" S LINE=LINE+1 D SET^VALM10(LINE,"Current Status Details: "_CSCOMM)
 I $D(LERXSTAT) S LINE=LINE+1 D SET^VALM10(LINE,"Last New Rx status: "_LERXSTAT)
 I $D(LOPSTAT) S LINE=LINE+1 D SET^VALM10(LINE,"Outpatient Prescription status: "_LOPSTAT)
 S LINE=LINE+1 D SET^VALM10(LINE,"")
 I "RRRE"[MTYPE S LINE=LINE+1 D SET^VALM10(LINE,"**************************MEDICATION PRESCRIBED******************************")
 S LINE=LINE+1
 ;/BLB/ - PSO*7.0*520 BEGIN CHANGE
 D ADDITEM^PSOERX1A(.LINETXT,"eRx Patient: ",$E(EPAT,1,39),1,52)
 D ADDITEM^PSOERX1A(.LINETXT,"DOB: ",EPATDOB,57,20)
 D SET^VALM10(LINE,LINETXT) S LINETXT=""
 I $L(EPAT)>39 D
 .S LINE=LINE+1 D SET^VALM10(LINE,"             "_$E(EPAT,40,78))
 I $L(EPAT)>78 D
 .S LINE=LINE+1 D SET^VALM10(LINE,"             "_$E(EPAT,79,135))
 ;/BLB/ - END CHANGE
 S LINETXT=""
 S LINE=LINE+1
 I MTYPE'="CA",MTYPE'="CN" D
 .D ADDITEM^PSOERX1A(.LINETXT,"Vista Patient"_$S(PAMANVAL:"[v]",1:"")_": ",$E(VPATNM,1,55),1,55)
 .D ADDITEM^PSOERX1A(.LINETXT,"DOB: ",VPATDOB,57,20)
 .D SET^VALM10(LINE,LINETXT) S LINETXT=""
 .S LINE=LINE+1 D SET^VALM10(LINE,"")
 .S LINE=LINE+1
 ;/BLB/ PSO*7.0*520 - BEGIN CHANGE
 D ADDITEM^PSOERX1A(.LINETXT,"eRx Provider: ",$E(EPRVNM,1,39),1,52)
 D ADDITEM^PSOERX1A(.LINETXT,"NPI: ",EPRVNPI,57,20)
 D SET^VALM10(LINE,LINETXT) S LINETXT=""
 I $L(EPRVNM)>39 D
 .S LINE=LINE+1 D SET^VALM10(LINE,"              "_$E(EPRVNM,40,77))
 I $L(EPRVNM)>77 D
 .S LINE=LINE+1 D SET^VALM10(LINE,"              "_$E(EPRVNM,78,135))
 ;/BLB/ - END CHANGE
 I MTYPE'="CA",MTYPE'="CN" D
 .S LINE=LINE+1
 .D ADDITEM^PSOERX1A(.LINETXT,"Vista Provider"_$S(PRMANVAL:"[v]",1:"")_": ",$E(VAPRVNM,1,55),1,55)
 .D ADDITEM^PSOERX1A(.LINETXT,"NPI: ",VAPRVNPI,57,20)
 .D SET^VALM10(LINE,LINETXT) S LINETXT=""
 S LINE=LINE+1 D SET^VALM10(LINE,"")
 S ERXDRUG=$$GET1^DIQ(52.49,PSOIEN,3.1,"E") I '$L(ERXDRUG) S ERXDRUG=$$GETDRUG^PSOERXU5(PSOIEN)
 S ERXQTY=$$GET1^DIQ(52.49,PSOIEN,5.1,"E")
 S ERXRFLS=$$GET1^DIQ(52.49,PSOIEN,5.6,"E")
 ; refills for a refill response is # or refills-1
 I MTYPE="RE",ERXRFLS S ERXRFLS=ERXRFLS-1
 ;/BLB/ PSO*7.0*520 - BEGIN CHANGE
 I ERXRFLS="" D
 .S ERXRFLS=$$GET1^DIQ(52.49,PSOIEN,5.7,"I")
 ;/BLB/ - END CHANGE
 ;I MTYPE="RE",ERXRFLS S ERXRFLS=ERXRFLS-1
 S ERXDS=$$GET1^DIQ(52.49,PSOIEN,5.5,"E")
 S ERXDT=$$GET1^DIQ(52.49,PSOIEN,.03,"E")
 S VADRG=$$GET1^DIQ(52.49,PSOIEN,3.2,"E")
 S VAREF=$$GET1^DIQ(52.49,PSOIEN,20.5,"E")
 S VAQTY=$$GET1^DIQ(52.49,PSOIEN,20.1,"E")
 S VADAYS=$$GET1^DIQ(52.49,PSOIEN,20.2,"E")
 I VADRG']"" S VADRG="NOT LINKED"
 D TXT2ARY^PSOERXD1(.DRGARY,ERXDRUG,,70)
 S LINE=LINE+1 D SET^VALM10(LINE,"eRx Drug: "_$G(DRGARY(1)))
 S DLP=1
 F  S DLP=$O(DRGARY(DLP)) Q:'DLP  D
 .S LINE=LINE+1 D SET^VALM10(LINE,"          "_$G(DRGARY(DLP)))
 S LINE=LINE+1
 D ADDITEM^PSOERX1A(.LINETXT,"eRx Qty: ",ERXQTY,1,17)
 D ADDITEM^PSOERX1A(.LINETXT,"eRx Refills: ",ERXRFLS,19,16)
 D ADDITEM^PSOERX1A(.LINETXT,"eRx Days Supply: ",ERXDS,37,20)
 D ADDITEM^PSOERX1A(.LINETXT,"eRx Date: ",$P(ERXDT,"@"),58,22)
 D SET^VALM10(LINE,LINETXT) S LINETXT=""
 D TXT2ARY^PSOERXD1(.SIGARY,EDIRECT,,70)
 S SFIRST=$O(SIGARY(0))
 S SGLOOP=0 F  S SGLOOP=$O(SIGARY(SGLOOP)) Q:'SGLOOP  D
 .S LINE=LINE+1 D SET^VALM10(LINE,$S(SGLOOP=SFIRST:"eRx Sig: ",1:"         ")_$G(SIGARY(SGLOOP)))
 ; PSO*7*508 - if this is a refill request, response, or inbound error build the proper segments, then quit.
 I MTYPE="RR" D  S VALMCNT=LINE Q
 .D MEDDIS^PSOERXU3(PSOIEN,"D",.LINE),RRRES^PSOERXU3(PSOIEN,.LINE),RRREQ^PSOERXU3(PSOIEN,.LINE),MSGHIS^PSOERXU3(PSOIEN,.LINE)
 I MTYPE="RE" D  S VALMCNT=LINE Q
 .D RRRES^PSOERXU3(PSOIEN,.LINE),MEDDIS^PSOERXU3(PSOIEN,"D",.LINE),RRREQ^PSOERXU3(PSOIEN,.LINE),MSGHIS^PSOERXU3(PSOIEN,.LINE)
 .; if the status is one of the failed status values, display the error details.
 .I $$GET1^DIQ(52.49,PSOIEN,1,"E")="RXF" D PROCERR^PSOERXU3(PSOIEN,.LINE)
 I MTYPE="IE" D  S VALMCNT=LINE Q
 .S RELIEN=$$RESOLV^PSOERXU2(PSOIEN)
 .S RELMTYPE=$$GET1^DIQ(52.49,RELIEN,.08,"I")
 .I RELMTYPE="RE" D ERRDISP^PSOERXU3(PSOIEN,.LINE),RRREQ^PSOERXU3(PSOIEN,.LINE),MSGHIS^PSOERXU3(PSOIEN,.LINE) Q
 .I RELMTYPE="CA" D ERRDISP^PSOERXU3(PSOIEN,.LINE),CANREQ^PSOERXU5(PSOIEN,.LINE),CANRES^PSOERXU5(PSOIEN,.LINE),MSGHIS^PSOERXU3(PSOIEN,.LINE) Q
 .I RELMTYPE="CN" D ERRDISP^PSOERXU3(PSOIEN,.LINE),CANRES^PSOERXU5(PSOIEN,.LINE),CANREQ^PSOERXU5(PSOIEN,.LINE) Q
 .D ERRDISP^PSOERXU3(PSOIEN,.LINE),MSGHIS^PSOERXU3(PSOIEN,.LINE)
 I MTYPE="CA" D  S VALMCNT=LINE Q
 .D CANREQ^PSOERXU5(PSOIEN,.LINE),CANRES^PSOERXU5(PSOIEN,.LINE),MSGHIS^PSOERXU3(PSOIEN,.LINE)
 I MTYPE="CN" D  S VALMCNT=LINE Q
 .I $$GET1^DIQ(52.49,PSOIEN,1,"E")="CNE" D  Q
 ..S ERRIEN=$$GETRESP^PSOERXU2(PSOIEN)
 ..D ERRDISP^PSOERXU3(ERRIEN,.LINE),CANRES^PSOERXU5(PSOIEN,.LINE),CANREQ^PSOERXU5(PSOIEN,.LINE),MSGHIS^PSOERXU3(PSOIEN,.LINE) Q
 .D CANRES^PSOERXU5(PSOIEN,.LINE),CANREQ^PSOERXU5(PSOIEN,.LINE),MSGHIS^PSOERXU3(PSOIEN,.LINE)
 I "RR,RE,CA,CN,IE"'[MTYPE!(MTYPE="N") D
 .S LINE=LINE+1 D SET^VALM10(LINE,"")
 .S LINE=LINE+1 D SET^VALM10(LINE,"Vista Drug"_$S(DRMANVAL:"[v]",1:"")_": "_VADRG)
 .S LINE=LINE+1
 .D ADDITEM^PSOERX1A(.LINETXT,"Vista Qty: ",$G(VAQTY),1,25)
 .D ADDITEM^PSOERX1A(.LINETXT,"Vista Refills: ",$G(VAREF),27,18)
 .D ADDITEM^PSOERX1A(.LINETXT,"Vista Days Supply: ",$G(VADAYS),54,22)
 .D SET^VALM10(LINE,LINETXT) S LINETXT=""
 .S VASIG=""
 .S SLOOP=0 F  S SLOOP=$O(^PS(52.49,PSOIEN,"SIG",SLOOP)) Q:'SLOOP  D
 ..I '$L($G(VASIG)) S VASIG=$G(^PS(52.49,PSOIEN,"SIG",SLOOP,0)) Q
 ..S VASIG=$G(VASIG)_" "_$G(^PS(52.49,PSOIEN,"SIG",SLOOP,0))
 .D TXT2ARY^PSOERXD1(.VASARY,VASIG,,68)
 .S FSSIG=$O(VASARY(0))
 .;/BLB/ PSO*7.0*551 - BEGIN CHANGE - CHANGED TEXT FROM "DO NOT SUB" TO "SUBSTITUTIONS? :"
 .S ERXDSUB=$$GET1^DIQ(52.49,PSOIEN,5.8,"I")
 .S ERXDSUB=$S(ERXDSUB=1:"NO",ERXDSUB=0:"YES",1:"")
 .S LINE=LINE+1 D SET^VALM10(LINE,"Substitutions? :"_ERXDSUB)
 .;/BLB/ PSO*7.0*551 - END CHANGE
 .S LINE=LINE+1 D SET^VALM10(LINE,"Vista Sig: "_$S(FSSIG:$G(VASARY(FSSIG)),1:""))
 .S SLOOP=1 F  S SLOOP=$O(VASARY(SLOOP)) Q:'SLOOP  D
 ..S LINE=LINE+1 D SET^VALM10(LINE,"                        "_VASARY(SLOOP))
 .S VPATINST=$$GET1^DIQ(52.49,PSOIEN,27,"E")
 .I VPATINST]"" S VPATINST=$$LSIG^PSOQUTIL(VPATINST)
 .D TXT2ARY^PSOERXD1(.VAPIARY,VPATINST,68)
 .S FSVPIN=$O(VAPIARY(0))
 .S LINE=LINE+1 D SET^VALM10(LINE," Pat Inst: "_$S(FSVPIN:$G(VAPIARY(FSVPIN)),1:""))
 .S VLOOP=1 F  S VLOOP=$O(VAPIARY(VLOOP)) Q:'VLOOP  D
 ..S LINE=LINE+1 D SET^VALM10(LINE,"                        "_VAPIARY(VLOOP))
 S LINE=LINE+1 D SET^VALM10(LINE,"Hold Status: "_$G(VAHSTA))
 S VAHREA="Hold Reason: "_$G(VAHREA)
 D TXT2ARY^PSOERXD1(.HARY,VAHREA," ",80)
 S HL=0 F  S HL=$O(HARY(HL)) Q:'HL  D
 .S LINE=LINE+1 D SET^VALM10(LINE,$G(HARY(HL)))
 S LINE=LINE+1 D SET^VALM10(LINE,"Placed on hold by: "_$G(VAHPER))
 S LINE=LINE+1 D SET^VALM10(LINE,"")
 S ERXCOMM="eRx Notes: "_$$GET1^DIQ(52.49,PSOIEN,8,"E")
 D TXT2ARY^PSOERXD1(.COMARY,ERXCOMM," ",68)
 ;/BLB/ PSO*7.0*551 - BEGIN CHANGE - CORRECTING DUPLICATE WORD ISSUE ON ERX NOTES
 S COM=0 F  S COM=$O(COMARY(COM)) Q:'COM  S LINE=LINE+1 D SET^VALM10(LINE,$G(COMARY(COM)))
 ;/BLB/ PSO*7.0*551 - END CHANGE
 S LINE=LINE+1 D SET^VALM10(LINE,"")
 ;/BLB/ PSO*7.0*520 - BEGIN
 I $$GET1^DIQ(52.49,PSOIEN,.05,"I") D
 .D ALG^PSOERXU1(.LINE)
 ;/BLB/ - END
 D DIAG^PSOERXU1(PSOIEN,.LINE)
 S VALMCNT=LINE
 Q
 ;
HELP ; -- help code
 S X="?" D DISP^XQORM1 W !!
 Q
 ;
EXIT ; -- exit code
 K @VALMAR
 ; PSO*7*527 - set VALMBCK and PSOREFSH to force refresh when returning to list view
 S VALMBCK="R",PSOREFSH=1
 Q
 ;
EXPND ; -- expand code
 Q
