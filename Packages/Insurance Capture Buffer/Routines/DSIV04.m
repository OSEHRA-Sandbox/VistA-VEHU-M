DSIV04 ;DSS/KC - PRE INSTALL P4 ;12/10/2010
 ;;2.2;INSURANCE CAPTURE BUFFER;**4**;May 19, 2009;Build 7
 ;Copyright 1995-2010, Document Storage Systems, Inc., All Rights Reserved
 ;
 ; Integration Control Registations
 ; 5323   MES^DSICXPDU
 ; 5322   ADD^DSICXPR
 ;
PRE ;pre-install
 ;Add the new Insured params
 N RET,J,SEXP,DOBP,SSNP,NOIP,SUBP
 D MES^DSICXPDU("Editing DSIV REQUIRED EDIT FIELDS param to add Insured fields")
 D GET^DSICXPR(.RET,"SYS~DSIV REQUIRED EDIT FIELDS~~~~")
 S J=0,(SEXP,DOBP,SSNP,SUBP)="0"
 F  S J=$O(RET(J)) Q:'J  D
 .I $P(RET(J),U,1)="CBOSEX" S SEXP=$E(RET(J),$L(RET(J)))
 .I $P(RET(J),U,1)="EDTINSDOB" S DOBP=$E(RET(J),$L(RET(J)))
 .I $P(RET(J),U,1)="EDTINSSSN" S SSNP=$E(RET(J),$L(RET(J)))
 .I $P(RET(J),U,1)="EDTNAMEOFINS" S NOIP=$E(RET(J),$L(RET(J)))
 .I $P(RET(J),U,1)="EDTSUBSCRID" S SUBP=$E(RET(J),$L(RET(J)))
 .Q
 D CHG^DSICXPR(,"SYS~DSIV REQUIRED EDIT FIELDS~CBOINSSEX~POLICY/SUBSCRIBER$$PGINSUREDSUBSCRIBER=0|INSURED'S SEX|"_SEXP)
 D CHG^DSICXPR(,"SYS~DSIV REQUIRED EDIT FIELDS~EDTINSDOB~POLICY/SUBSCRIBER$$PGINSUREDSUBSCRIBER=0|INSURED'S DOB|"_DOBP)
 D CHG^DSICXPR(,"SYS~DSIV REQUIRED EDIT FIELDS~EDTINSSSN~POLICY/SUBSCRIBER$$PGINSUREDSUBSCRIBER=0|INSURED'S SSN|"_SSNP)
 D CHG^DSICXPR(,"SYS~DSIV REQUIRED EDIT FIELDS~EDTNAMEOFINS~POLICY/SUBSCRIBER$$PGINSUREDSUBSCRIBER=0|NAME OF INSURED|"_NOIP)
 D CHG^DSICXPR(,"SYS~DSIV REQUIRED EDIT FIELDS~EDTSUBSCRID~POLICY/SUBSCRIBER$$PGINSUREDSUBSCRIBER=0|SUBSCRIBER ID|"_SUBP)
 D DEL^DSICXPR(,"SYS~DSIV REQUIRED EDIT FIELDS~EDTSUBSCRADDR1~~~~")
 D DEL^DSICXPR(,"SYS~DSIV REQUIRED EDIT FIELDS~EDTSUBSCRADDR2~~~~")
 D DEL^DSICXPR(,"SYS~DSIV REQUIRED EDIT FIELDS~EDTSUBSCRADDRCITY~~~~")
 D DEL^DSICXPR(,"SYS~DSIV REQUIRED EDIT FIELDS~CBOSUBSCRADDRSTATE~~~~")
 D DEL^DSICXPR(,"SYS~DSIV REQUIRED EDIT FIELDS~EDTSUBSCRADDRZIP~~~~")
 ;
 D MULT
 Q
 ;
POST ;set new param system default
 N RET S RET=$$GET1^DSICXPR(,"SYS~DSIV ICB ENABLE NO INS LIMIT~1~~~",1)
 I RET<0 D ADD^DSICXPR(,"SYS~DSIV ICB ENABLE NO INS LIMIT~1~1=65") ;will add if not there
 Q
MULT ;Initialize [multi-instance] parameters
 ; Tag MP has data in the following format:
 ; First line has PARAMETER^ENTITY
 ; Following lines have INSTANCE^VALUE, one per line
 ; End of parameter indicated by one blank line (;;)
 ; End of initialization indicated by two blank lines
 ; 
 N DSIVI,DSIVPARM,DSIVENT,DSIVINST,DSIVVAL,DSIVQUIT,DSIVX
 S (DSIVPARM,DSIVQUIT)=""
 F DSIVI=1:1 S DSIVX=$P($T(@("MP+"_DSIVI)),";",3,99) D  Q:DSIVQUIT
 .I DSIVPARM="",DSIVX="" S DSIVQUIT=1 Q  ;All parameters done
 .I DSIVPARM=""!(DSIVX="") D  Q
 ..S DSIVPARM=$P(DSIVX,U),DSIVENT=$P(DSIVX,U,2)
 ..S:DSIVENT="" DSIVENT="SYS" ;Default entity
 ..Q
 .S DSIVINST=$P(DSIVX,U),DSIVVAL=$P(DSIVX,U,2) ;Instance value pair
 .D ADD^DSICXPR(,DSIVENT_"~"_DSIVPARM_"~"_DSIVINST_"~"_DSIVVAL) ;will add if not there
 .D CHG^DSICXPR(,DSIVENT_"~"_DSIVPARM_"~"_DSIVINST_"~"_DSIVVAL) ;will edit if exists
 .Q
 Q 
MP ;;
 ;;DSIV REQUIRED EDIT FIELDS
 ;;CBOINSUREDBRANCH^POLICY/SUBSCRIBER$$PGINSUREDSUBSCRIBER=0|INSURED'S BRANCH|0
 ;;CBOINSUREDRANK^POLICY/SUBSCRIBER$$PGINSUREDSUBSCRIBER=0|INSURED'S RANK|0
 ;;CBOINSUREDSTATE^POLICY/SUBSCRIBER$$PGINSUREDSUBSCRIBER=1|INSURED'S STATE|0
 ;;EDTINSUREDSTREET1^POLICY/SUBSCRIBER$$PGINSUREDSUBSCRIBER=1|INSURED'S STREET 1|0
 ;;EDTINSUREDSTREET2^POLICY/SUBSCRIBER$$PGINSUREDSUBSCRIBER=1|INSURED'S STREET 2|0
 ;;EDTINSUREDCITY^POLICY/SUBSCRIBER$$PGINSUREDSUBSCRIBER=1|INSURED'S CITY|0
 ;;EDTINSUREDZIP^POLICY/SUBSCRIBER$$PGINSUREDSUBSCRIBER=1|INSURED'S ZIP|0
 ;;EDTINSUREDPHONE^POLICY/SUBSCRIBER$$PGINSUREDSUBSCRIBER=1|INSURED'S PHONE|0
 ;;
