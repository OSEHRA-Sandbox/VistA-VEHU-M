DSIV07 ;DSS/NR - POST INSTALL P7 ;05/02/2012
 ;;2.2;INSURANCE CAPTURE BUFFER;**7**;May 19, 2009;Build 1
 ;Copyright 1995-2012, Document Storage Systems, Inc., All Rights Reserved
 ;
 ; Integration Control Registations
 ; 5323   MES^DSICXPDU
 ; 5322   ADD^DSICXPR
 ; 5322   CHG^DSICXPR
 ; 5322   ADDWP^DSICXPR
 ; 5322   CHGWP^DSICXPR
 ;
PRE  ;pre-install
 ;
POST ;post-install
 ;Change the words GROUP POLICY to GROUP PLAN in parameter DSIV REQUIRED EDIT FIELDS DSIV*2.2*7 Sprint 2 NCR 05.02.2012
 ;Change the words INSURANCE COMPANY to INSURANCE COMPANY$$PGINSCOINFOBILLPARAMS=0| in DSIV REQUIRED EDIT FIELDS DSIV*2.2*7 Sprint 4 NCR 5/21/2012
 N RET,J,DSIINS,DSIARR,CNT,DSIVENT,DSIVPARM,DSIVINST,DSIVVAL,DSIVI
 S (DSIARR,DSIVENT,DSIVPARM,DSIVINST,DSIVVAL)="",(J,CNT,DSIVI)=0
 D MES^DSICXPDU("Editing of parameter DSIV REQUIRED EDIT FIELDS has begun.")
 D GET^DSICXPR(.RET,"SYS~DSIV REQUIRED EDIT FIELDS~~~~")
 S DSIVENT="SYS",DSIVPARM="DSIV REQUIRED EDIT FIELDS"
 F  S J=$O(RET(J)) Q:'J  D
 .I +$G(RET(1))=-1 Q  ;parameter doesn't exist
 .S DSIARR(J)=""
 .S DSIINS=$P(RET(J),"|")
 .S DSIARR(J)=$P(RET(J),"|",1,2)
 .I $P(DSIINS,U,3)="INSURANCE COMPANY" S $P(DSIARR(J),U,3)="INSURANCE COMPANY$$PGINSCOINFOBILLPARAMS=0|"_$P(DSIARR(J),"|",2)
 .I $P(DSIINS,U,3)="GROUP POLICY" S $P(DSIARR(J),U,3)="GROUP PLAN|"_$P(DSIARR(J),"|",2)
 .I $P(DSIINS,U)="EDTGROUPPOLCOMMENTS" S $P(DSIARR(J),"|",2)="GROUP PLAN COMMENTS"
 .S DSIARR(J)=DSIARR(J)_"|"_+$P(RET(J),"|",3)
 .S DSIVINST=$P(DSIARR(J),U)
 .S DSIVVAL=$P(DSIARR(J),U,3)
 .D CHG^DSICXPR(,DSIVENT_"~"_DSIVPARM_"~"_DSIVINST_"~"_DSIVVAL) ;will edit if exists
 .S CNT=CNT+1
 .Q
 I CNT=0 S DSIVI=1 D INITPARM(DSIVI)  ;First time creation of parameter DSIV REQUIRED EDIT FIELDS
 I CNT'=0 S DSIVI=63 D INITPARM(DSIVI)  ;Revision of parameter DSIV REQUIRED EDIT FIELDS for insurance company billing -
 ;                                      ;creates seven new instance/value pairs of this parameter beginning with the
 ;                                      ;data at line MP+63
 ;
 D REJRSNS ; add parameter DSIV ICB REJECT REASON
 ;
 D MES^DSICXPDU("Changes to parameter DSIV REQUIRED EDIT FIELDS are complete.")
 Q
 ;
INITPARM(DSIVI) ;Initialize parameter DSIV REQUIRED EDIT FIELDS
 ; Tag MP has data in the following format:
 ; First line has PARAMETER^ENTITY
 ; Following lines have INSTANCE^VALUE, one per line
 ; End of parameter indicated by two blank lines (;;)
 ; 
 ; This routine is used in two ways:
 ;   1) DSIVI=1 =>  First time creation of parameter DSIV REQUIRED EDIT FIELDS
 ;   2) DSIVI=63 => Add seven new instance/value pairs of DSIV REQUIRED EDIT FIELDS for insurance billing
 ;                  starting with line MP+63      DSIV*2.2*7 Sprint 4 NCR 5.21.2012
 ;
 N NEWPARAM S NEWPARAM=0
 I DSIVI=1 S NEWPARAM=1 D MES^DSICXPDU("    Parameter DSIV REQUIRED EDIT FIELDS is being created.")
 N DSIVPARM,DSIVENT,DSIVINST,DSIVVAL,DSIVQUIT,DSIVX,CNT,RET
 S (DSIVPARM,DSIVQUIT,RET)="",CNT=0
 S DSIVPARM="DSIV REQUIRED EDIT FIELDS",DSIVENT="SYS"
 F DSIVI=$G(DSIVI):1 S DSIVX=$P($T(@("MP+"_DSIVI)),";",3,99) D  Q:DSIVQUIT
 .I DSIVX="" S DSIVQUIT=1 Q  ;All parameters done
 .S DSIVINST=$P(DSIVX,U),DSIVVAL=$P(DSIVX,U,2) ;Instance value pair
 .N RET S RET=""
 .D ADD^DSICXPR(.RET,DSIVENT_"~"_DSIVPARM_"~"_DSIVINST_"~"_DSIVVAL) ;will add if not there
 .S CNT=CNT+1
 .Q
 I CNT>0,NEWPARAM=1 D MES^DSICXPDU("    Creation of parameter DSIV REQUIRED EDIT FIELDS is complete.")
 Q 
 ;
REJRSNS ;post-install routine   DSIV*2.2*7 S3 NCR 5.14.2012
 ;Initialize parameter DSIV ICB REJECT REASON
 ;  
 D MES^DSICXPDU("        Parameter DSIV ICB REJECT REASON is required.")
 N DSIVI,DSIVQUIT,DSIVX,CNT,IVMRSN,CUSTRSN,IVMIEN,DSIVARR,DSIVARRC,DSIVRSNS
 S (DSIVQUIT,IVMRSN,CUSTRSN,IVMIEN,DSIVARR,DSIVARRC,DSIVRSNS)="",CNT=0
 S DSIVRSNS(1)="ALREADY HAVE INSURANCE POLICY^POLICY NOT DUE FOR REVERIFICATION"
 S DSIVRSNS(2)="BENEFITS NOT AVAILABLE^INVALID HMS UPLOAD"
 S DSIVRSNS(3)="DATA APPEARS TO BE INCORRECT^WRONG PATIENT IDENTIFIED"
 S DSIVRSNS(4)="BENEFITS NOT AVAILABLE^INSUFFICIENT INFORMATION"
 S DSIVRSNS(5)="DATA APPEARS TO BE INCORRECT^INVALID REMOTE INSURANCE QUERY"
 S DSIVRSNS(6)="POLICY COVERAGE EXPIRED^POLICY NEVER ACTIVE/IN FORCE"
 S DSIVRSNS(7)="ALREADY HAVE INSURANCE POLICY^DUPLICATE BUFFER ENTRY"
 S DSIVRSNS(8)="DATA APPEARS TO BE INCORRECT^IMAGE SCANNED IS NOT INSURANCE CARD"
 F DSIVI=1:1 S DSIVX=$G(DSIVRSNS(DSIVI)) D  Q:DSIVQUIT
 .I $G(DSIVX)="" S DSIVQUIT=1 Q  ;All parameters done
 .S IVMRSN=$P(DSIVX,"^"),CUSTRSN=$P(DSIVX,"^",2)
 .S IVMIEN="",IVMIEN=$O(^IVM(301.91,"C",IVMRSN,IVMIEN)) ;find IEN to which this reason is mapped
 .S DSIVARR(DSIVI)=IVMIEN_"="_CUSTRSN
 .S CNT=CNT+1
 S DSIVARRC=DSIVARR
 D ADDWP^DSICXPR(.RET,"SYS~DSIV ICB REJECT REASON~1",.DSIVARR)
 D CHGWP^DSICXPR(.RET,"SYS~DSIV ICB REJECT REASON~1",.DSIVARRC)
 I CNT>0 D MES^DSICXPDU("        Parameter DSIV ICB REJECT REASON processed successfully.")
 Q
 ; 
MP ;;  Last seven entries in this list are for insurance company billing parameters
 ;;DSIV REQUIRED EDIT FIELDS
 ;;CBOINSUREDBRANCH^POLICY/SUBSCRIBER$$PGINSUREDSUBSCRIBER=0|INSURED'S BRANCH|0
 ;;CBOINSUREDRANK^POLICY/SUBSCRIBER$$PGINSUREDSUBSCRIBER=0|INSURED'S RANK|0
 ;;CBOINSUREDSTATE^POLICY/SUBSCRIBER$$PGINSUREDSUBSCRIBER=1|INSURED'S STATE|0
 ;;CBOAMBCARECERT^GROUP PLAN|AMBULATORY CARE CERTIFICATION|0
 ;;CBOBENASSIGN^GROUP PLAN|BENEFITS ASSIGNABLE|0
 ;;CBOCOORDBEN^POLICY/SUBSCRIBER|COORDINATION OF BENEFITS|0
 ;;CBOELECPLANTYPE^GROUP PLAN|ELECTRONIC PLAN TYPE|0
 ;;CBOEMPSTATE^PROVIDER/EMPLOYER|EMPLOYER CLAIMS STATE|0
 ;;CBOEMPSTATUS^PROVIDER/EMPLOYER|EMPLOYMENT STATUS|0
 ;;CBOESGHP^POLICY/SUBSCRIBER|ESGHP|0
 ;;CBOEXCPREEXISTCOND^GROUP PLAN|EXCLUDE PRE-EXISTING CONDITION|0
 ;;CBOINSSEX^POLICY/SUBSCRIBER$$PGINSUREDSUBSCRIBER=0|INSURED'S SEX|0
 ;;CBOPLANSTDFTF^GROUP PLAN|PLAN STANDARD FTF|0
 ;;CBOPRECERTREQ^GROUP PLAN|PRE-CERTIFICATION REQUIRED|0
 ;;CBOPTRELTOINS^POLICY/SUBSCRIBER|PT. RELATIONSHIP TO INSURED|0
 ;;CBOREIMBURSE^INSURANCE COMPANY$$PGINSCOINFOBILLPARAMS=0|REIMBURSE|1
 ;;CBOSENDBILLTOEMP^PROVIDER/EMPLOYER|SEND BILL TO EMPLOYER|0
 ;;CBOSOURCE^INSURANCE COMPANY$$PGINSCOINFOBILLPARAMS=0|SOURCE|1
 ;;CBOSTATE^INSURANCE COMPANY$$PGINSCOINFOBILLPARAMS=0|STATE|0
 ;;CBOTYPEOFPLAN^GROUP PLAN|TYPE OF PLAN|0
 ;;CBOUTILREVREQ^GROUP PLAN|UTILIZATION REVIEW REQUIRED|0
 ;;CBOWHOSEINS^POLICY/SUBSCRIBER|WHOSE INSURANCE|0
 ;;CBOPOLNOTBILL^POLICY/SUBSCRIBER|POLICY NOT BILLABLE|0
 ;;EDTBANKIDNO^GROUP PLAN|BANKING ID NUMBER|0
 ;;EDTBILLPHONENO^INSURANCE COMPANY$$PGINSCOINFOBILLPARAMS=0|BILLING PHONE NUMBER|0
 ;;EDTCITY^INSURANCE COMPANY$$PGINSCOINFOBILLPARAMS=0|CITY|0
 ;;EDTEFFDATE^POLICY/SUBSCRIBER|EFFECTIVE DATE|0
 ;;EDTEMPCITY^PROVIDER/EMPLOYER|EMPLOYER CLAIMS CITY|0
 ;;EDTEMPSTREET1^PROVIDER/EMPLOYER|EMPLOYER CLAIMS STREET [1]|0
 ;;EDTEMPSTREET2^PROVIDER/EMPLOYER|EMPLOYER CLAIMS STREET [2]|0
 ;;EDTEMPSTREET3^PROVIDER/EMPLOYER|EMPLOYER CLAIMS STREET [3]|0
 ;;EDTEMPZIP^PROVIDER/EMPLOYER|EMPLOYER CLAIMS ZIP|0
 ;;EDTEMPPHONENO^PROVIDER/EMPLOYER|EMPLOYER CLAIMS PHONE|0
 ;;EDTEXPDATE^POLICY/SUBSCRIBER|EXPIRATION DATE|0
 ;;EDTGROUPNAME^GROUP PLAN|GROUP NAME|0
 ;;EDTGROUPNO^GROUP PLAN|GROUP NUMBER|0
 ;;EDTGROUPPOLCOMMENTS^GROUP PLAN|GROUP PLAN COMMENTS|0
 ;;EDTINSCONAME^INSURANCE COMPANY$$PGINSCOINFOBILLPARAMS=0|INSURANCE COMPANY|1
 ;;EDTINSDOB^POLICY/SUBSCRIBER$$PGINSUREDSUBSCRIBER=0|INSURED'S DOB|0
 ;;EDTINSSSN^POLICY/SUBSCRIBER$$PGINSUREDSUBSCRIBER=0|INSURED'S SSN|0
 ;;EDTINSUREDSTREET1^POLICY/SUBSCRIBER$$PGINSUREDSUBSCRIBER=1|INSURED'S STREET 1|0
 ;;EDTINSUREDSTREET2^POLICY/SUBSCRIBER$$PGINSUREDSUBSCRIBER=1|INSURED'S STREET 2|0
 ;;EDTINSUREDCITY^POLICY/SUBSCRIBER$$PGINSUREDSUBSCRIBER=1|INSURED'S CITY|0
 ;;EDTINSUREDZIP^POLICY/SUBSCRIBER$$PGINSUREDSUBSCRIBER=1|INSURED'S ZIP|0
 ;;EDTINSUREDPHONE^POLICY/SUBSCRIBER$$PGINSUREDSUBSCRIBER=1|INSURED'S PHONE|0
 ;;EDTNAMEOFINS^POLICY/SUBSCRIBER|NAME OF INSURED|0
 ;;EDTPATIENTID^POLICY/SUBSCRIBER|PATIENT ID|0
 ;;EDTPATPOLCOMMENTS^POLICY/SUBSCRIBER|PATIENT POLICY COMMENTS|0
 ;;EDTPCN^GROUP PLAN|PROCESS CONTROL NO. (PCN)|0
 ;;EDTPCPROV^PROVIDER/EMPLOYER|PRIMARY CARE PROVIDER|0
 ;;EDTPCPROVPHONENO^PROVIDER/EMPLOYER|PROVIDER PHONE NUMBER|0
 ;;EDTPHONENO^INSURANCE COMPANY$$PGINSCOINFOBILLPARAMS=0|PHONE NUMBER|0
 ;;EDTPLANFILETIMEFRAME^GROUP PLAN|PLAN FILE TIME FRAME|0
 ;;EDTPLANSTDFTFVALUE^GROUP PLAN|PLAN STANDARD FTF VALUE|0
 ;;EDTPRECERTPHONENO^INSURANCE COMPANY$$PGINSCOINFOBILLPARAMS=0|PRE-CERT PHONE NUMBER|0
 ;;EDTRETDATE^PROVIDER/EMPLOYER|RETIREMENT DATE|0
 ;;EDTSPEMPNAME^PROVIDER/EMPLOYER|SPONSORING EMPLOYER NAME|0
 ;;EDTSTREET1^INSURANCE COMPANY$$PGINSCOINFOBILLPARAMS=0|STREET ADDRESS [1]|0
 ;;EDTSTREET2^INSURANCE COMPANY$$PGINSCOINFOBILLPARAMS=0|STREET ADDRESS [2]|0
 ;;EDTSTREET3^INSURANCE COMPANY$$PGINSCOINFOBILLPARAMS=0|STREET ADDRESS [3]|0
 ;;EDTSUBSCRID^POLICY/SUBSCRIBER$$PGINSUREDSUBSCRIBER=0|SUBSCRIBER ID|0
 ;;EDTZIP^INSURANCE COMPANY$$PGINSCOINFOBILLPARAMS=0|ZIP CODE|0
 ;;CBOINSCOSIGNATUREREQUIRED^INSURANCE COMPANY$$PGINSCOINFOBILLPARAMS=1|SIGNATURE REQUIRED?|0
 ;;CBOINSCOSTANDARDFTF^INSURANCE COMPANY$$PGINSCOINFOBILLPARAMS=1|STANDARD FTF|0
 ;;EDTINSCOSTANDARDFTFVALUE^INSURANCE COMPANY$$PGINSCOINFOBILLPARAMS=1|STANDARD FTF VALUE|0
 ;;EDTINSCOFILINGTIMEFRAME^INSURANCE COMPANY$$PGINSCOINFOBILLPARAMS=1|FILING TIME FRAME|0
 ;;CBOINSCOTYPEOFCOVERAGE^INSURANCE COMPANY$$PGINSCOINFOBILLPARAMS=1|TYPE OF COVERAGE|0
 ;;CBOINSCOTRANSMIT^INSURANCE COMPANY$$PGINSCOINFOBILLPARAMS=1|TRANSMIT?|0
 ;;CBOINSCOINSURANCETYPE^INSURANCE COMPANY$$PGINSCOINFOBILLPARAMS=1|INSURANCE TYPE|0
 ;;EDTINSCOFAXNUMBER^INSURANCE COMPANY$$PGINSCOINFOBILLPARAMS=0|FAX NUMBER|0
 ;;MEMOINSCOREMARKS^INSURANCE COMPANY$$PGINSCOINFOBILLPARAMS=1|REMARKS|0
 ;;MEMOINSCOSYNONYM^INSURANCE COMPANY$$PGINSCOINFOBILLPARAMS=1|SYNONYM|0
 ;;
 ;;
